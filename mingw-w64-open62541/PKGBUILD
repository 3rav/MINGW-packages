# Maintainer: open62541 Team <open62541-core@googlegroups.com>
branch=1.2
name=open62541
pkgname="${MINGW_PACKAGE_PREFIX}-${name}"
pkgver=v1.2.2_r6_g200579cbe
pkgrel=1
pkgdesc="An open source and free implementation of OPC Unified Architecture written in the common subset of the C99 and C++98 languages."
arch=('any')
url="http://open62541.org/"
license=('MPL2')
makedepends=('git'
                "${MINGW_PACKAGE_PREFIX}-cmake"
                "${MINGW_PACKAGE_PREFIX}-ninja")
source=("git+https://github.com/open62541/open62541.git#branch=$branch")
md5sums=('SKIP')
sha256sums=('SKIP')

prepare() {
    OPEN62541_CMAKE_FLAGS_DEFAULT=\
"-DBUILD_SHARED_LIBS=ON"\
" -DUA_NAMESPACE_ZERO=FULL"\
" -DUA_ENABLE_AMALGAMATION=OFF"\
" -DCMAKE_BUILD_TYPE=RelWithDebInfo"

    # OPEN62541_CMAKE_FLAGS is an environment variable which can be
    # set in order to specify custom compilation flags for open62541.
    # This allows integrating further features. Please review the wiki
    # for more information.

    OPEN62541_CMAKE_FLAGS="$OPEN62541_CMAKE_FLAGS_DEFAULT $OPEN62541_CMAKE_FLAGS"

    cd "$srcdir/$name"
    git submodule init
    git config submodule.UA-Nodeset.url "$srcdir/UA-Nodeset"
    git config submodule.mdnsd "$srcdir/mdnsd"
    git submodule update
}

pkgver() {
     cd "$srcdir/$name"
     git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/_/g'
}

build() {
    cd "$srcdir/$name"

    mkdir -p build
    cd build

    cmake $OPEN62541_CMAKE_FLAGS ..
    cmake --build .
}

package() {
    cd "$srcdir/$name/build"

    cmake --install .
    install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$name/LICENSE"
}
