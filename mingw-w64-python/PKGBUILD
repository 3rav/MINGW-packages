# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Maintainer: Ray Donnelly <mingw.android@gmail.com>
# Contributor: Saul Ibarra Corretge <saghul@gmail.com>
# Contributor: Frode Solheim <frode@fs-uae.net>

_primary_python="yes"
_pybasever=3.10
_realname=python

if [[ "${_primary_python}" == "yes" ]]; then
  pkgbase="mingw-w64-${_realname}"
  pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
  provides=("${MINGW_PACKAGE_PREFIX}-python3"
            "${MINGW_PACKAGE_PREFIX}-python${_pybasever}")
  conflicts=("${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python${_pybasever}"
            "${MINGW_PACKAGE_PREFIX}-python2<2.7.16-7")
  replaces=("${MINGW_PACKAGE_PREFIX}-python3"
            "${MINGW_PACKAGE_PREFIX}-python${_pybasever}")
else
  pkgbase="mingw-w64-${_realname}${_pybasever}"
  pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}${_pybasever}")
fi
pkgver=${_pybasever}.11
pkgrel=1
pkgdesc="A high-level scripting language (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
license=('PSF')
url="https://www.python.org/"
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-bzip2"
         "${MINGW_PACKAGE_PREFIX}-libffi"
         "${MINGW_PACKAGE_PREFIX}-mpdecimal"
         "${MINGW_PACKAGE_PREFIX}-ncurses"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-sqlite3"
         "${MINGW_PACKAGE_PREFIX}-tcl"
         "${MINGW_PACKAGE_PREFIX}-tk"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-xz"
         "${MINGW_PACKAGE_PREFIX}-tzdata")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-autotools"
  "autoconf-archive"
)
options=('makeflags') # 'debug' '!strip')
source=("https://www.python.org/ftp/python/${pkgver%rc?}/Python-${pkgver}.tar.xz"
        0001-sysconfig-make-_sysconfigdata.py-relocatable.patch
        0002-restore-setup-config.patch
        0003-build-add-with-nt-threads-and-make-it-default-on-min.patch
        0004-MINGW-translate-gcc-internal-defines-to-python-platf.patch
        0005-MINGW-configure-MACHDEP-and-platform-for-build.patch
        0006-MINGW-preset-configure-defaults.patch
        0007-MINGW-configure-largefile-support-for-windows-builds.patch
        0008-MINGW-add-srcdir-PC-to-CPPFLAGS.patch
        0009-MINGW-init-system-calls.patch
        0010-MINGW-build-in-windows-modules-winreg.patch
        0011-MINGW-determine-if-pwdmodule-should-be-used.patch
        0012-issue6672-v2-Add-Mingw-recognition-to-pyport.h-to-al.patch
        0013-MINGW-configure-for-shared-build.patch
        0014-MINGW-dynamic-loading-support.patch
        0015-MINGW-ignore-main-program-for-frozen-scripts.patch
        0016-MINGW-setup-_multiprocessing-module.patch
        0017-MINGW-setup-select-module.patch
        0018-MINGW-setup-_ctypes-module-with-system-libffi.patch
        0019-MINGW-defect-winsock2-and-setup-_socket-module.patch
        0020-MINGW-exclude-unix-only-modules.patch
        0021-MINGW-setup-msvcrt-and-_winapi-modules.patch
        0022-sysconfig-MINGW-build-extensions-with-GCC.patch
        0023-MINGW-setup-_ssl-module.patch
        0024-MINGW-generalization-of-posix-build-in-sysconfig.py.patch
        0025-MINGW-support-stdcall-without-underscore.patch
        0026-use-replace-instead-rename-to-avoid-failure-on-windo.patch
        0027-MINGW-customize-site.patch
        0028-add-python-config-sh.patch
        0029-cross-darwin-feature.patch
        0030-mingw-system-libffi.patch
        0031-msys-mingw-prefer-unix-sep-if-MSYSTEM.patch
        0032-mingw-use-posix-getpath.patch
        0033-mingw-add-ModuleFileName-dir-to-PATH.patch
        0034-mingw-add-BUILDIN_WIN32_MODULEs-time-msvcrt.patch
        0035-msys-cygwin-semi-native-build-sysconfig.patch
        0036-sysconfig-mingw-sysconfig-like-posix.patch
        0037-build-_winapi-earlier-so-we-can-use-it-in-distutils-.patch
        0038-cross-dont-add-multiarch-paths-if-cross-compiling.patch
        0039-mingw-use-backslashes-in-compileall-py.patch
        0040-msys-convert_path-fix-and-root-hack.patch
        0041-allow-static-tcltk.patch
        0042-mingw-pdcurses_ISPAD.patch
        0043-builddir-fixes.patch
        0044-msys-monkeypatch-os-system-via-sh-exe.patch
        0045-msys-replace-slashes-used-in-io-redirection.patch
        0046-remove_path_max.default.patch
        0047-dont-link-with-gettext.patch
        0048-ctypes-python-dll.patch
        0049-gdbm-module-includes.patch
        0050-use-gnu_printf-in-format.patch
        0051-mingw-fix-ssl-dont-use-enum_certificates.patch
        0052-fix-using-dllhandle-and-winver-mingw.patch
        0053-Add-AMD64-to-sys-config-so-msvccompiler-get_build_ve.patch
        0054-MINGW-link-with-additional-library.patch
        0055-install-msilib.patch
        0056-fix-signal-module-build.patch
        0057-build-build-winconsoleio-and-_testconsole.patch
        0058-expose-sem_unlink.patch
        0059-cygpty-isatty.patch
        0060-disable-broken-gdbm-module.patch
        0061-build-link-win-resource-files-and-build-pythonw.patch
        0062-disable-readline.patch
        0063-fix-isselectable.patch
        0064-use-_wcsnicmp-instead-wcsncasecmp.patch
        0065-_xxsubinterpretersmodule.patch
        0066-sqlite3-module-defines.patch
        0067-configure.ac-fix-inet_pton-check.patch
        0068-set-venv-activate-path-unix.patch
        0069-venv-creation-fixes.patch
        0070-pass-gen-profile-ldflags.patch
        0071-pkg-config-windows-must-link-ext-with-python-lib.patch
        0072-importlib-bootstrap-path-sep.patch
        0073-pathlib-path-sep.patch
        0074-warnings-fixes.patch
        0075-fix-build-testinternalcapi.patch
        0076-extend-MS_WINDOWS-flag.patch
        0077-clang-arm64.patch
        0078-configure.ac-set-MINGW-stack-reserve.patch
        0079-Don-t-use-os.pathsep-to-find-EOF.patch
        0080-Fix-extension-suffix-for-c-extensions-on-mingw.patch
        0081-Change-the-get_platform-method-in-sysconfig.patch
        0082-distutils-compiler-customize-mingw-cygwin-compilers.patch
        0083-distutils-compiler-enable-new-dtags.patch
        0084-distutils-MINGW-build-extensions-with-GCC.patch
        0085-distutils-use-Mingw32CCompiler-as-default-compiler-f.patch
        0086-distutils-find-import-library.patch
        0087-distutils-avoid-circular-dependency-from-time-module.patch
        0088-distutils-generalization-of-posix-build-in-distutils.patch
        0089-distutils-mingw-sysconfig-like-posix.patch
        0090-distutils-get_versions-fixes.patch
        0091-distutils-install-layout-as-posix.patch
        0092-distutils-msys-convert_path-fix-and-root-hack.patch
        0093-distutils-mingw-build-optimized-ext.patch
        0094-distutils-cygwinccompiler-dont-strip-modules-if-pyde.patch
        0095-distutils-get-compilers-from-env-vars.patch
        0096-distutils-add-windmc-to-cygwinccompiler.patch
        0097-distutils-fix-msvc9-import.patch
        0098-distutils-mingw-add-LIBPL-to-library-dirs.patch
        0099-distutils-Change-the-get_platform-method-in-distutil.patch
        0100-build-Fix-ncursesw-include-lookup.patch
        0101-tests-fix-test_bytes.patch
        0102-time-fix-strftime-not-raising-for-invalid-year-value.patch
        0103-ctypes-find_library-c-should-return-None-with-ucrt.patch
        0104-build-Disable-checks-for-dlopen-dlfcn.patch
        0105-Fix-install-location-of-the-import-library.patch
        0106-Set-MSYS2_ARG_CONV_EXCL-for-the-shared-Python-module.patch
        0107-build-Integrate-venvlauncher-build-installation-into.patch
        0108-configure.ac-set-_WIN32_WINNT-version.patch
        0109-configure.ac-don-t-check-for-clock_-functions.patch
        0110-expanduser-normpath-paths-coming-from-env-vars.patch
        0111-Add-support-for-Windows-7-Python-3.10.patch
        0112-CI-test-the-build-and-add-some-mingw-specific-tests.patch
        0113-smoketests-test-that-_decimal-exists.patch
        0114-Prefer-sysconfig.python_build.patch
        0115-Define-PY3_DLLNAME-to-fix-build.patch
        0116-distutils-remove-checks-for-ancient-gcc-binutils.patch
        0117-distutils-split-CC-env-var-before-passing-to-subproc.patch
        0118-_testconsole.c-Fix-casing-path-sep.patch
        0119-Return-consistent-architecture-markers-for-python-on.patch
        0120-distutils-add-back-gcc_version.patch
        0121-fix-mingw-cross-compiling-in-setup.py.patch
        0122-Use-actions-setup-python-for-setting-up-correct-vers.patch
        0123-handle-ncursesw-pkg-config-when-cross-compiling.patch
        0124-CI-add-cross-llvm-mingw-jobs.patch
        0125-mingw_smoketests-fix-_UCRT-condition.patch
        0126-CI-fix-sed-pattern-for-python-config.py-shebang.patch
        0127-Modify-sys.winver-to-match-upstream.patch
        0128-Change-user-site-packages-path-to-include-the-enviro.patch
        0129-configure-Include-a-header-in-the-check-for-_beginth.patch
        0130-configure.ac-Default-to-without-c-locale-coercion-on.patch
        0131-CI-remove-without-c-locale-coercion.patch
        0132-Fix-failing-tests.patch
        0133-distutils-add-build-root-to-libdirs-when-building-un.patch
        0134-Avoid-some-macros-only-available-in-Win7.patch
        0135-Make-sure-we-always-use-the-stdlib-distutils.patch
        0136-CI-skip-some-more-flaky-tests.patch
        0137-Don-t-change-os.sep-with-an-empty-MSYSTEM-env-var-no.patch
        0138-Commit-regenerated-importlib.patch
        0139-CI-update-actions-and-images.patch)

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}/Python-${pkgver}"

 apply_patch_with_msg 0001-sysconfig-make-_sysconfigdata.py-relocatable.patch \
  0002-restore-setup-config.patch \
  0003-build-add-with-nt-threads-and-make-it-default-on-min.patch \
  0004-MINGW-translate-gcc-internal-defines-to-python-platf.patch \
  0005-MINGW-configure-MACHDEP-and-platform-for-build.patch \
  0006-MINGW-preset-configure-defaults.patch \
  0007-MINGW-configure-largefile-support-for-windows-builds.patch \
  0008-MINGW-add-srcdir-PC-to-CPPFLAGS.patch \
  0009-MINGW-init-system-calls.patch \
  0010-MINGW-build-in-windows-modules-winreg.patch \
  0011-MINGW-determine-if-pwdmodule-should-be-used.patch \
  0012-issue6672-v2-Add-Mingw-recognition-to-pyport.h-to-al.patch \
  0013-MINGW-configure-for-shared-build.patch \
  0014-MINGW-dynamic-loading-support.patch \
  0015-MINGW-ignore-main-program-for-frozen-scripts.patch \
  0016-MINGW-setup-_multiprocessing-module.patch \
  0017-MINGW-setup-select-module.patch \
  0018-MINGW-setup-_ctypes-module-with-system-libffi.patch \
  0019-MINGW-defect-winsock2-and-setup-_socket-module.patch \
  0020-MINGW-exclude-unix-only-modules.patch \
  0021-MINGW-setup-msvcrt-and-_winapi-modules.patch \
  0022-sysconfig-MINGW-build-extensions-with-GCC.patch \
  0023-MINGW-setup-_ssl-module.patch \
  0024-MINGW-generalization-of-posix-build-in-sysconfig.py.patch \
  0025-MINGW-support-stdcall-without-underscore.patch \
  0026-use-replace-instead-rename-to-avoid-failure-on-windo.patch \
  0027-MINGW-customize-site.patch \
  0028-add-python-config-sh.patch \
  0029-cross-darwin-feature.patch \
  0030-mingw-system-libffi.patch \
  0031-msys-mingw-prefer-unix-sep-if-MSYSTEM.patch \
  0032-mingw-use-posix-getpath.patch \
  0033-mingw-add-ModuleFileName-dir-to-PATH.patch \
  0034-mingw-add-BUILDIN_WIN32_MODULEs-time-msvcrt.patch \
  0035-msys-cygwin-semi-native-build-sysconfig.patch \
  0036-sysconfig-mingw-sysconfig-like-posix.patch \
  0037-build-_winapi-earlier-so-we-can-use-it-in-distutils-.patch \
  0038-cross-dont-add-multiarch-paths-if-cross-compiling.patch \
  0039-mingw-use-backslashes-in-compileall-py.patch \
  0040-msys-convert_path-fix-and-root-hack.patch \
  0041-allow-static-tcltk.patch \
  0042-mingw-pdcurses_ISPAD.patch \
  0043-builddir-fixes.patch \
  0044-msys-monkeypatch-os-system-via-sh-exe.patch \
  0045-msys-replace-slashes-used-in-io-redirection.patch \
  0046-remove_path_max.default.patch \
  0047-dont-link-with-gettext.patch \
  0048-ctypes-python-dll.patch \
  0049-gdbm-module-includes.patch \
  0050-use-gnu_printf-in-format.patch \
  0051-mingw-fix-ssl-dont-use-enum_certificates.patch \
  0052-fix-using-dllhandle-and-winver-mingw.patch \
  0053-Add-AMD64-to-sys-config-so-msvccompiler-get_build_ve.patch \
  0054-MINGW-link-with-additional-library.patch \
  0055-install-msilib.patch \
  0056-fix-signal-module-build.patch \
  0057-build-build-winconsoleio-and-_testconsole.patch \
  0058-expose-sem_unlink.patch \
  0059-cygpty-isatty.patch \
  0060-disable-broken-gdbm-module.patch \
  0061-build-link-win-resource-files-and-build-pythonw.patch \
  0062-disable-readline.patch \
  0063-fix-isselectable.patch \
  0064-use-_wcsnicmp-instead-wcsncasecmp.patch \
  0065-_xxsubinterpretersmodule.patch \
  0066-sqlite3-module-defines.patch \
  0067-configure.ac-fix-inet_pton-check.patch \
  0068-set-venv-activate-path-unix.patch \
  0069-venv-creation-fixes.patch \
  0070-pass-gen-profile-ldflags.patch \
  0071-pkg-config-windows-must-link-ext-with-python-lib.patch \
  0072-importlib-bootstrap-path-sep.patch \
  0073-pathlib-path-sep.patch \
  0074-warnings-fixes.patch \
  0075-fix-build-testinternalcapi.patch \
  0076-extend-MS_WINDOWS-flag.patch \
  0077-clang-arm64.patch \
  0078-configure.ac-set-MINGW-stack-reserve.patch \
  0079-Don-t-use-os.pathsep-to-find-EOF.patch \
  0080-Fix-extension-suffix-for-c-extensions-on-mingw.patch \
  0081-Change-the-get_platform-method-in-sysconfig.patch \
  0082-distutils-compiler-customize-mingw-cygwin-compilers.patch \
  0083-distutils-compiler-enable-new-dtags.patch \
  0084-distutils-MINGW-build-extensions-with-GCC.patch \
  0085-distutils-use-Mingw32CCompiler-as-default-compiler-f.patch \
  0086-distutils-find-import-library.patch \
  0087-distutils-avoid-circular-dependency-from-time-module.patch \
  0088-distutils-generalization-of-posix-build-in-distutils.patch \
  0089-distutils-mingw-sysconfig-like-posix.patch \
  0090-distutils-get_versions-fixes.patch \
  0091-distutils-install-layout-as-posix.patch \
  0092-distutils-msys-convert_path-fix-and-root-hack.patch \
  0093-distutils-mingw-build-optimized-ext.patch \
  0094-distutils-cygwinccompiler-dont-strip-modules-if-pyde.patch \
  0095-distutils-get-compilers-from-env-vars.patch \
  0096-distutils-add-windmc-to-cygwinccompiler.patch \
  0097-distutils-fix-msvc9-import.patch \
  0098-distutils-mingw-add-LIBPL-to-library-dirs.patch \
  0099-distutils-Change-the-get_platform-method-in-distutil.patch \
  0100-build-Fix-ncursesw-include-lookup.patch \
  0101-tests-fix-test_bytes.patch \
  0102-time-fix-strftime-not-raising-for-invalid-year-value.patch \
  0103-ctypes-find_library-c-should-return-None-with-ucrt.patch \
  0104-build-Disable-checks-for-dlopen-dlfcn.patch \
  0105-Fix-install-location-of-the-import-library.patch \
  0106-Set-MSYS2_ARG_CONV_EXCL-for-the-shared-Python-module.patch \
  0107-build-Integrate-venvlauncher-build-installation-into.patch \
  0108-configure.ac-set-_WIN32_WINNT-version.patch \
  0109-configure.ac-don-t-check-for-clock_-functions.patch \
  0110-expanduser-normpath-paths-coming-from-env-vars.patch \
  0111-Add-support-for-Windows-7-Python-3.10.patch \
  0112-CI-test-the-build-and-add-some-mingw-specific-tests.patch \
  0113-smoketests-test-that-_decimal-exists.patch \
  0114-Prefer-sysconfig.python_build.patch \
  0115-Define-PY3_DLLNAME-to-fix-build.patch \
  0116-distutils-remove-checks-for-ancient-gcc-binutils.patch \
  0117-distutils-split-CC-env-var-before-passing-to-subproc.patch \
  0118-_testconsole.c-Fix-casing-path-sep.patch \
  0119-Return-consistent-architecture-markers-for-python-on.patch \
  0120-distutils-add-back-gcc_version.patch \
  0121-fix-mingw-cross-compiling-in-setup.py.patch \
  0122-Use-actions-setup-python-for-setting-up-correct-vers.patch \
  0123-handle-ncursesw-pkg-config-when-cross-compiling.patch \
  0124-CI-add-cross-llvm-mingw-jobs.patch \
  0125-mingw_smoketests-fix-_UCRT-condition.patch \
  0126-CI-fix-sed-pattern-for-python-config.py-shebang.patch \
  0127-Modify-sys.winver-to-match-upstream.patch \
  0128-Change-user-site-packages-path-to-include-the-enviro.patch \
  0129-configure-Include-a-header-in-the-check-for-_beginth.patch \
  0130-configure.ac-Default-to-without-c-locale-coercion-on.patch \
  0131-CI-remove-without-c-locale-coercion.patch \
  0132-Fix-failing-tests.patch \
  0133-distutils-add-build-root-to-libdirs-when-building-un.patch \
  0134-Avoid-some-macros-only-available-in-Win7.patch \
  0135-Make-sure-we-always-use-the-stdlib-distutils.patch \
  0136-CI-skip-some-more-flaky-tests.patch \
  0137-Don-t-change-os.sep-with-an-empty-MSYSTEM-env-var-no.patch \
  0138-Commit-regenerated-importlib.patch \
  0139-CI-update-actions-and-images.patch
 
  autoreconf -vfi
}

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("--enable-optimizations")
    # Upstream defaults to -O3, so we can do too
    CFLAGS+=" -O3"
    # FIXME: https://github.com/msys2-contrib/cpython-mingw/issues/10
    # _extra_config+=("--with-lto")
  else
    CFLAGS+=" -O0 -ggdb"
    CXXFLAGS+=" -O0 -ggdb"
    _extra_config+=("--with-pydebug")
  fi

  case "${CARCH}" in
    i686)
      LDFLAGS+=" -Wl,--large-address-aware"
    ;;
  esac

  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  ../Python-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --host=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --enable-shared \
    --with-system-expat \
    --with-system-ffi \
    --with-system-libmpdec \
    --without-ensurepip \
    --enable-loadable-sqlite-extensions \
    --with-tzpath=${MINGW_PREFIX}/share/zoneinfo \
    "${_extra_config[@]}"

  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  # Some basic tests to ensure nothing major or MSYS2 specific features are broken
  ./python.exe "../Python-${pkgver}/mingw_smoketests.py"
  MSYSTEM= ./python.exe "../Python-${pkgver}/mingw_smoketests.py"
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make -j1 install DESTDIR="${pkgdir}"

  # gdb pretty printers for debugging Python itself; to use:
  # python
  # sys.path.append('C:/msys64/mingw64/share/gdb/python3')
  # import python_gdb
  # reload(python_gdb)
  # end
  if [[ "${_primary_python}" == "yes" ]]; then
    [[ -d "${pkgdir}${MINGW_PREFIX}"/share/gdb/python3/ ]] || mkdir -p "${pkgdir}${MINGW_PREFIX}"/share/gdb/python3/
    cp -f python.exe-gdb.py "${pkgdir}${MINGW_PREFIX}"/share/gdb/python3/python_gdb.py
  fi

  # some useful "stuff"
  install -dm755 "${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/Tools/{i18n,scripts}
  install -m755 "${srcdir}/Python-${pkgver}"/Tools/i18n/{msgfmt,pygettext}.py "${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/Tools/i18n/
  install -m755 "${srcdir}/Python-${pkgver}"/Tools/scripts/{README,*py} "${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/Tools/scripts/

  # fixup shebangs
  for fscripts in 2to3 2to3-${_pybasever} idle3 idle${_pybasever} pydoc3 pydoc${_pybasever}; do
    sed -i "s|$(cygpath -w ${MINGW_PREFIX} | sed 's|\\|\\\\|g')/bin/python${_pybasever}.exe|/usr/bin/env python${_pybasever}.exe|g" \
      "${pkgdir}${MINGW_PREFIX}"/bin/${fscripts}
  done

  # default aliases for all scripts/binaries
  if [[ "${_primary_python}" == "yes" ]]; then
    # Default names are aliases for Python now
    cp "${pkgdir}${MINGW_PREFIX}"/bin/python3.exe "${pkgdir}${MINGW_PREFIX}"/bin/python.exe
    cp "${pkgdir}${MINGW_PREFIX}"/bin/python3w.exe "${pkgdir}${MINGW_PREFIX}"/bin/pythonw.exe
    cp "${pkgdir}${MINGW_PREFIX}"/bin/python3-config "${pkgdir}${MINGW_PREFIX}"/bin/python-config
    cp "${pkgdir}${MINGW_PREFIX}"/bin/idle3 "${pkgdir}${MINGW_PREFIX}"/bin/idle
    cp "${pkgdir}${MINGW_PREFIX}"/bin/pydoc3 "${pkgdir}${MINGW_PREFIX}"/bin/pydoc
  fi

  # delete anything that could conflict with the primary Python
  if [[ "${_primary_python}" != "yes" ]]; then
    rm "${pkgdir}${MINGW_PREFIX}"/bin/python3.exe
    rm "${pkgdir}${MINGW_PREFIX}"/bin/python3w.exe
    rm "${pkgdir}${MINGW_PREFIX}"/bin/python3-config
    rm "${pkgdir}${MINGW_PREFIX}"/bin/idle3
    rm "${pkgdir}${MINGW_PREFIX}"/bin/pydoc3
    rm "${pkgdir}${MINGW_PREFIX}"/bin/2to3
    rm "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/python3-embed.pc
    rm "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/python3.pc
    rm "${pkgdir}${MINGW_PREFIX}"/share/man/man1/python3.1
  fi
}

sha256sums=('3c3bc3048303721c904a03eb8326b631e921f11cc3be2988456a42f115daf04c'
            '18c2073dc106e16e503cedfb8d22d98fe894f61fd870a2f209a27343c4f5356a'
            'a434e224fb14724bf00f95e828da8e453ba65b6988d98fabda2b32caf110adb0'
            '8bd71f40554d3efe432e0e9fc63bd0d9740929c750d1b16e4f55b41f3ab8ad92'
            'ec2afa2f91d5a7ab9df64f633fb4cb8f1274216609933bccbd5bea15cd262578'
            'eea1d612c0608cc0032d8343273e5ed2ae5f2ae4a70d29357b2461b17efe8cdb'
            '035ba6d9ad224a348896cbc4593894bf35f6fe07cceb34fc5dc9022245501e6e'
            'b7e7a4e2a9242cc3ccafc6635846d83f3d4b730dbd40356a55e91b8c8af4a9c1'
            'aab53a74c2a921e820948dc4e46fd2365427deb76418288430c0cc3803bd17cb'
            'f41c6996fd7e795fb8829792710c0cc98602e7438aa9ab6f2798007a247166b8'
            '69b07e9ac3a730a7ae4a8a46d9e97a564c3aea88d1c812d733d4af3d7bb9a80d'
            'e90d9fe1486e92b42de2806bbd92b21a368f2a8409c189f3efa34d13917853a0'
            '8daa98e3a456cae8ce58f39fd94719d1c641695ff3a60ce148f1131c63f1467c'
            '96b51a4f564aa3ed05a59d9e23ff62c846e2892547e6ec64d99bd6821d2cf06e'
            '217fd46099c6741a41ae4a68ca53c6cbd8d2e409e8943c64d714f72500a16268'
            '113ddd855b58696d2e636a7f000ba6a38985a77f0abe3769922be1e20ee587d1'
            '52693ec73406cb1030a1e6c147f357a9666f95b2fb599032b4f064c5a726e0f7'
            'fff0825f39f63c8dd6298d93e5e29e0790cd96949bcd8b9a719796160f9c0ec8'
            'bb041ed67557cef6ca13fcf05953d1a59310370728fa07cbd95d43417d408c62'
            '30f93735b7c96a1c389878b41ce78b3803334f2f90ec852add318d14c45477fb'
            '1e157fd40c545340d46905c1d89a4381ba1bcab5fdaad019b70dd320199af9e3'
            'a9fb6a6ae284d7ec7cd3f4509a0395ab336d2d141758492e8ae48afb97612c9c'
            'db0e1ba51584cebf6c6358e9ad4ebc34a232e4d1fc78bb5629946598ccf4b2c4'
            '4e4ebadb0f00362adb0932179d5e59ea82f072119373d064445cf66435737fd5'
            '7ce4cde7790ffae3c596d464f6efa06f27c2f17230bd08e3791a7aa46fcbb2e1'
            '2526bc8649ee4f6d34816204870a7840ca38d39bfe1e9ae1228c9cbf1c164b61'
            '4e35d48c4d112ea1ca0ff1bcdd57ebbb99ea220f61b4455510fff9af6dcc2d7b'
            '21dae5fdff7266e830064ac6d290e5ad2efda1af714a70d517e775d543cc00bb'
            '593a4f485f732b31c27333fdd8c130b4afd52f4d2a9dbb40bcfb1fbda93eaa25'
            '62c852b4dd39d8175e06628d56eb052a57ba2935450d9465eda8bd204ade964b'
            '3ef4dd2f759a26d7e1f70bc52d23f89f4b65fb7c022432041dc8e89f4d729e07'
            '8310bf1c8cb54101df5aa913933dda5ba7b4b7e54ddb09ad8e8c9022ae53d2f7'
            '241f4cb84fc8132eabbca614ca91f453ad107ed55be251458e16babd92690a7b'
            '2242aef00c1bbd576349fc9ae84a947e57fa6d3a0a9a8669fa81a1c0ccf8c18c'
            'bed4ad024087f0bf87f5e9e8d5b8599231b45806f61b47b1b9ecaa5cb690408f'
            '5da15f7a1f79c152f9fd6dea2404e132ce9a4838fb51ceb579a19e97863b5971'
            'f6d9361ad8583a0787aef46439b968870b9da7abf8eb85d53dfd952de94d5b25'
            '08a2fb1d4ad15d31e82195cf6233dba1463f1cf321e661505c9eb39595fb6584'
            '1ba0b97a1a83b6e010c71cadb21b54a4c49903be0e1be1c0d07b383c28bca97e'
            '48076ee3c9f70fd4e7235d3d8478762dd19f57ad3155fd513afaee09425e4dde'
            '0aaafa1633ffcba6676e1151d4ed42c86fd476f161c11054cbc27655cdab17b5'
            'fe281cb7023381605452ce15ec9807abc63092b675f575f88dbffa832c82a06f'
            '67d6209e2702a36558be382dc18f403f890d2767a16700e8d5f326ef8d1bea20'
            'ea4af6895c21b8a6848900971ef6bd8d5b486bc2044145b3bcddc450d1b0809f'
            '1cca73c5a3dd10258cc5df27d67444d52974e0d7e47fa5a5d2c1a3f969cae369'
            '6a01f4b647546184fcf9ebfd47f396c6daec0537203f8aea605642c87ea42333'
            '024599baee812cdcef45837d0e9fc1c0205e6b1a211c0422e3197cbe89c5db77'
            '8d6e1de4724351fe56832716c0dfff9023bf6b7a83297b1572028d8a99236452'
            '2d9c37a16b63cb9d12c0e59619fea83ec4a4890235343e27ab57ea9739e41bb4'
            'eb412e4ad0cf59ca2eb235d2b357be825a3431887ee55fbce4922e63ee579a88'
            '3513b0a6701c5d71a04b312403ea3c3d64081fa8ecf1d79c6b93833d5427d9ad'
            'cb9e59a4332f80e0c21f143db67e46bfdbe71feea1fa14acfe9eb0ec286a2859'
            '7594b1dc827a800479eeda9a85f9b32c98936d5368282004c01d0ef5245095f8'
            '4b97f66f69e18b95b591d4c9ddbd0079109b45db60532d45f46263a5ab60bdbb'
            '3ca988b991c4ccdec2ffcd4a34b8388213e33a8e6433ebc212fdd4d4f11594f4'
            '5f04746e1aabad7926b4da3d381df2edc1e0a3f7d1c53585d45b339e3236422e'
            '205fcb3b778b590014be43f9da889678fffae6cb16686a69aa247484aabb93ea'
            'b95850ac11e5a269d4c6956841951141872a8cdb1feb55fccf23d3669f6c8a4e'
            'c1215356f28f599b2b584a9176aae335cfd82c4e2c4514e74bd0ebf6e052d3a2'
            'eb48b42a18df40484189d3014f0177760a299a2426c36f020130e0fa7b9352db'
            'f80d07ccc35c7fc072a1235d03ae9b77afb0f4b7d8c8ffe128f325c7c5ad1529'
            '35b37eafa0f982a4adba6a5aed0dc3512b38d3ea25c5e64b0cd1b955ea777da7'
            '0c1f13dc5aa332d649769317c1417a6e4d834340f2884b2f315008055333c2e3'
            'bd0fb8054425fc63dcd9636fe20e08d9a58a517ce10a95e22f8b844b44ca508a'
            '667b82025ed506ad7fa1a2dc17a858ce4c20ef287dc012120fe7f273877166a1'
            'e8ea4ae9f454205c7bb84a21a8fba9426301c95b4ad2c0f1e8d2dd27c63720fc'
            'cbed091cd9370a63476bf25ef3d33f53fe5f6b1d7227c6d51b8d7af533ebf09e'
            '8386107e1b585eb2686863f8de4e7eab4ed67b496b7877543609b0f84af5822e'
            'febf7a5f31a358efc9a8b93c2cb38c085fa503e6ed5584bdac5c978e804f7235'
            '5658a984ca6c970ba201eb4488274a49ad8246c77388708d60a3776ca2f2e532'
            'fff92fd94549d9ea30bbfa7a89f73b83091fc851aa5f42e4509cb97ba89e2fbc'
            '176b9dd3e319a5e8109bc7a2ede8c51ebe7b27d907e3bca56352a632a94d59a7'
            '3d042025492afd7f902b3f4aeb8ff6fa75cd46e4bf0a7f7091178de70c39cabe'
            '4eaa7f734435a24ee1f575ab5b5d59f310de4c825e22518432ab1f6ecc7e06ab'
            'e60f9b6d361ca41dee75be80714af6ffa9800c30c363655714d3d6575ad65767'
            '182eb446e99fb3bc33fbe318b59fb1bfa39fc5c5c0c2397d4f28e9e3791b5ca3'
            '73e1a028d62745007777ff5e56a530ea65d8964d733866b9524851f732bedf06'
            'fa6cce25b65cae2ca165fc3cbad9e452ea29b3baba53ecd2bce88f5399ddb0a2'
            '897f220db759ff1e0d58b60192a5202e872955269087a49534ffa16783438492'
            'c466bd3bd4ffdbe6f9d64751d36611a4e071c1cc660996005cb0ecadafd8a633'
            '8cd2f88e4413101edfc4929a4b389d1a17734d454a0bf9dc2f04ee83ea3d807c'
            'd55e3caecc3eeb102d40ee3ffc92809aeaa83f53544204ba1c3b543ffe86cd52'
            'acf7991fc5a6a75140b6059d42ba595e0eb0c0b0df12f31c1a4d49a9a018e282'
            'd85109c73639afdda3175c46b81354f174111bdc26f15f8133fc2762bd6ddc4b'
            'a9e08be38d39a44a284735b9cb7c9f5f38fab2a4aa809985afcf9f330fdb0860'
            'c4c620a5e17740fea8a3815336f9d51b9ec78a100ed5482cd1f9a908ddab1511'
            'c6a4410c95e5bd891e66d60fb7a9bf461670bcaef1c4deb7033beab82cf1aecc'
            'e51103849e0fb41bee103e0a5ac7c089e7e6b30ec3d6461895cd9bdd0ea1c496'
            'f8c54619f2bfd568453c6094ded7d908fcb4069779af7037c907816566163156'
            '317efc67c8089f71692d5047730d6f692610985e84ab45534c115e9dd16fff2d'
            '512b8488a9dd9c5068f8bd5419a454db4cca03cbe8b61bf06b8121275702dfa4'
            '004b3d3108be66dced123e29bd32b69c5dc7ff14fde525136420f0151f93200a'
            '95a6ba780e2754ba1615932ef3636608078a75c060435958d82f6600b04954f4'
            'f05efe0c5226ead75b6ee29a28a1bde80544cb6ca4899a8e6f479379a459736f'
            'be952af59a7968e3ad4970cdb1072b8249fef36d1f44ace6aebba470694711fc'
            '317b6b592dadba4af3979ebc820cfde65aa5079befc81465374cc21afda74026'
            '1ba63208e5b27e00676407730a4c635250d2ce22a5bac1898124cc99fd193c6c'
            '34d5c13a8e349c072c6363480385552eef20db374f8f758099ec939810bb8b14'
            'cd0e38837bcf82522fa5485686a2700951bf753fec54c115f6399dac3171f5c6'
            'dd92d1444161d8cea0f5fad596483a3165bd02673992eab8d255d2be73f2c221'
            '6dd1f2729ee3b3323e7c6ebed962d1c401a37d0b8bd980d559d78ed4d1cc263d'
            'e5268f6edaf0772bfbbe247dd8a01a6c536bbcc7b11d1ecdd1b4627cdde48361'
            '3acc64a0d2f0fe0c981f33be60834e4e1d9a44c2792214c55d7f576ad7895c38'
            'b68d586fc76a32360b20d4a65a6dfd75419a9889cdf99bedf88e5ee842815712'
            '2d2f8cff7f04a467c970618262e2008554e925d3a49ec21f1d8b4731fd0a0d7d'
            '797c4c204c1ee218fd71309bbb32a7d36888c3036fdd1e71a200d0e630849d64'
            'ecc8c008a73dc41aebbaee2d36ce11f6fce68e4cbe9daa5365515c71bc5d7605'
            'aa796a15223d95b5e3f2539699657055b366b85a3c3145b70b86294afe62bb42'
            'a01d68f5c18e0f04567b0af0bbf9212ce1e1bc36c19c9b386652e46ef14d34f6'
            '6bd856f579a389881188724f492c531f8eb242292558e2da10d2f6c69f561c05'
            '9f42a60d546f9a28ba1aa202d85b1199583bb14318fd219eee875b6349cbb82f'
            'a641da0809e999fd49039cbc8316040d4a68b2a604da0fb416429ce09cf3de5d'
            'd7efc9d5b1c2f977e47ac2c0caa84c5cc2a8e7c72554574766d25a2caeefc4d6'
            '5a7d22ebe917059228c57799a4402681632a4988526895958bd9ab6fe8098675'
            'a44213e10d8c8e67368a581980f00155da8a84cc2d25c97977bc573ca741e813'
            '551bf1cd7620480cc8e64aaa115f255bb2339bd6eec3e66f01e0817c85c0ae29'
            '002777d5f14183859f10c8c14e8d7a78cccf864a3ddaa5ca2a27ffd58e9ea68a'
            'fa5bc2a3a5413a79b32aca2463b031fd8519fffdecf976228a81754f708cd2f4'
            '61cb3180a73ad27f566be232af8832521cbd581f47f953f6df30c1810a12bf5d'
            '282f4c85f9dac37897dac2df504c35b4d2199193bb1a3b686ac237c1965bcd92'
            '6ba675509c2b9afa35322454e5a543a5ecea8fb1194dc273774c9457ef97c2ba'
            '940b2c8bebafa597be26daf6a82e3f68cf0678051e45bdc6d69784c43f63bfd4'
            '8ae17a357edff790463aeaac027698f8126cb635ef85403df71909329b56d448'
            '3ab43b638529f3fb9039d9f9c2ae8511c2c7d8600ff7fe2f843e8c3d17af1c57'
            '1aa594b13263c8600a5c4a7ffdca84e3ab1031e30540252a75331e733cd2ac05'
            'db8eb548f6c10870d7cb2b7064b24538356da34fee305decd6b1017a6b8cb979'
            '116064df80cf4a5978d8df44ad6d9f925f39679d34ef337324d5ebe4f265e282'
            'b50545ec4736f9b43b3be979ae88cf6ba0e9c7d6c2ad863daa3c5ea810718a1d'
            'bbfb8ac5473c577dd621522a5e8814b4656dcae7663575af50437398f6e6089c'
            'b3f353e0490dda02ce5dc4f09918f531c65fa3e1373385cece06501cdad8b665'
            'c31e773b43e0b663acf51608846374d8044829fafb01bbbcd8f875947262c095'
            'bd11fb88ce9bf834a86089a82ce46083ae36916855b737d94ff2125e5583004c'
            '05abd17c088a2ffcb783b26e4ed27944e59462df6b939cb17450e3350b906d0a'
            'ac2c4c8baadbb1dae9aafd67539480af30ce1dcf2d25edf499e0dd7382f2d841'
            'cf122926ea5e29cf38678a2227480a6a7b7270bd79c7602f8cc5bde71db8dc0f'
            '544b930bd7a07a512fa36c4f0f188f2dcced817ca1a6653dd8a1faaa61b1cd4e'
            'b4ffc9a1be5447ecc717ccfeb1214d1149947c53745da6b39630f7b4a7d03efd'
            '17db02dcd932627df7d606ac4aa4f30bdd27c4fe443e5cb65380d06729516232'
            '4e033897bb5e3855abd78eaf99a6379533ad7fdeb8c17bca0c87bbe3d595fab4'
            '788bede9b9b27d210745bfe24c4c9f6e79ee3011182601b4ee24b8cd7319b76e')
