# Maintainer: Ruben Agin <phabrics@phabrics.com>
_realname=tme
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.12rc10
pkgrel=1
pkgdesc="The Machine Emulator, or ${_realname}, provides a general-purpose framework for computer emulation. (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
license=('GPL3')
url="http://nme.osdn.io"
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
	     "bison"
	     "texinfo")
depends=("${MINGW_PACKAGE_PREFIX}-gtk4"
	 "${MINGW_PACKAGE_PREFIX}-libvncserver"
	 "${MINGW_PACKAGE_PREFIX}-libltdl")
source=("https://osdn.net/dl/nme/${_realname}-${pkgver}.tar.xz")
sha256sums=('e2c90f7c63de196c7bf52c9b06f17e2ab9db86fa86d8c3ff3ee2360fb20914f8')
options=('libtool')

build()
{
    mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

    ../"${_realname}-${pkgver}"/configure \
      --prefix="${MINGW_PREFIX}" \
      --build="${MINGW_CHOST}" \
      --host="${MINGW_CHOST}" \
      --enable-hosts="gtk rfb openvpn"

    make
}

package()
{
    cd "${srcdir}/build-${MSYSTEM}"
    make install DESTDIR="${pkgdir}"
    install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
