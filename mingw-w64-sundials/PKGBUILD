# Maintainer: Gene Harvey <gharveymn@gmail.com>

_realname=sundials
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.7.0
pkgrel=1
pkgdesc="SUite of Nonlinear and DIfferential/ALgebraic equation Solvers (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
url="https://computation.llnl.gov/projects/sundials"
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gcc-libgfortran"
         "${MINGW_PACKAGE_PREFIX}-msmpi"
         "${MINGW_PACKAGE_PREFIX}-openblas"
         "${MINGW_PACKAGE_PREFIX}-openmp"
         "${MINGW_PACKAGE_PREFIX}-suitesparse")
makedepends=('make'
             'patch'
             'sed'
             "${MINGW_PACKAGE_PREFIX}-cmake>=3.12.0"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-gcc-fortran")
optdepends=("${MINGW_PACKAGE_PREFIX}-python: for running examples")
conflicts=("${MINGW_PACKAGE_PREFIX}-cvode")
source=("${_realname}-${pkgver}.tar.gz"::https://github.com/LLNL/sundials/releases/download/v5.7.0/sundials-5.7.0.tar.gz
        '0001-sundials-link-errors.patch'
        '0002-sundials-missing-export.patch'
        '0003-sundials-use-default-installdirs.patch')
sha256sums=('48DA7BAA8152DDB22AED1B02D82D1DBB4FBFEA22ACF67634011AA0303A100A43'
            'AB4D3B7BC2AA8F363665E37BB5F51C3B62F8A0F7A6C668E74678FE3AF7D4BB5C'
            '7F119FBCC8A630A4E3443E3BBA252DAFB4B1567CED1BF1389052253529E97DDC'
            '310B9BEB86426FD2F817391BAF72C0F9AEFE9DC31E737DAED5CC0280B7693311')

prepare() {
  patch -d "${_realname}-${pkgver}" -p1 < 0001-sundials-link-errors.patch
  patch -d "${_realname}-${pkgver}" -p1 < 0002-sundials-missing-export.patch
  patch -d "${_realname}-${pkgver}" -p1 < 0003-sundials-use-default-installdirs.patch
}

build() {
  [[ -d build-${MINGW_PACKAGE_PREFIX} ]] && rm -rf build-${MINGW_PACKAGE_PREFIX}

  # Note: SuperLU_DIST is incompatible because it is built with 32-bit indices.

  # Why is there zero documentation on the PETSc architectures other than the PKGBUILD?
  # Seriously, are people just supposed to know what 'dto' means? "Ah yes, of course that
  # means the double-precision-real OpenMP optimized build." Absurd. In any case, the
  # current packaging of PETSc isn't compatible without a bunch of patches to SUNDIALS.

  # Set the precision of types.
  if [[ "${CARCH}" == "i686" ]]; then
    _index_type_size=32
  else  
    _index_type_size=64
  fi

  I_MPI_ROOT="" \
  MSYS2_ARG_CONV_EXCL="CMAKE_INSTALL_PREFIX=;EXAMPLES_INSTALL_PATH=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G "MSYS Makefiles" \
    -D BUILD_ARKODE=ON \
    -D BUILD_CVODE=ON \
    -D BUILD_CVODES=ON \
    -D BUILD_FORTRAN77_INTERFACE=ON \
    -D BUILD_FORTRAN_MODULE_INTERFACE=OFF \
    -D BUILD_IDA=ON \
    -D BUILD_IDAS=ON \
    -D BUILD_KINSOL=ON \
    -D BUILD_NVECTOR_MANYVECTOR=ON \
    -D BUILD_NVECTOR_MPIMANYVECTOR=ON \
    -D BUILD_NVECTOR_MPIPLUSX=ON \
    -D BUILD_NVECTOR_OPENMP=ON \
    -D BUILD_NVECTOR_PARALLEL=ON \
    -D BUILD_NVECTOR_PTHREADS=ON \
    -D BUILD_SHARED_LIBS=ON \
    -D BUILD_STATIC_LIBS=ON \
    -D BUILD_SUNLINSOL_KLU=ON \
    -D BUILD_SUNLINSOL_LAPACKBAND=ON \
    -D BUILD_SUNLINSOL_LAPACKDENSE=ON \
    -D BUILD_TESTING=ON \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -D CMAKE_RUNTIME_OUTPUT_DIRECTORY="${srcdir}/build-${MINGW_PACKAGE_PREFIX}"/build-output/bin \
    -D CMAKE_SYSTEM_IGNORE_PATH=/usr/lib \
    -D ENABLE_CUDA=OFF \
    -D ENABLE_HIP=OFF \
    -D ENABLE_HYPRE=OFF \
    -D ENABLE_KLU=ON \
    -D ENABLE_LAPACK=ON \
    -D ENABLE_MAGMA=OFF \
    -D ENABLE_MPI=ON \
    -D ENABLE_OPENMP=ON \
    -D ENABLE_OPENMP_DEVICE=OFF \
    -D ENABLE_PETSC=OFF \
    -D ENABLE_PTHREAD=ON \
    -D ENABLE_RAJA=OFF \
    -D ENABLE_SUPERLUDIST=OFF \
    -D ENABLE_SUPERLUMT=OFF \
    -D ENABLE_SYCL=OFF \
    -D ENABLE_TRILINOS=OFF \
    -D ENABLE_XBRAID=OFF \
    -D EXAMPLES_ENABLE_C=ON \
    -D EXAMPLES_ENABLE_CXX=ON \
    -D EXAMPLES_ENABLE_CUDA=OFF \
    -D EXAMPLES_ENABLE_F77=OFF \
    -D EXAMPLES_ENABLE_F90=OFF \
    -D EXAMPLES_ENABLE_F2003=OFF \
    -D EXAMPLES_INSTALL=ON \
    -D EXAMPLES_INSTALL_PATH=${MINGW_PREFIX}/share/sundials/examples \
    -D Fortran_INSTALL_MODDIR=include/sundials \
    -D KLU_LIBRARY=${MINGW_PREFIX}/lib/libklu.dll.a \
    -D MPI_C_COMPILER=${MINGW_PREFIX}/bin/mpicc.exe \
    -D MPI_CXX_COMPILER=${MINGW_PREFIX}/bin/mpicxx.exe \
    -D MPI_Fortran_COMPILER=${MINGW_PREFIX}/bin/mpifort.exe \
    -D SUNDIALS_BUILD_WITH_MONITORING=ON \
    -D SUNDIALS_INDEX_SIZE=${_index_type_size} \
    -D SUNDIALS_PRECISION=DOUBLE \
    -B build-${MINGW_PACKAGE_PREFIX} \
    -S ${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build build-${MINGW_PACKAGE_PREFIX}
}

check() {
  # LAPACK tests fail because our LAPACK (OpenBLAS) has 32-bit precision (I think. The overall 
  # results of the tests are fine. It's just something happening in the setup).

  # Note: The maintainer should have mpiexec.exe installed (https://www.microsoft.com/en-us/download/details.aspx?id=57467).
  ${MINGW_PREFIX}/bin/ctest --exclude-regex sunlinsol_lapack --test-dir build-${MINGW_PACKAGE_PREFIX}
}

package() {
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install build-${MINGW_PACKAGE_PREFIX}

  # Install the LICENSE and NOTICE files to the system licenses directory.
  install -d "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/"
  install -D -m644 "${srcdir}/${_realname}-${pkgver}"/{LICENSE,NOTICE} \
          "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/"

  ## Patch various files to be relocatable.

  # Patch the CMake export modules.
  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  for _f in "${pkgdir}${MINGW_PREFIX}"/lib/cmake/sundials/SUNDIALSTargets*.cmake; do
    sed -e "s|${PREFIX_WIN}|\$\{_IMPORT_PREFIX\}|g" -i "${_f}"
    sed -e "s|${MINGW_PREFIX}|\$\{_IMPORT_PREFIX\}|g" -i "${_f}"
  done
  
  # Patch the CMake config module.
  sed -e "s|${PREFIX_WIN}|\$\{CMAKE_CURRENT_LIST_DIR\}/../../..|g" \
      -i "${pkgdir}${MINGW_PREFIX}"/lib/cmake/sundials/SUNDIALSConfig.cmake

  sed -e "s|${MINGW_PREFIX}|\$\{CMAKE_CURRENT_LIST_DIR\}/../../..|g" \
      -i "${pkgdir}${MINGW_PREFIX}"/lib/cmake/sundials/SUNDIALSConfig.cmake

  # Patch the CMakeLists.txt files in the examples.
  shopt -s globstar
  for _f in "${pkgdir}${MINGW_PREFIX}"/share/sundials/examples/**/CMakeLists.txt; do
    _fdir=$(dirname "${_f}")
    _relative_root=$(realpath --relative-to="${_fdir}" "${pkgdir}${MINGW_PREFIX}")
    sed -e "s|${PREFIX_WIN}|\$\{CMAKE_CURRENT_LIST_DIR\}/${_relative_root}|g" -i "${_f}"
    sed -e "s|${MINGW_PREFIX}|\$\{CMAKE_CURRENT_LIST_DIR\}/${_relative_root}|g" -i "${_f}"
  done
}
