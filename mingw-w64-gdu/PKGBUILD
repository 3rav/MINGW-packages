# Maintainer: Wu Zhenyu <wuzy01@qq.com>

_realname=gdu
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=5.17.1
pkgrel=1
license=('spdx:MIT')
pkgdesc="Fast disk usage analyzer (mingw-w64)"
makedepends=(
  "$MINGW_PACKAGE_PREFIX-go"
  "$MINGW_PACKAGE_PREFIX-cc"
  git)
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://github.com/dundee/gdu"
source=("$url/archive/v$pkgver.tar.gz")
sha256sums=('1b37383e0054e059b02f71b3b5f216c19044f64432b5c5e59d62b361c12c9ad3')

prepare() {
  cd ${_realname}-${pkgver}
  mkdir -p build-${MSYSTEM}
}

build() {
  cd ${_realname}-${pkgver}
  local _DATE=$(git log -1 --pretty=%ct) # get commit date to make it REPRODUCABLE
  local _BUILDINFO="-X 'github.com/dundee/gdu/v5/build.Version=${pkgver}' \
                    -X 'github.com/dundee/gdu/v5/build.User=${PACKAGER}' \
                    -X 'github.com/dundee/gdu/v5/build.Time=${_DATE}'"
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" ${_BUILDINFO}" \
    -o build-${MSYSTEM}/gdu \
    github.com/dundee/gdu/v5/cmd/gdu
}

package() {
  cd ${_realname}-${pkgver}
  install -Dm755 build-${MSYSTEM}/${_realname} "${pkgdir}$MINGW_PREFIX/bin/$_realname.exe"
  install -Dm644 gdu.1    "${pkgdir}$MINGW_PREFIX/share/man/man1/$_realname.1"
  install -Dm644 LICENSE.md  "${pkgdir}$MINGW_PREFIX/share/licenses/${_realname}/LICENSE"
}
