# Maintainer: Maksim Bondarenkov <maksapple2306@gmail.com>

_realname=keepawake
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=0.4.5
pkgrel=1
pkgdesc="Keep your computer awake (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url="https://github.com/segevfiner/keepawake-rs"
msys2_references=(
    'aur'
)
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
options=('!strip') # upstream strips while building
source=("https://github.com/segevfiner/keepawake-rs/archive/v${pkgver}/${_realname}-rs-${pkgver}.tar.gz")
sha256sums=('bb3960e4aea90ae8a9fc6a0044d820d1c8443e29dc9f7936c26012999d5f0365')

prepare() {
  cd "${srcdir}/${_realname}-rs-${pkgver}"

  local _target="${CARCH}-pc-windows-gnu"
  if [[ $MINGW_PACKAGE_PREFIX == *-clang-aarch64 ]]; then
      _target="${CARCH}-pc-windows-gnullvm"
  fi
  cargo fetch --locked --target ${_target}
}

build() {
  cd "${srcdir}/${_realname}-rs-${pkgver}"

  cargo build --release --locked --features bin
  for _shell in bash zsh fish; do
    ./target/release/keepawake --completions ${_shell} > keepawake.${_shell}
  done
}

check() {
  cd "${srcdir}/${_realname}-rs-${pkgver}"

  cargo test --release --locked --features bin
}

package() {
  cd "${srcdir}/${_realname}-rs-${pkgver}"

  cargo install \
    --frozen \
    --offline \
    --no-track \
    --features bin \
    --path . \
    --root "${pkgdir}${MINGW_PREFIX}"

  install -Dm644 keepawake.bash "${pkgdir}${MINGW_PREFIX}/share/bash-completion/completions/keepawake"
  install -Dm644 keepawake.fish "${pkgdir}${MINGW_PREFIX}/share/fish/vendor_completions.d/keepawake.fish"
  install -Dm644 keepawake.zsh "${pkgdir}${MINGW_PREFIX}/share/zsh/site-functions/_keepawake"
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
