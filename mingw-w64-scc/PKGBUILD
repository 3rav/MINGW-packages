# Maintainer: Wu Zhenyu <wuzy01@qq.com>

_realname=scc
pkgbase=mingw-w64-$_realname
pkgname=$MINGW_PACKAGE_PREFIX-$_realname
pkgver=3.0.0
pkgrel=2
pkgdesc='Sloc, Cloc and Code: a very fast accurate code counter with complexity calculations and COCOMO estimates written in pure Go (mingw-w64)'
arch=(any)
mingw_arch=(mingw32 mingw64 ucrt64 clang64 clang32 clangarm64)
url="https://github.com/boyter/scc"
license=('spdx:MIT OR Unlicense')
makedepends=(
  "$MINGW_PACKAGE_PREFIX-go"
  "$MINGW_PACKAGE_PREFIX-gcc"
  "git"
)
source=("$_realname-$pkgver::git+https://github.com/boyter/$_realname#commit=a500fe2f535d421c03006c6521be5")
sha256sums=('SKIP')

prepare(){
  cd "$_realname-$pkgver"
  mkdir -p build/
}

build() {
  cd "$_realname-$pkgver"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  case "${CARCH}" in
    i686|x86_64)
      GOFLAGS+=" -buildmode=pie"
      ;;
  esac

  go build -o build ./...
}

check() {
  cd "$_realname-$pkgver"
  go test ./...
}

package() {
  cd "$_realname-$pkgver"
  install -Dm755 build/$_realname "$pkgdir$MINGW_PREFIX/bin/$_realname"
  install -Dm644 LICENSE "$pkgdir$MINGW_PREFIX/share/licenses/$_realname/LICENSE"
}
