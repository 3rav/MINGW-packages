# Maintainer: Wu Zhenyu <wuzy01@qq.com>

realname=scc
pkgbase=mingw-w64-$realname
pkgname=$MINGW_PACKAGE_PREFIX-$realname
pkgver=3.0.0
pkgrel=1
pkgdesc='Sloc, Cloc and Code: a very fast accurate code counter with complexity calculations and COCOMO estimates written in pure Go (mingw-w64)'
arch=(any)
mingw_arch=(mingw32 mingw64 ucrt64 clang64 clang32 clangarm64)
url="https://github.com/boyter/scc"
license=('MIT' 'Unlicense')
makedepends=($MINGW_PACKAGE_PREFIX-'go' $MINGW_PACKAGE_PREFIX-'gcc' 'git')
source=("$realname-$pkgver::git+https://github.com/boyter/$realname#tag=v3.0.0")
sha256sums=('SKIP')

prepare(){
  cd "$realname-$pkgver"
  mkdir -p build/
}

build() {
  cd "$realname-$pkgver"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build -o build ./...
}

check() {
  cd "$realname-$pkgver"
  go test ./...
}

package() {
  cd "$realname-$pkgver"
  install -Dm755 build/$realname "$pkgdir$MINGW_PREFIX/bin/$realname"
  install -Dm644 LICENSE "$pkgdir$MINGW_PREFIX/share/licenses/$realname/LICENSE"
}
