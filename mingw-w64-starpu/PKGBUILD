# Maintainer: Rafa≈Ç Brzegowy <rafal.brzegowy@yahoo.com>

_realname=starpu
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.3.9
pkgrel=4
pkgdesc='StarPU is a task programming library for hybrid architectures (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
depends=("${MINGW_PACKAGE_PREFIX}-fftw"
         "${MINGW_PACKAGE_PREFIX}-libssp"
         "${MINGW_PACKAGE_PREFIX}-hwloc"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread-git"
         $([[ "${CARCH}" = "i686" ]] || echo "${MINGW_PACKAGE_PREFIX}-opencl-icd")
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-autotools"
  "${MINGW_PACKAGE_PREFIX}-cc"
  $([[ "${CARCH}" = "i686" ]] || echo "${MINGW_PACKAGE_PREFIX}-opencl-headers")
)
options=('!docs')
license=('LGPL')
url='https://starpu.gitlabpages.inria.fr'
source=(https://files.inria.fr/starpu/${_realname}-${pkgver}/${_realname}-${pkgver}.tar.gz
        https://gitlab.inria.fr/starpu/starpu/-/commit/3e1de7001c67c25faaffb9c03d7edc87a118429f.patch)
sha256sums=('73adf2a5d25b04023132cfb1a8d9293b356354af7d1134e876122a205128d241'
            'bb2f0efc773bd134392fb1d6ce7da9440e713ce693c8ebfdba5d891e63233779')

prepare() {
  cd ${_realname}-${pkgver}
  patch -Np1 < ${srcdir}/3e1de7001c67c25faaffb9c03d7edc87a118429f.patch
  autoreconf -fiv
}

build() {
  local _arch_opt=""
  if [ "${CARCH}" = "i686" ]; then
    _arch_opt="--disable-opencl --disable-socl"
  else 
    _arch_opt="--enable-opencl --enable-socl"
  fi

  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  ../${_realname}-${pkgver}/configure \
    LDFLAGS=-lssp \
    --prefix=${MINGW_PREFIX} \
    --disable-opencl \
    --with-gnu-ld \
    --disable-mpi \
    --disable-build-doc \
    --disable-build-examples \
    --enable-quick-check \
    --disable-gdb \
    ${_arch_opt}

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make check || true
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make install DESTDIR="${pkgdir}"
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING.LGPL ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}

# vim: ts=2 sw=2 et:
