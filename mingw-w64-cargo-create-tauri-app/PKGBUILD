# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_basename=create-tauri-app
_realname=cargo-${_basename}
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.11.1
pkgrel=1
pkgdesc="Rapidly scaffold out a new tauri app project (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64' 'clang32')
url="https://github.com/tauri-apps/create-tauri-app"
license=('MIT' 'Apache')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
options=('staticlibs' 'strip')
source=("${_basename}-${_basename}-v${pkgver}.tar.gz::${url}/archive/${_basename}-v${pkgver}.tar.gz")
sha256sums=('cedc4afc0e06fc9179bbe317d53035b2374d83e288fd7ed83e8f56e6b8214ef3')

prepare() {
  cd "${srcdir}/${_basename}-${_basename}-v${pkgver}"

  "${MINGW_PREFIX}/bin/cargo.exe" fetch --locked
}

build() {
  msg "Cargo build for ${MSYSTEM}"
  cd "${srcdir}"
  cp -r "${_basename}-${_basename}-v${pkgver}" "build-${MSYSTEM}"
  cd "build-${MSYSTEM}"

  "${MINGW_PREFIX}/bin/cargo.exe" build \
    --release \
    --locked
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  "${MINGW_PREFIX}/bin/cargo.exe" test \
    --release \
    --locked
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  install -Dm755 "target/release/${_realname}.exe" "${pkgdir}${MINGW_PREFIX}/bin/${_realname}.exe"
  install -Dm644 "README.md" "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/README.md"
  install -Dm644 "LICENSE_MIT" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE_MIT"
  install -Dm644 "LICENSE_APACHE-2.0" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE_APACHE-2.0"
}

