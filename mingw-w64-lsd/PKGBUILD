# Maintainer: Peltoche <dev@halium.fr>
# Co-maintainer: Andrea Pascal <andrea@anodium.net>
# Co-Maintainer: Deeraurty at github

_realname=lsd
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.20.1
pkgrel=1
pkgdesc="The next gen ls command (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://github.com/Peltoche/lsd"
license=("MIT")
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
#depends=()
#optdepends=('powerline-fonts: powerline symbols for terminals'
#            'nerd-fonts-complete: popular collections such as Font Awesome & fonts such as Hack')
source=("https://github.com/Peltoche/lsd/archive/refs/tags/${pkgver}.tar.gz")
#https://github.com/Peltoche/lsd/archive/refs/tags/0.20.1.tar.gz
sha256sums=('a2086aa049b8bd21c880f23b21b0e9ef21d3c3829d40641aa8810c08be936c19')

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  export SHELL_COMPLETIONS_DIR="$PWD/completions"
  cargo build --release --locked
}

check() {
  cd ${srcdir}/${_realname}-${pkgver}

  # Unset lower-cased envs temp and tmp
  # that points to AppData/Local/Temp
  # to pass 
  #   - test_list_almost_all_empty_directory 
  #   - test_list_all_empty_directory
  unset temp
  unset tmp

  cargo test --release --locked
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  install -Dm755 target/release/${_realname} "$pkgdir${MINGW_PREFIX}/bin/${_realname}"
  install -Dm644 LICENSE "$pkgdir${MINGW_PREFIX}/share/licenses/$pkgname/LICENSE"
  install -Dm644 completions/_lsd "$pkgdir${MINGW_PREFIX}/share/zsh/site-functions/_lsd"
  install -Dm644 completions/lsd.bash "$pkgdir${MINGW_PREFIX}/share/bash-completion/completions/lsd"
  install -Dm644 completions/lsd.fish "$pkgdir${MINGW_PREFIX}/share/fish/vendor_completions.d/lsd.fish"
}
