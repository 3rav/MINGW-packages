# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=scdoc
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.11.2
pkgrel=2
license=('MIT')
pkgdesc='Tool for generating roff manual pages (mingw-w64)'
arch=('any')
url='https://git.sr.ht/~sircmpwn/scdoc/'
makedepends=("${MINGW_PACKAGE_PREFIX}-cc")
source=("${_realname}-$pkgver.tar.gz"::"https://git.sr.ht/~sircmpwn/${_realname}/archive/$pkgver.tar.gz"
        parsedate.patch)
sha512sums=('3fbf1a74b222dd88410636124e4b6ca73f7e77f67d512cf430a878fefcaa5c6c13a1e9f6c0c9654de15353f94bb1bd528665acebc2293bebb325501d1eb6cda3'
            'b07767b00f1b5e6b54b0135391effaa50703501fecadd6b93fd93e0ce1ed5da91524671fcf3cb8cb428e6b1149d49fbf0bd97da1f7e0d40912585a260552d3f4')

prepare() {
  cd "$srcdir/${_realname}-$pkgver"
  patch -Np1 -i "${srcdir}"/parsedate.patch
  cp -r "$srcdir/${_realname}-$pkgver" "$srcdir/build-${MSYSTEM}"
}

build() {
  cd $srcdir/"build-${MSYSTEM}"
  # Upstream purposefully does not honor the user's LDFLAGS in order to keep
  # scdoc static. As we gain no benefit to statically linking and, in fact,
  # lose standard Arch practices (such as RELRO and PIE), we must override
  # LDFLAGS
  make PREFIX="${MINGW_PREFIX}" LDFLAGS="${LDFLAGS}"
}

check() {
  cd $srcdir/"build-${MSYSTEM}"
  make check
}

package() {
  cd $srcdir/"build-${MSYSTEM}"
  make PREFIX="${MINGW_PREFIX}" DESTDIR="${pkgdir}" install
  install -Dm644 COPYING -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
}
