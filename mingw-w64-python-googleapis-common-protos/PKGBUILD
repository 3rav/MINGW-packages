# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>

_realname=googleapis-common-protos
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.63.0
pkgrel=1
pkgdesc='Python classes generated from the common protos in the googleapis repository (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
msys2_references=(
  'archlinux: python-googleapis-common-protos'
  'pypi: googleapis-common-protos'
)
url="https://github.com/googleapis/python-api-common-protos"
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-protobuf")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python-wheel")
#optdepends=("${MINGW_PACKAGE_PREFIX}-python-grpcio")
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz"
        "001-increase-upper-limit-for-protobuf-5.X-versions.patch::https://github.com/googleapis/python-api-common-protos/commit/28fc17a9.patch")
sha256sums=('17ad01b11d5f1d0171c06d3ba5c04c54474e883b66b949722b4938ee2694ef4e'
            '3282a024ea3488333835f2bd3e54ef619ffb9d52e0f9995c1107bb0b06529c85')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/001-increase-upper-limit-for-protobuf-5.X-versions.patch
}

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
