# Maintainer: Swarnava Ghosh <swarnavaghosh04@gmail.com>
# Credits: Maximilian Schiller <manimax3@outlook.de> [https://aur.archlinux.org/cgit/aur.git/tree/PKGBUILD?h=krpc-cpp]
_realname=krpc-cpp
pkgbase=mingw-w64-${_realname}-git
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}-git
pkgver=0.4.9
_kspver=1.12.1
pkgrel=1
pkgdesc="KRPC Client for c++"
arch=('x86_64' 'i686')
url="https://github.com/krpc/krpc"
license=('LGPL3')
groups=()
depends=(
	"${MINGW_PACKAGE_PREFIX}-asio" 
	"${MINGW_PACKAGE_PREFIX}-protobuf")
makedepends=(
	"git"
	"patch"
	"unzip"
	"${MINGW_PACKAGE_PREFIX}-make"
	"${MINGW_PACKAGE_PREFIX}-cmake")
source=("https://github.com/nullprofile/krpc/releases/download/$pkgver-$_kspver/krpc-$pkgver-$_kspver.zip"
	    "https://gist.githubusercontent.com/swarnavaghosh04/452aa320a747ede48ea7f52403e45457/raw/514a82383f794314b71ad805a10cb04b7aef40f2/001-$_realname-$pkgver-cmake.patch")
md5sums=('9d6b9357447e1adf40e09c1cede52bbb'
        'e55b95137c27cc8654e4d254019e80e0')
noextract=("krpc-$pkgver-$_kspver.zip")

prepare() {
	echo prepare
	unzip krpc-$pkgver-$_kspver.zip client/$_realname-$pkgver.zip
	unzip client/$_realname-$pkgver.zip
	rm -rf client *.zip
	cd $_realname-$pkgver
	patch --forward --strip=1 --input="${srcdir}/001-$_realname-$pkgver-cmake.patch"	
}

build() {
	echo build
	cd "$_realname-$pkgver"
	mkdir cmake-build && cd cmake-build
	MSYS2_ARG_CONV_EXCL=* \
	cmake .. -G "MinGW Makefiles" -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX}
	mingw32-make
}

package() {
	echo package
	cd "$_realname-$pkgver/cmake-build"
	mingw32-make DESTDIR="$pkgdir/" install
}