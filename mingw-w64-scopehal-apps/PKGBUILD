# Maintainer: umarcor <unai.martinezcorral@ehu.eus>

_realname=scopehal-apps
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.0.0.r1275.g885265e5
pkgrel=2
pkgdesc="scopehal-apps: applications for libscopehal (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url="https://github.com/azonenberg/scopehal-apps"
license=('BSD-3-Clause')
groups=("${MINGW_PACKAGE_PREFIX}-eda")
depends=(
  "${MINGW_PACKAGE_PREFIX}-glm"
  "${MINGW_PACKAGE_PREFIX}-libsigc++"
  "${MINGW_PACKAGE_PREFIX}-gtkmm3"
  "${MINGW_PACKAGE_PREFIX}-yaml-cpp"
  "${MINGW_PACKAGE_PREFIX}-glew"
  "${MINGW_PACKAGE_PREFIX}-ffts"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-ninja"
)
_commit='885265e5'
source=("${_realname}::git://github.com/azonenberg/scopehal-apps.git#commit=${_commit}"
        "0001-glscopeclient-Check-WIXPATH-before-creating-MSI-inst.patch")
sha256sums=('SKIP'
            'e8f7082d09bc1dff2eb9aee8078ea804b7283f03f8212ea7bcb0e5178fba2b87')

pkgver() {
  cd "${_realname}"
  printf "0.0.0.r%s.g%s" "$(git rev-list --count "${_commit}")" "$(git rev-parse --short=8 ${_commit})"
}

prepare() {
  cd "${srcdir}/${_realname}"

  git remote set-url origin git://github.com/azonenberg/scopehal-apps
  git submodule update --init --recursive

  # https://github.com/azonenberg/scopehal-apps/pull/388
  git am --committer-date-is-author-date "${srcdir}/0001-glscopeclient-Check-WIXPATH-before-creating-MSI-inst.patch"
}

build() {
  cd "${srcdir}"
  [[ -d build-${MSYSTEM} ]] && rm -rf build-${MSYSTEM}
  mkdir -p build-${MSYSTEM}
  cd build-${MSYSTEM}

  CXXFLAGS+=" -Wno-format" \
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G "Ninja" \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DBUILD_TESTING=OFF \
    ../${_realname}

  ${MINGW_PREFIX}/bin/cmake.exe --build ./
}

check() {
  "${srcdir}/${_realname}"/build/src/glscopeclient/glscopeclient.exe --help
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --build ./ --target install

  install -Dm644 "${srcdir}/${_realname}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
