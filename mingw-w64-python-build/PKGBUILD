# Maintainer: @naveen521kk on Github Naveen M K <naveen521kk@gmail.com>

_realname=build
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.0.3
pkgrel=2
pkgdesc="A simple, correct Python build frontend (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
msys2_references=(
  'pypi: build'
)
url='https://github.com/pypa/build'
license=('spdx:MIT')
depends=(
  "${MINGW_PACKAGE_PREFIX}-python-packaging"
  "${MINGW_PACKAGE_PREFIX}-python-pyproject-hooks"
)
optdepends=(
  "${MINGW_PACKAGE_PREFIX}-python-colorama"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python-flit-core"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
)
options=('!strip')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz"
        "0001-optional-colorama.patch")
sha256sums=('538aab1b64f9828977f84bc63ae570b060a8ed1be419e7870b8b4fc5e6ea553b'
            'c122e055e4c963629a6f5f5ebe894d15d32c92dcdd2b97e72598d860e5846c47')

prepare() {
  cp -r "${_realname}-${pkgver}" "python-build-${CARCH}"
  cd "python-build-${CARCH}"

  patch -Np1 -i "${srcdir}/0001-optional-colorama.patch"
}

build() {
  cd "${srcdir}/python-build-${CARCH}"

  python -m flit_core.wheel
}

package() {
  cd "${srcdir}/python-build-${CARCH}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -D -m644 LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/python-${_realname}/LICENSE
}
