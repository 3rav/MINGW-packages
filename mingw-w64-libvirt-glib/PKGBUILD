# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=libvirt-glib
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.0.0
pkgrel=1
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
msys2_references=(
  'archlinux: libvirt-glib'
)
msys2_repository_url='https://gitlab.com/libvirt/libvirt-glib/'
pkgdesc="libvirt GLib and GObject mapping library (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-glib2"
         #"${MINGW_PACKAGE_PREFIX}-libssp" # disabled due to gir issue
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-libvirt")
makedepends=("${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-gtk-doc"
             "${MINGW_PACKAGE_PREFIX}-vala"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-cc")
license=('spdx: LGPL-2.1-or-later')
url="https://libvirt.org/"
#install=${_realname}-${CARCH}.install
source=(https://libvirt.org/sources/glib/${_realname}-${pkgver}.tar.xz{,.asc}
        002-disable-stack-protector-windows.patch)
sha256sums=('9bfec346382416a3575d87299bc641b2a464aa519fd9b1287e318aa43a2f3b8b'
            'SKIP'
            '4c8dd45e8cc1c24955e5bb967f46c5c857dca318aa2b34a66c97d6fcc3d5dd40')
validpgpkeys=('DAF3A6FDB26B62912D0E8E3FBE86EBB415104FDF') # Daniel P. Berrange <dan@berrange.com>

prepare() {
  cd ${_realname}-${pkgver}

  # https://gitlab.gnome.org/GNOME/gobject-introspection/-/issues/176,
  # via https://github.com/mesonbuild/meson/issues/1910.
  # Blowing up with -fstack-protector on mingw for the same reasons
  patch -p1 -i "${srcdir}/002-disable-stack-protector-windows.patch"
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}

  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("--buildtype=release")
  else
    _extra_config+=("--buildtype=debug")
  fi

  MSYS2_ARG_CONV_EXCL="--prefix=" \
  ${MINGW_PREFIX}/bin/meson.exe setup \
    --prefix="${MINGW_PREFIX}" \
    "${_extra_config[@]}" \
    --wrap-mode=nodownload \
    --default-library=both \
    --auto-features=enabled \
    -Dgit_werror=disabled \
    "../${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/meson.exe compile
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/meson.exe install
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
