# Maintainer: Ray Donnelly <mingw.android@gmail.com>
# Maintainer: Joan Karadimov <joan.karadimov@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>
# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname='pywin32'
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
url='https://sourceforge.net/projects/pywin32'
pkgdesc='Python for Windows Extensions (mingw-w64)'
license=('PSF')
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
pkgver=302
pkgrel=1
makedepends=(
    ${MINGW_PACKAGE_PREFIX}-python
    ${MINGW_PACKAGE_PREFIX}-python-setuptools
    rsync)
source=("https://github.com/mhammond/${_realname}/archive/b${pkgver}.zip"
        001-compile-and-setup-fixes.patch
        002-com-extensions-fix.patch
        003-win32-extensions-fix.patch
        004-isapi-fix.patch
        005-swig-fix.patch
        006-mapi-headers-fix.patch
        010-workaround-broken-add-dll-dir.patch)
sha256sums=('33547aa989573dc615bf69c9cc589ef8aefe7ec3858009faf1c26d59f73e4a30'
            '66e52d8f0978641145ace009f99ab0a7f3007e02154f81113535d9eff1b681c9'
            'b9387c7604da02c12d81ed7c06805cb87f088ee29ccfb51feeab8653e6ca2d3c'
            'a9722f8a2eb754ea88f0f2082dd2e3a6fff71a51766313dc40192a9a784072a6'
            '82f7745c4eef1e7c3262f6de71655967090c981c542a2e3b0544fda1649be68b'
            'a364eb52c9e0098516bb08ca5abb687fff4ee1c2e6d4a53dcbe91e60a6820879'
            'a04e4c8ee86606188c55349c4be755c72a9f03830d6f24828aba6bfa251e030d'
            '0168b0ea893cd252201dbde6baad6cfa4b382bc76ba65657606b6c76ce78262f')

prepare() {
    cd "${srcdir}/${_realname}-b${pkgver}"
    patch -p1 < "${startdir}"/001-compile-and-setup-fixes.patch
    patch -p1 < "${startdir}"/002-com-extensions-fix.patch
    patch -p1 < "${startdir}"/003-win32-extensions-fix.patch
    patch -p1 < "${startdir}"/004-isapi-fix.patch
    patch -p1 < "${startdir}"/005-swig-fix.patch
    patch -p1 < "${startdir}"/006-mapi-headers-fix.patch

    # FIXME: https://github.com/msys2-contrib/cpython-mingw/issues/48
    patch -p1 < "${startdir}"/010-workaround-broken-add-dll-dir.patch
}

build() {
    msg2 "Synchronizing build directory"
    rsync --recursive --times "${srcdir}/${_realname}-b${pkgver}"/* "${srcdir}/build-python-${CARCH}-${_realname}-${pkgver}"
    cd "${srcdir}/build-python-${CARCH}-${_realname}-${pkgver}"

    LIB="${MINGW_PREFIX}/lib:${MINGW_PREFIX}/${CARCH}-w64-mingw32/lib" \
    INCLUDE="${MINGW_PREFIX}/include:${MINGW_PREFIX}/${CARCH}-w64-mingw32/include" \
    ${MINGW_PREFIX}/bin/python setup.py build --skip-verstamp
}

package() {
    cd "${srcdir}/build-python-${CARCH}-${_realname}-${pkgver}"
    LIB="${MINGW_PREFIX}/lib:${MINGW_PREFIX}/${CARCH}-w64-mingw32/lib" \
    DISTUTILS_DEBUG=1 \
    MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python setup.py install \
        --prefix=${MINGW_PREFIX} \
        --root="${pkgdir}" \
        --optimize=1
}
