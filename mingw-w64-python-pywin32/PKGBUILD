# Maintainer: Ray Donnelly <mingw.android@gmail.com>
# Maintainer: Joan Karadimov <joan.karadimov@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>
# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname='pywin32'
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
url='https://sourceforge.net/projects/pywin32'
pkgdesc='Python for Windows Extensions (mingw-w64)'
license=('PSF')
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
pkgver=301
pkgrel=1
makedepends=(
    ${MINGW_PACKAGE_PREFIX}-python
    ${MINGW_PACKAGE_PREFIX}-python-setuptools
    rsync)
source=("https://github.com/mhammond/${_realname}/archive/b${pkgver}.zip"
        seh.h
        001-compile-and-setup-fixes.patch
        002-com-extensions-fix.patch
        003-win32-extensions-fix.patch
        004-isapi-fix.patch
        005-swig-fix.patch)
sha256sums=('b55811d60112aea58c58bbed2ffd92f7be6727cb2400764755c7e6cd9789d210'
            '03f8c0372059bb5b1933d77309101c67fa08504b42a6fce1186ed9a29e0c271f'
            '0d0406f36f0d203e69a7cb489f83428a2546c417e56e2147ddf2f73256d14af7'
            'c171bfbaeedd95cfd68a533e2255372f384f5f96b4c413109bf21bdd277c8253'
            '851d01864eea19d668e748214cb2ed19164c34f9a2db470e9b393a33559e7f26'
            '495735237e62d9cd829671a20e28896b4055ab3c017b06a12f3a56cfcfa5e9bd'
            '076cc45da1e5c44ee5530e72766a6aeac222b3db4117043761de5f1ac18f8aeb')

prepare() {
    cd "${srcdir}/${_realname}-b${pkgver}"
    patch -p1 < "${startdir}"/001-compile-and-setup-fixes.patch
    patch -p1 < "${startdir}"/002-com-extensions-fix.patch
    patch -p1 < "${startdir}"/003-win32-extensions-fix.patch
    patch -p1 < "${startdir}"/004-isapi-fix.patch
    patch -p1 < "${startdir}"/005-swig-fix.patch
}

build() {
    msg2 "Synchronizing build directory"
    rsync --recursive --times "${srcdir}/${_realname}-b${pkgver}"/* "${srcdir}/build-python-${CARCH}-${_realname}-${pkgver}"
    cd "${srcdir}/build-python-${CARCH}-${_realname}-${pkgver}"

    LIB="${MINGW_PREFIX}/lib:${MINGW_PREFIX}/${CARCH}-w64-mingw32/lib" \
    INCLUDE="${MINGW_PREFIX}/include:${MINGW_PREFIX}/${CARCH}-w64-mingw32/include" \
    ${MINGW_PREFIX}/bin/python setup.py build --skip-verstamp
}

package() {
    cd "${srcdir}/build-python-${CARCH}-${_realname}-${pkgver}"
    LIB="${MINGW_PREFIX}/lib:${MINGW_PREFIX}/${CARCH}-w64-mingw32/lib" \
    DISTUTILS_DEBUG=1 \
    MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python setup.py install \
        --prefix=${MINGW_PREFIX} \
        --root="${pkgdir}" \
        --optimize=1
}
