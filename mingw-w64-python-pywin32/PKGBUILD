# Maintainer: Ray Donnelly <mingw.android@gmail.com>
# Maintainer: Joan Karadimov <joan.karadimov@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>
# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname='pywin32'
pkgbase=mingw-w64-python-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-python-${_realname}
url='https://sourceforge.net/projects/pywin32'
pkgdesc='Python for Windows Extensions (mingw-w64)'
license=('PSF')
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang32' 'clang64')
pkgver=301
pkgrel=1
makedepends=(
    ${MINGW_PACKAGE_PREFIX}-python
    ${MINGW_PACKAGE_PREFIX}-python-setuptools
    rsync)
source=("https://github.com/mhammond/${_realname}/archive/b${pkgver}.zip"
        001-compile-and-setup-fixes.patch
        002-com-extensions-fix.patch
        003-win32-extensions-fix.patch
        004-isapi-fix.patch
        005-swig-fix.patch
        006-mapi-headers-fix.patch)
sha256sums=('b55811d60112aea58c58bbed2ffd92f7be6727cb2400764755c7e6cd9789d210'
            '7039ff36a782b169c2e01d53c2a12b654694a02a3da8f54b9227d917df724ed7'
            '180d1ae45b4cf8a15e64d68f56035feaec54f85b6acc5b5294804582c4a54cb8'
            'c3a120907950509c1852e44d066fceadc03421c39afa270c8dfb79166768cf75'
            '495735237e62d9cd829671a20e28896b4055ab3c017b06a12f3a56cfcfa5e9bd'
            '076cc45da1e5c44ee5530e72766a6aeac222b3db4117043761de5f1ac18f8aeb'
            '65da9bd818544422446b2f41ef6ab2d52a9dba5c21402a499ac89c3049d09324')

prepare() {
    cd "${srcdir}/${_realname}-b${pkgver}"
    patch -p1 < "${startdir}"/001-compile-and-setup-fixes.patch
    patch -p1 < "${startdir}"/002-com-extensions-fix.patch
    patch -p1 < "${startdir}"/003-win32-extensions-fix.patch
    patch -p1 < "${startdir}"/004-isapi-fix.patch
    patch -p1 < "${startdir}"/005-swig-fix.patch
    patch -p1 < "${startdir}"/006-mapi-headers-fix.patch
}

build() {
    msg2 "Synchronizing build directory"
    rsync --recursive --times "${srcdir}/${_realname}-b${pkgver}"/* "${srcdir}/build-python-${CARCH}-${_realname}-${pkgver}"
    cd "${srcdir}/build-python-${CARCH}-${_realname}-${pkgver}"

    LIB="${MINGW_PREFIX}/lib:${MINGW_PREFIX}/${CARCH}-w64-mingw32/lib" \
    INCLUDE="${MINGW_PREFIX}/include:${MINGW_PREFIX}/${CARCH}-w64-mingw32/include" \
    ${MINGW_PREFIX}/bin/python setup.py build --skip-verstamp
}

package() {
    cd "${srcdir}/build-python-${CARCH}-${_realname}-${pkgver}"
    LIB="${MINGW_PREFIX}/lib:${MINGW_PREFIX}/${CARCH}-w64-mingw32/lib" \
    DISTUTILS_DEBUG=1 \
    MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python setup.py install \
        --prefix=${MINGW_PREFIX} \
        --root="${pkgdir}" \
        --optimize=1
}
