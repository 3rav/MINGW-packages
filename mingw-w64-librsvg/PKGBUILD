# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=librsvg
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.52.4
pkgrel=2
pkgdesc="SVG rendering library (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
url="https://wiki.gnome.org/Projects/LibRsvg"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "${MINGW_PACKAGE_PREFIX}-vala"
             "${MINGW_PACKAGE_PREFIX}-rust"
             "${MINGW_PACKAGE_PREFIX}-gtk-doc")
depends=("${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2"
         "${MINGW_PACKAGE_PREFIX}-pango"
         "${MINGW_PACKAGE_PREFIX}-cairo"
         "${MINGW_PACKAGE_PREFIX}-libxml2")
optdepends=("${MINGW_PACKAGE_PREFIX}-gtk3: for rsvg-view-3")
options=('staticlibs' 'strip')
source=("https://download.gnome.org/sources/librsvg/${pkgver%.*}/${_realname}-${pkgver}.tar.xz"
        "0001-fix-stdout-to-file.patch"
        "0002-win32-libs.patch"
        "0005-hack-unixy-paths.patch"
        "0007-missing-exe-ext.patch")
sha256sums=('660ec8836a3a91587bc9384920132d4c38d1d1718c67fe160c5213fe4dec2928'
            '0a6a978722f88686cd51c15ffc4d4fcad0519eceb3b963e8057e2603ec7b3c26'
            'cf5bf24898122b3fe4ec4dcf02c53d69447a3d7c012704cafc3bf772322dfd74'
            '8f77800142cbd51c8d19b4f601ebc35bbcbb5e8a3b3ffb445013ed52b5b27b32'
            'c237e6d9b64ba5d45c5c3138502c6cc9bafa2f33e07521e54dc1a7f8e2cb4e83')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # Fix https://gitlab.gnome.org/GNOME/librsvg/-/issues/812
  patch -p1 -i "${srcdir}/0001-fix-stdout-to-file.patch"

  patch -p1 -i "${srcdir}/0002-win32-libs.patch"
  patch -p1 -i "${srcdir}/0005-hack-unixy-paths.patch"
  patch -p1 -i "${srcdir}/0007-missing-exe-ext.patch"

  autoreconf -fiv
}

build() {
  [[ -d "${srcdir}/build-${MSYSTEM}" ]] && rm -rf "${srcdir}/build-${MSYSTEM}"
  mkdir "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  # html documentation doesn't install from out of tree
  cp -rf "${srcdir}/${_realname}-${pkgver}/doc" "${srcdir}/build-${MSYSTEM}/doc"

  export lt_cv_deplibs_check_method='pass_all'

  # an argument passed to glib-mkenums starts with a c++ comment
  export MSYS2_ARG_CONV_EXCL="/*"

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-introspection \
    --enable-vala \
    --enable-silent-rules

  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING.LIB" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING.LIB"
}
