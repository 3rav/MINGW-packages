# Maintainer: Jonathan Armstrong <jda2158@gmail.com>

options=(debug !strip)

_realname=thor
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=r267.f9b7890
pkgrel=1
_commit='f9b78905374353d7f2650cf404bfe9d5fe0a823d'
pkgdesc="SFML Extension with various game programming features, like particles, animations, vector operations (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://bromeon.ch/libraries/thor/index.html"
license=('zlib')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-sfml"
             "git")

source=("thor"::"git+https://github.com/Bromeon/Thor#commit=$_commit"
        001-fix-stdc++11-and-constexpr.patch)
sha256sums=('SKIP'
            'd167b594ccd0d76e154eae61fe5478f4770015a5a0863da6a9f7146535e9c880')


pkgver() {
    cd "$srcdir/thor"
    printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

apply_patch_with_msg() {
  for _fname in "$@"
  do
    msg2 "Applying ${_fname}"
    patch -Nbp1 -i "${srcdir}"/${_fname}
  done
}

prepare() {
  # apply patch https://github.com/Bromeon/Thor/pull/20
  cd thor
  apply_patch_with_msg \
  001-fix-stdc++11-and-constexpr.patch
}

build() {

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  # shared build
  [[ -d ${srcdir}/build-${MSYSTEM}-shared ]] && rm -rf ${srcdir}/build-${MSYSTEM}-shared
  mkdir -p ${srcdir}/build-${MSYSTEM}-shared && cd ${srcdir}/build-${MSYSTEM}-shared
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ${extra_config} \
    -DTHOR_SHARED_LIBS=ON \
     ../thor
  ${MINGW_PREFIX}/bin/cmake --build .

  # static build
  [[ -d ${srcdir}/build-${MSYSTEM}-static ]] && rm -rf ${srcdir}/build-${MSYSTEM}-static
  mkdir -p ${srcdir}/build-${MSYSTEM}-static && cd ${srcdir}/build-${MSYSTEM}-static
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"Ninja" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ${extra_config} \
    -DTHOR_SHARED_LIBS=OFF \
    ../thor
  ${MINGW_PREFIX}/bin/cmake --build .
}


package() {
  # Static Install
  cd "${srcdir}/build-${MSYSTEM}-static"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .

  # Shared Install
  cd "${srcdir}/build-${MSYSTEM}-shared"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .
}
