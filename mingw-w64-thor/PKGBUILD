# Maintainer: Jonathan Armstrong <jda2158@gmail.com>

_realname=thor
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.0
pkgrel=1
pkgdesc="SFML Extension with various game programming features, like particles, animations, vector operations"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://github.com/Bromeon/Thor/"
license=('zlib')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-make"
             "${MINGW_PACKAGE_PREFIX}-cmake"
			 "${MINGW_PACKAGE_PREFIX}-gcc"
			 "${MINGW_PACKAGE_PREFIX}-sfml")

source=("https://github.com/Bromeon/Thor/archive/refs/heads/master.zip"
        001-fix-stdc++11-and-constexpr.patch)
sha256sums=('3f5a97980fd53b3c2ac4c48bf38238206c7e7ae97f87393eb44eb3e4b4b3dfb5'
            'd167b594ccd0d76e154eae61fe5478f4770015a5a0863da6a9f7146535e9c880')


# versioned releases unused for now due to age
#source=("https://github.com/Bromeon/Thor/archive/refs/tags/v${pkgver}.tar.gz")
#sha256sums=(fillme)

apply_patch_with_msg() {
  for _fname in "$@"
  do
    msg2 "Applying ${_fname}"
    patch -Nbp1 -i "${srcdir}"/${_fname}
  done
}

prepare() {
  # apply patch https://github.com/Bromeon/Thor/pull/20
  cd Thor-master
  apply_patch_with_msg \
  001-fix-stdc++11-and-constexpr.patch
}

build() {
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  # shared build
  [[ -d ${srcdir}/build-shared ]] && rm -rf ${srcdir}/build-shared
  mkdir -p ${srcdir}/build-shared && cd ${srcdir}/build-shared
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ${extra_config} \
    -DLIB_TYPE=SHARED \
     ../Thor-master
  make

  # static build
  [[ -d ${srcdir}/build-static ]] && rm -rf ${srcdir}/build-static
  mkdir -p ${srcdir}/build-static && cd ${srcdir}/build-static
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ${extra_config} \
    -DLIB_TYPE=STATIC \
    ../Thor-master
  make
}

options=(debug !strip)

package() {
  # Shared Install
  cd "${srcdir}/build-shared"
  make DESTDIR="${pkgdir}" install

  # Static Install
  cd "${srcdir}/build-static"
  make DESTDIR="${pkgdir}" install
}
