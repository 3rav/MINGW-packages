# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=intel-tbb
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2021.5.0
pkgrel=1
epoch=1
pkgdesc='High level abstract threading library (mingw-w64)'
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-make")
options=('!strip' 'staticlibs')
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://www.threadingbuildingblocks.org/'
license=('Apache')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/intel/tbb/archive/refs/tags/v${pkgver}.tar.gz
        "tbb-cmake-config.patch"
        "mingw-strict-ansi-back.patch")
sha256sums=('e5b57537c741400cf6134b428fc1689a649d7d38d9bb9c1b6d64f092ea28178a'
            '6530687a7f2f27f79fd519c11e57844f23c07a9cb11dddcf48897491bcecc98f'
            '48321d6276e6e3bc81e2d4c5b5d6a27b2389dadef3b4c50de9582ab5d2029133')

prepare () {
  cd ${srcdir}/oneTBB-${pkgver}
  patch -p1 -i ${srcdir}/tbb-cmake-config.patch
  patch -p1 -i ${srcdir}/mingw-strict-ansi-back.patch
}

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  [[ -d "build-${MINGW_CHOST}" ]] && rm -rf "build-${MINGW_CHOST}"
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    ${extra_config} \
    -DTBB_TEST=OFF \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ../oneTBB-${pkgver}
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install
  
  install -Dm644 ${srcdir}/oneTBB-${pkgver}/LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
