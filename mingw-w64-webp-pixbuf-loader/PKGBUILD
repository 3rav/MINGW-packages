# Maintainer: Miloš Komarčević <miloskomarcevic@aim.com>

_realname=webp-pixbuf-loader
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.0.6
pkgrel=1
pkgdesc="WebP GDK Pixbuf Loader library (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://github.com/aruiz/webp-pixbuf-loader"
license=('spdx:LGPL-2.0-or-later')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-meson")
depends=("${MINGW_PACKAGE_PREFIX}-libwebp"
         "${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/aruiz/webp-pixbuf-loader/archive/${pkgver}.tar.gz"
        "fix-gdk-pixbuf-loader-path.patch::https://github.com/aruiz/webp-pixbuf-loader/commit/43d3d77.patch"
         "0001-meson-support-installing-to-a-different-prefix.patch")
sha256sums=('451cb6924a9aa6afaa21d5b63b402dcfcfe952a1873e078b17078c4a1964a693'
            '9b0922dee048b61a59a72446f107f1d905a563178fd2301e08b3e53a485ce238'
            '86d2b9ac8fe91dc5e4b08e4874a042c2d7a7f4f411425747b7abdb8a874c919d')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 -i "${srcdir}"/fix-gdk-pixbuf-loader-path.patch
  patch -p1 -i "${srcdir}"/0001-meson-support-installing-to-a-different-prefix.patch
}

build() {
  [[ -d "${srcdir}/build-${MSYSTEM}" ]] && rm -rf "${srcdir}/build-${MSYSTEM}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/meson.exe \
      --prefix=${MINGW_PREFIX} \
      --wrap-mode=nodownload \
      --auto-features=enabled \
      --buildtype=plain \
      "../${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/meson.exe compile
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/meson.exe install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE.LGPL-2" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
