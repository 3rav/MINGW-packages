# Maintainer: umarcor <unai.martinezcorral@ehu.eus>

_realname=ffts
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.0.0.r800.g305a92c
pkgrel=1
pkgdesc="ffts: The Fastest Fourier Transform in the South  (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://github.com/anthonix/ffts"
license=('custom')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
_commit='fe86885e'
source=("${_realname}::git://github.com/anthonix/ffts.git#commit=${_commit}"
        "0001-CMake-Pretend-clang-is-gcc.patch")
sha256sums=('SKIP'
            '8a93db6b71231d0483cc5a4f9e2ed615779034c837e7956850d4d1cb46b5ba01')

pkgver() {
  cd "${_realname}"
  echo '0.0.0.r'"$(git rev-list --count HEAD)"'.g'"$(git describe --all --long | sed 's/^.*-g\(.*\)/\1/')"
}

prepare() {
  cd "${_realname}"
  git am --committer-date-is-author-date "${srcdir}/0001-CMake-Pretend-clang-is-gcc.patch"
}

build() {
  cd "${srcdir}/${_realname}"
  mkdir build
  cd build
  MSYS2_ARG_CONV_EXCL=-DCMAKE_INSTALL_PREFIX= cmake \
    -G "MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DENABLE_SHARED=ON \
    ../
  cmake --build .
}

package() {
  cd "${srcdir}/${_realname}"/build
  make DESTDIR="${pkgdir}" install

  _pre="${pkgdir}${MINGW_PREFIX}"

  mkdir "${_pre}"/bin
  mv "${_pre}"/lib/*.dll "${_pre}"/bin/

  _licenses="${_pre}/share/licenses/${_realname}"
  mkdir -p "${_licenses}"
  install -m 644 "${srcdir}/${_realname}"/COPYRIGHT "${_licenses}"
}
