# Maintainer: fauxpark <fauxpark@gmail.com>

_realname=libnatpmp
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=20150609
pkgrel=1
pkgdesc="A portable and fully compliant implementation of the NAT-PMP protocol (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='http://miniupnp.free.fr/libnatpmp.html'
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=("https://miniupnp.tuxfamily.org/files/${_realname}-${pkgver}.tar.gz")
sha256sums=('e1aa9c4c4219bc06943d6b2130f664daee213fb262fcb94dd355815b8f4536b0')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # The Makefile is useless...

  export CFLAGS="${CFLAGS} -Os -fPIC -Wall -DENABLE_STRNATPMPERR -DSTATICLIB"
  export LDFLAGS="${LDFLAGS} -lws2_32 -lIphlpapi -Wl,--no-undefined -Wl,--out-implib,libnatpmp.dll.a"

  # Compile
  for f in natpmp getgateway wingettimeofday; do
    ${CC} ${CFLAGS} -c -o ${f}.o ${f}.c ${LDFLAGS}
  done

  # Build static lib
  ar crs libnatpmp.a *.o

  # Build shared lib
  ${CC} -shared ${CFLAGS} *.o -o libnatpmp.dll ${LDFLAGS}

  # Build natpmpc
  ${CC} ${CFLAGS} -c -o natpmpc.o natpmpc.c ${LDFLAGS}
  ${CC} ${CFLAGS} -o natpmpc.exe natpmpc.c libnatpmp.dll.a ${LDFLAGS}
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  install -Dm644 libnatpmp.dll -t "${pkgdir}${MINGW_PREFIX}/bin"
  install -Dm644 natpmpc.exe -t "${pkgdir}${MINGW_PREFIX}/bin"
  install -Dm644 natpmp.h -t "${pkgdir}${MINGW_PREFIX}/include"
  install -Dm644 *.a -t "${pkgdir}${MINGW_PREFIX}/lib"
  install -Dm644 natpmpc.1 -t "${pkgdir}${MINGW_PREFIX}/share/man/man1"

  install -Dm644 LICENSE -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
}
