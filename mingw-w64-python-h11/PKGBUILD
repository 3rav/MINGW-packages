# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=h11
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}"
         "${MINGW_PACKAGE_PREFIX}-python-${_realname}-docs")
pkgver=0.14.0
pkgrel=1
pkgdesc='A pure-Python, bring-your-own-I/O implementation of HTTP/1.1 (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://github.com/python-hyper/h11'
license=('MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python-sphinx"
             "${MINGW_PACKAGE_PREFIX}-python-ipython"
             "${MINGW_PACKAGE_PREFIX}-python-mistune"
             "${MINGW_PACKAGE_PREFIX}-python-jsonschema"
	     "${MINGW_PACKAGE_PREFIX}-graphviz")
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-pytest")
source=("${_realname}-$pkgver.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        '0001-h11-0.14.0-docs-fix-conf.patch')
sha256sums=('d65a85d094b76846653fa7c3b45abdaf8b4f055c643bb6eec623f1311636a474'
            '8bbd40e614c23388ecb384b7e8da1b419f968ea89a783a8476d5616a74b83aca')

prepare() {
  cp -r "${srcdir}/${_realname}-${pkgver}" "${srcdir}/python-build-${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"

  patch -p1 -i "${srcdir}/0001-h11-0.14.0-docs-fix-conf.patch"
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py build

  msg "Build documentation"
  cd "${srcdir}/python-build-${MSYSTEM}/docs"
  make html
}

check() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/pytest
}

package_python-h11() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1 --skip-build

  install -Dm644 LICENSE.txt  ${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE.txt
}

package_python-h11-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mkdir -p "${pkgdir}${MINGW_PREFIX}/share/doc/python-${_realname}"
  cp -a "${srcdir}/python-build-${MSYSTEM}/docs/build/html" "${pkgdir}${MINGW_PREFIX}/share/doc/python-${_realname}/html"
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;

