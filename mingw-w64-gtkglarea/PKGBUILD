# Maintainer: 

_realname=gtkglarea
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.1.0
pkgrel=1
pkgdesc="opengl area for gtk2 (mingw-w64)"
arch=('any')
url="http://www.student.oulu.fi/~jlof/gtkglarea/"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-cc" "${MINGW_PACKAGE_PREFIX}-gtk2" "${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2")
options=('staticlibs' 'strip')
source=("https://download.gnome.org/sources/gtkglarea/2.1/gtkglarea-${pkgver}.tar.xz"
        "001-mingw-autotools.patch")
sha256sums=('8e1e545877d2f5139300b54c2e3bcd70acf7d80a78421a894192186a64db82de'
            '92d008d3b74a9c9e963f27701b2f562041dc421f7131b3338826ab9d17bd2f39')

prepare() {
  cd "${srcdir}/gtkglarea-${pkgver}"
  patch -Np1 -i "${srcdir}/001-mingw-autotools.patch"
  autoreconf -vi
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  cp -r "${srcdir}/gtkglarea-${pkgver}" "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
   --with-lib-opengl32

  make
}

package() {
  mkdir -p ${pkgdir}${MINGW_PREFIX}/lib 
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
  find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
}
