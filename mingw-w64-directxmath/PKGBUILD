# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=directxmath
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
# Version from CMakeLists.txt
pkgver=3.1.8
_tag=dec2023
pkgrel=3
pkgdesc="DirectXMath is an all inline SIMD C++ linear algebra library for use in games and graphics apps (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
msys2_references=(
  'aur: mingw-w64-directxmath'
)
url='https://go.microsoft.com/fwlink/?LinkID=615560'
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cc")
source=("${_realname}-${_tag}.tar.gz"::"https://github.com/microsoft/DirectXMath/archive/refs/tags/${_tag}.tar.gz"
        "https://github.com/microsoft/DirectXMath/commit/4def0cb7d36ae4e8b390de908d3ee246ccd08fd9.patch")
sha256sums=('cdc09f55a9999497d8c350d1c8c0e3f0fe92055cbf5490a2c319f9625080dc13'
            '17807f92c467620324f33975f97d512cc56bd5490a2b99dcc7c6fd9770ae9448')

prepare() {
  cd "${srcdir}/${_realname}-${_tag}"

  # https://github.com/microsoft/DirectXMath/pull/176
  patch -Np1 -i "${srcdir}/4def0cb7d36ae4e8b390de908d3ee246ccd08fd9.patch"
}

build() {
  cd "${srcdir}/${_realname}-${_tag}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      ../${_realname}-${_tag}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .

  install -Dm644 "${srcdir}/${_realname}-${_tag}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
