_realname=libipt
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgbase="mingw-w64-${_realname}"
pkgver=2.0.5.r18.gfa32af7
pkgrel=1
pkgdesc='Intel(R) Processor Trace decoder library (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://github.com/intel/libipt'
license=('spdx:BSD-3-Clause')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
           #  'pandoc' # Only required for building the manpage, we likely don't want that
             "${MINGW_PACKAGE_PREFIX}-cc"
             'git')
_commit='fa32af7a3aaa88b11e19ba8259fcd6eb4a2e768a'
source=("${_realname}"::"git+${url}#commit=${_commit}"
        "001-fix-import-lib-naming.patch")
sha256sums=('SKIP'
            '4e2a72080256239bf38abc1b9bbbd80bb9b9b4e584e914d57f8f93bcec15a26d')

pkgver() {
  cd "${srcdir}/${_realname}"

  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}/${_realname}"

  # https://github.com/intel/libipt/issues/99
  patch -Np1 -i "${srcdir}/001-fix-import-lib-naming.patch"
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -DDEVBUILD=OFF \
      -DFEATURE_THREADS=ON \
      -DFEATURE_ELF=ON \
      -DMAN=OFF \
      -DPTDUMP=ON \
      -DPTTC=ON \
      -DPTUNIT=ON \
      -DPTXED=OFF \
      -DSIDEBAND=ON \
      ../${_realname}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/cmake.exe --build . --target test
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .

  install -Dm644 "${srcdir}/${_realname}/README"  "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/README"
  install -Dm644 "${srcdir}/${_realname}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
