# Maintainer: Bet4 <0xbet4@gmail.com>

_realname=libipt
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgbase=mingw-w64-${_realname}
pkgver=2.0.6.r164.g7a863ba
pkgrel=1
pkgdesc='Intel(R) Processor Trace decoder library (mingw-w64)'
arch=(
  'i686'
  'x86_64'
)
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://github.com/intel/libipt'
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
           #  'pandoc' # Only required for building the manpage, we likely don't want that
             "${MINGW_PACKAGE_PREFIX}-cc"
             'git')
_commit='fa32af7a3aaa88b11e19ba8259fcd6eb4a2e768a'
source=("${_realname}"::"git+${url}#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_realname}"

  _ver="$(git describe --tags | sed -E -e 's|^[vV]||' -e 's|\-g[0-9a-f]*$||' | tr '-' '+')"
  _rev="$(git rev-list --count HEAD)"
  _date="$(git log -1 --date=format:"%Y%m%d" --format="%ad")"
  _hash="$(git rev-parse --short HEAD)"

  if [ -z "${_ver}" ]; then
    error "Could not determine version."
    return 1
  else
    printf '%s' "${_ver}.r${_rev}.${_date}.${_hash}"
  fi
}

prepare() {
  cd "${srcdir}/${_realname}"
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -DBUILD_SHARED_LIBS=ON \
      -DDEVBUILD=OFF \
      -DFEATURE_THREADS=ON \
      -DFEATURE_ELF=ON \
      -DMAN=OFF \
      -DPTDUMP=ON \
      -DPTSEG=ON \
      -DPTTC=ON \
      -DPTUNIT=ON \
      -DPTXED=OFF \
      -DSIDEBAND=ON \
      ../${_realname}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/cmake.exe --build . --target test
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
  # work around bad install name
  mv "${pkgdir}${MINGW_PREFIX}/lib/liblibipt.dll.a" "${pkgdir}${MINGW_PREFIX}/lib/libipt.dll.a"

  install -Dm644 "${srcdir}/${_realname}/README"  "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/README"
  install -Dm644 "${srcdir}/${_realname}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
