# Contributor: Teemu NÃ¤tkinniemi ("teepean") <tnatkinn@gmail.com>

_realname=htslib
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.12
pkgrel=1
pkgdesc="library for high-throughput sequencing data formats"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
url="https://github.com/samtools/htslib"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-bzip2"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-xz"
         "${MINGW_PACKAGE_PREFIX}-libdeflate")
provides=("${MINGW_PACKAGE_PREFIX}-tabix")
replaces=("${MINGW_PACKAGE_PREFIX}-tabix")
conflicts=("${MINGW_PACKAGE_PREFIX}-tabix")
source=(https://github.com/samtools/htslib/releases/download/$pkgver/$_realname-$pkgver.tar.bz2
        0001-mingw32-1.12-configure.patch)
sha256sums=('2280141b46e953ba4ae01b98335a84f8e6ccbdb6d5cdbab7f70ee4f7e3b6f4ca'
            'bc498890efa059576c7a2264c0eee8d72878442a14332712fb3e14eca59647a8')
if [[ "$MSYSTEM" == "MINGW32" ]]; then
  prepare() {
    cd ${srcdir}/${_realname}-${pkgver}
    patch -p1 -i ${srcdir}/0001-mingw32-1.12-configure.patch
    mkdir -p bin
  }
fi

build() {
  cd $srcdir/$_realname-$pkgver
    if [[ "$MSYSTEM" == "MINGW32" ]]; then
      LIBS=-lws2_32 ./configure \
        --prefix=${MINGW_PREFIX} \
        --build=${MINGW_CHOST} \
        --host=${MINGW_CHOST} \
        --target=${MINGW_CHOST} \
        --enable-libcurl \
        --disable-plugins
    else
    ./configure \
        --prefix=${MINGW_PREFIX} \
        --build=${MINGW_CHOST} \
        --host=${MINGW_CHOST} \
        --target=${MINGW_CHOST} \
        --enable-libcurl \
        --disable-plugins
  fi

  make
}

check() {
  cd $srcdir/$_realname-$pkgver

  make check
}

package() {
  cd $srcdir/$_realname-$pkgver

  make DESTDIR=$pkgdir install

  install -Dm644 LICENSE $pkgdir${MINGW_PREFIX}/share/licenses/$_realname/LICENSE

}

