# Contributor: Raed Rizqie <raed.rizqie@gmail.com>

_realname=winpty
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.4.3
pkgrel=3
pkgdesc='A Windows software package providing an interface similar to a Unix pty-master for communicating with Windows console programs (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url='https://github.com/rprichard/winpty'
msys2_repository_url='https://github.com/rprichard/winpty'
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc")
source=(https://github.com/rprichard/${_realname}/archive/${pkgver}.tar.gz
        001-makefile-mingw-fix.patch
        002-backport-new-function.patch)
sha256sums=('093037c39f9c899d79b74d5e15ff74fb59a98c492c5ed621e97e1090c3442865'
            '08635450bff09ba34291b82865519aba3ab364819a5d5ef767b91af91dba7b82'
            '226ceae8cb16372a46c114b63eedf075dc7dd5236fe621c169e194c1f80b47e4')

prepare() {
  cd ${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/001-makefile-mingw-fix.patch
  patch -p1 -i ${srcdir}/002-backport-new-function.patch
}

build() {
  cp -r ${_realname}-${pkgver} build-${MSYSTEM} && cd build-${MSYSTEM}
  
  ./configure  
  make
}

package() {
  cd build-${MSYSTEM}

  make PREFIX=${pkgdir}${MINGW_PREFIX} install

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
