# Maintainer: Jeff Hubbard <musikernel@gmail.com>

_realname=rubberband
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.0.0
pkgrel=1
pkgdesc="High quality software library for audio time-stretching and pitch-shifting. (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://breakfastquay.com/rubberband/"
license=("GPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-fftw"
         "${MINGW_PACKAGE_PREFIX}-libsamplerate"
         "${MINGW_PACKAGE_PREFIX}-libsndfile"
         "${MINGW_PACKAGE_PREFIX}-ladspa-sdk"
         "${MINGW_PACKAGE_PREFIX}-vamp-plugin-sdk")
source=("https://breakfastquay.com/files/releases/${_realname}-${pkgver}.tar.bz2"
        "https://github.com/breakfastquay/rubberband/commit/b3c920a.patch"
        "https://github.com/breakfastquay/rubberband/commit/3a6d8a8.patch")
sha256sums=('eccbf0545496ce3386a2433ceec31e6576a76ed6884310e4b465003bfe260286'
            '632f484fd16807f3fce251163bef4287be2d0c26a11f31c1750290afa21c0bb8'
            'f3d77ed80de0a4c52642a1f060eaece4b9f0d88009121b844d92a9ba286d8185')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}/b3c920a.patch"
  patch -p1 -i "${srcdir}/3a6d8a8.patch"
}

build() {
  [[ -d "${srcdir}/build-${MSYSTEM}" ]] && rm -rf "${srcdir}/build-${MSYSTEM}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("--buildtype=release")
  else
    extra_config+=("--buildtype=debug")
  fi

  MSYS2_ARG_CONV_EXCL="--prefix=" \
  ${MINGW_PREFIX}/bin/meson.exe \
    --prefix="${MINGW_PREFIX}" \
    "${extra_config[@]}" \
    --wrap-mode=nodownload \
    --default-library=both \
    --auto-features=enabled \
    -Dfft=fftw \
    -Dresampler=libsamplerate \
    "../${_realname}-${pkgver}"

  ${MINGW_PREFIX}/bin/meson.exe compile
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/meson.exe install
}
