# Maintainer: Carsten Haitzler <raster@rasterman.com>
# Contributor: Ryan Gammon <ryan@gamnation.net>

_pkgname=efl
pkgbase=mingw-w64-${_pkgname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_pkgname}"
pkgver=1.26.99.66986.g6efb60be59
pkgrel=1
pkgdesc="Enlightenment Foundation Libraries - Development version"
arch=('any')
mingw_arch=('mingw32' 'mingw64')
url="http://www.enlightenment.org"
license=('BSD' 'LGPL2.1' 'GPL2' 'MIT' 'custom')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-openjpeg2"
         "${MINGW_PACKAGE_PREFIX}-lz4" "${MINGW_PACKAGE_PREFIX}-luajit" 
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo" "${MINGW_PACKAGE_PREFIX}-dbus"
         "${MINGW_PACKAGE_PREFIX}-libsndfile" "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libtiff" "${MINGW_PACKAGE_PREFIX}-libavif"
         "${MINGW_PACKAGE_PREFIX}-libheif" "${MINGW_PACKAGE_PREFIX}-libjxl"
         "${MINGW_PACKAGE_PREFIX}-freetype" "${MINGW_PACKAGE_PREFIX}-fontconfig"
         "${MINGW_PACKAGE_PREFIX}-fribidi" "${MINGW_PACKAGE_PREFIX}-pixman"
         "${MINGW_PACKAGE_PREFIX}-gstreamer" "${MINGW_PACKAGE_PREFIX}-gst-plugins-base"
         "${MINGW_PACKAGE_PREFIX}-libraw" "${MINGW_PACKAGE_PREFIX}-openssl")
makedepends=("git" "${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-meson" "${MINGW_PACKAGE_PREFIX}-pkg-config")
options=('debug' 'ccache')
source=("git+https://git.enlightenment.org/enlightenment/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname

  local v_ver=$(grep version meson.build | head -1 | sed s/version//g | tr ":'," "   " | awk '{print $1}')

  printf "%s.%s.g%s" "$v_ver" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "${srcdir}/efl"
    patch --forward --strip=1 --input="../../install.patch"
}

build() {
  cd $_pkgname

  export CFLAGS="$CFLAGS -fvisibility=hidden"
  export CXXFLAGS="$CXXFLAGS -fvisibility=hidden"

  # EFL_RUN_IN_TREE needs to be set manually on Windows. See:
  # https://github.com/Enlightenment/efl-ci/commit/18ce3e8066f954bd32f2ba890e308def86c6a668
  export EFL_RUN_IN_TREE=1

  rm -rf build
  meson setup --prefix=/usr \
    --default-library shared \
    -Dsystemd=false \
    -Dpulseaudio=false \
    -Dv4l2=false \
    -Dlibmount=false \
    -Deeze=false \
    -Dx11=false \
    -Dxinput2=false \
    -Devas-loaders-disabler='pdf','ps','rsvg','json' \
    -Dopengl=none \
    -Dpixman=true \
    -Dembedded-lz4=false \
    -Dfribidi=true \
    -Dinput=false \
    -Dbuild-examples=false \
    -Dbuild-tests=false \
    -Dbindings= \
    -Dbuild-examples=false \
    -Dbuild-tests=false \
    . build
  ninja -C build
}

package() {
  cd $_pkgname

  DESTDIR="$pkgdir" ninja -C build install

  install -Dm644 -t "$pkgdir/usr/share/doc/$_pkgname/" README.md
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" AUTHORS COMPLIANCE COPYING
}
