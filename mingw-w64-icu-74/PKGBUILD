# Maintainer: Alexey Pavlov <Alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>
# Contributor: wirx6 <wirx654@gmail.com>

_realname=icu
pkgbase=mingw-w64-${_realname}-74
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-74")
pkgver=74.2
pkgrel=1
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64')
pkgdesc="International Components for Unicode library (mingw-w64)"
url="https://icu.unicode.org/home/"
msys2_repository_url="https://github.com/unicode-org/icu"
msys2_references=(
  "cpe: cpe:/a:icu-project:international_components_for_unicode"
)
license=('spdx:ICU')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "autoconf-archive")
source=(https://github.com/unicode-org/icu/releases/download/release-${pkgver//./-}/icu4c-${pkgver//./_}-src.tgz
        0001-Delete-obsolete-__STRICT_ANSI__-workaround-for-MinGW.patch::https://github.com/unicode-org/icu/commit/564c92d6.patch
        0011-sbin-dir.mingw.patch
        0012-libprefix.mingw.patch
        0014-mingwize-pkgdata.mingw.patch
        0015-debug.mingw.patch
        0016-icu-pkgconfig.patch
        0021-mingw-static-libraries-without-s.patch
        0023-fix-twice-include-platform_make_fragment.patch)
sha256sums=('68db082212a96d6f53e35d60f47d38b962e9f9d207a74cfac78029ae8ff5e08c'
            'e071322219b1f09175805e2adfea46a27e738950b2eb5d2e50b016af14406e2c'
            '4f4787caeccf70607cf0cbde0c005f05f5c6de1543265a927839122405b4054f'
            'e7ecdafe85e18a4a4b5f29bbfde38776521a848e5b65089a2379b90e59f1592d'
            'd9f5f756443d7d14175aca06f4f1bb33237b58907e5db7a6ff7e38d2c4ea7957'
            '0181f9ff8b7dd0a423869d8772da0a4feea64188347eb0dee7eb059aea92131c'
            '87ebe8962f8c387714f2a697a664a0c49aed2331b988548069d0c211abc36e05'
            'd8612f40b1731d9a94290afcf80c896184a2f15b8ae8f23b3643c64f6cabfa2f'
            '517a4b2308c5d7662768ece12b01b457b83a2cd80d8bf407e593e6412dfbde92')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"/icu

  patch -p2 -i "${srcdir}"/0001-Delete-obsolete-__STRICT_ANSI__-workaround-for-MinGW.patch

  apply_patch_with_msg \
    0011-sbin-dir.mingw.patch \
    0012-libprefix.mingw.patch \
    0014-mingwize-pkgdata.mingw.patch \
    0015-debug.mingw.patch \
    0016-icu-pkgconfig.patch \
    0021-mingw-static-libraries-without-s.patch \
    0023-fix-twice-include-platform_make_fragment.patch

  cd source
  autoreconf -vfi
}

build() {
  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("--enable-release")
  else
    _extra_config+=("--enable-debug")
  fi

  if [[ ${CC} == gcc ]]; then
    CXXFLAGS+=" -fext-numeric-literals"
  fi

  ../icu/source/configure \
    --prefix=${MINGW_PREFIX} \
    --includedir=${MINGW_PREFIX}/include/icu-74 \
    --libdir=${MINGW_PREFIX}/lib/icu-74 \
    --enable-shared \
    --enable-static \
    --disable-icuio \
    --disable-icu-config \
    --disable-rpath \
    --disable-extras \
    --disable-tools \
    --disable-tests \
    --disable-samples \
    --with-data-packaging=static \
    "${_extra_config[@]}"

  make
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  make install DESTDIR="${pkgdir}" RM="rm -rf"

  for _pcfile in "${pkgdir}${MINGW_PREFIX}"/lib/icu-74/pkgconfig/*.pc; do
    sed -s "s|libdir = ${MINGW_PREFIX}|libdir = \$\{prefix\}|g" -i "${_pcfile}"
    sed -s "s|includedir = ${MINGW_PREFIX}|includedir = \$\{prefix\}|g" -i "${_pcfile}"
  done
}
