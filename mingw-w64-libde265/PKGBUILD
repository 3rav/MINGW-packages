# Contributor: Edward E. <develinthedetail@gmail.com>

_realname=libde265
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0.8
pkgrel=3
pkgdesc="Open h.265 video codec library (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://github.com/strukturag/libde265"
license=('LGPL3' 'MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-SDL")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
optdepends=("${MINGW_PACKAGE_PREFIX}-SDL: for dec265 video output")
source=("https://github.com/strukturag/libde265/releases/download/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        "001-fix-VPATH.patch"
        "003-fix-pkgconfig-provide-includedir.patch"
        "004-CVE-2022-1253.patch"
        "005-CVE-2021-36408.patch"
        "006-CVE-2021-36410.patch"
        "007-CVE-2021-35452.patch"
        "008-CVE-2021-36411.patch")
sha256sums=('24c791dd334fa521762320ff54f0febfd3c09fc978880a8c5fbc40a88f21d905'
            'b4cdce2b362fde574512c2fad2e479f27969c0fe4df241322d9915d189dcb763'
            'b3638582753f637f458bc3a1ba701148e4147408547f90e09f171ce2882d84c3'
            '17d3e07e780cb30554635d4a19c069c79f81c52d78edce96dced749b17e1242f'
            '380d09ca3ab27c78a8f0b3ddcdfeec4666431ee342e3b7858e2237b6a550d56d'
            'aff66e4f939ddf1abf094de4ac7f288902b68897208691f893c8f6b61ce9d6a7'
            '738b0d3cc92fd1e76f04cf4e5623fc2df2c1a78823aae452ba8b54a5c3e2b840'
            'bae356d00ce928fe1aa02b4114e236db67b6ba2450478ae88ff557aed554bfee')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 -i ${srcdir}/001-fix-VPATH.patch
  patch -p1 -i ${srcdir}/003-fix-pkgconfig-provide-includedir.patch

  ./autogen.sh
}

build() {
  [[ -d "${srcdir}"/build-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${MSYSTEM}
  mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  ../${_realname}-${pkgver}/configure \
    CPPFLAGS='-I$(top_srcdir)/libde265 -I$(top_srcdir) -I$(top_srcdir)/sherlock265' \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST}

  make
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
