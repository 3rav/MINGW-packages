# Maintainer: Matthias Gondan <Matthias.Gondan-Rochon@uibk.ac.at>

_realname=swi-prolog-tests
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=9.2.5
pkgrel=1
pkgdesc="Unit tests for SWI-Prolog"
arch=(any)
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url="https://www.swi-prolog.org/"
msys2_references=("cpe: cpe:/a:swi-prolog:swi-prolog")
license=('spdx:BSD-2-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-swi-prolog")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-gmp"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-libarchive"
             "${MINGW_PACKAGE_PREFIX}-libyaml"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-db"
             "${MINGW_PACKAGE_PREFIX}-pcre2"
             "${MINGW_PACKAGE_PREFIX}-pkgconf")
source=("https://www.swi-prolog.org/download/stable/src/swipl-${pkgver}.tar.gz")

sha256sums=('b9f40771906c7e04be80ae4cfaa4463aeb44c52010a478edd8c7a4c022fe8781')

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  "${MINGW_PREFIX}"/bin/cmake \
    -G Ninja \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DMSYS2=1 \
    "${extra_config[@]}" \
    -DUSE_GMP=ON \
    -DSWIPL_PACKAGES_JAVA=OFF \
    -DSWIPL_PACKAGES_X=OFF \
    -DINSTALL_DOCUMENTATION=OFF \
    -DBUILD_TESTING=ON \
    -DINSTALL_TESTS=ON \
    -DSWIPL_{SHARED,STATIC}_LIB=ON \
    ../swipl-${pkgver}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR=${pkgdir} "${MINGW_PREFIX}"/bin/cmake.exe --install .

  rm -rf "${pkgdir}${MINGW_PREFIX}/bin"
  rm -rf "${pkgdir}${MINGW_PREFIX}/share"
  rm -rf "${pkgdir}${MINGW_PREFIX}/lib/swipl/app"
  rm -rf "${pkgdir}${MINGW_PREFIX}/lib/swipl/bin"
  rm -rf "${pkgdir}${MINGW_PREFIX}/lib/swipl/boot"
  rm -rf "${pkgdir}${MINGW_PREFIX}/lib/swipl/cmake"
  rm -rf "${pkgdir}${MINGW_PREFIX}/lib/swipl/customize"
  rm -rf "${pkgdir}${MINGW_PREFIX}/lib/swipl/demo"
  rm -rf "${pkgdir}${MINGW_PREFIX}/lib/swipl/doc"
  rm -rf "${pkgdir}${MINGW_PREFIX}/lib/swipl/include"
  rm -rf "${pkgdir}${MINGW_PREFIX}/lib/swipl/library"
  rm -rf "${pkgdir}${MINGW_PREFIX}/lib/swipl/lib"
  rm -rf "${pkgdir}${MINGW_PREFIX}/lib/cmake"
  rm -f "${pkgdir}${MINGW_PREFIX}/lib/swipl/boot.prc"
  rm -f "${pkgdir}${MINGW_PREFIX}/lib/swipl/swipl.home"
  rm -f "${pkgdir}${MINGW_PREFIX}/lib/swipl/LICENSE"
  rm -f "${pkgdir}${MINGW_PREFIX}/lib/swipl/README.md"
  rm -f "${pkgdir}${MINGW_PREFIX}/lib/swipl/swipl.ico"
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"
  PYTHONHOME="${MINGW_PREFIX}" "${MINGW_PREFIX}"/bin/cmake.exe --build . --target test
}
