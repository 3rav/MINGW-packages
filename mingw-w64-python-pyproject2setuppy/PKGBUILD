# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=pyproject2setuppy
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=20
pkgrel=1
pkgdesc='Cheap builder for pyproject-based build systems (mingw-w64)'
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
arch=('any')
url='https://github.com/mgorny/pyproject2setuppy'
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-python-setuptools"
         "${MINGW_PACKAGE_PREFIX}-python-toml")
source=("${_realname}-$pkgver.tar.gz"::"$url/archive/v$pkgver.tar.gz")
sha512sums=('1d75b908611f121c6723037d0132c890d9d00f3f7b1b237fa658dc8c217a1f60e7141a99a7d744e9b8ff67b5fdef1fd11b38f4a3bda0858a55836ad49211a068')

prepare() {
  cd "${srcdir}"
  rm -rf python-build-${MSYSTEM} || true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}"

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1 --skip-build
}
