# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=f2cblaslapack
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=3.4.2.q4
pkgrel=1
pkgdesc="f2c BLAS/LAPACK (mingw-w64)"
provides=("${MINGW_PACKAGE_PREFIX}-lapack")
arch=('any')
mingw_arch=('clang64')
url='https://www.petsc.org/' # Part of petsc
license=('2-clause BSD')
source=("http://ftp.mcs.anl.gov/pub/petsc/externalpackages/${_realname}-${pkgver}.tar.gz"
        001-no-petsc-conf.patch)
sha256sums=('41a43c6f2514ab34bf55aca5474b875882c7d4cd70c76bf4bc7ac433e6c2003e'
            '7a77aa2621945d2103bb85735b0e9d08526e1943b7f055c1ee7680fd401064f1')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i "${srcdir}"/001-no-petsc-conf.patch
}

build() {
  [[ -d "${srcdir}"/build-${MSYSTEM} ]] && rm -rf "${srcdir}"/build-${MSYSTEM}
  cp -r "${srcdir}"/${_realname}-${pkgver} "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}

  make
}

package() {
  cd "${srcdir}"/build-${MSYSTEM}
  install -Dm644 libf2cblas.a "${pkgdir}"${MINGW_PREFIX}/lib/libblas.a
  install -Dm644 libf2clapack.a "${pkgdir}"${MINGW_PREFIX}/lib/liblapack.a
}
