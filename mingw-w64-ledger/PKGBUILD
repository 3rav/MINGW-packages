
_realname=ledger
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.3.2
pkgrel=1
pkgdesc="Ledger is a powerful, double-entry accounting system that is accessed from the UNIX command-line."
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
license=("spdx:BSD-3-Clause")
makedepends=("${MINGW_PACKAGE_PREFIX}-"{cmake,cc,ninja})
depends=("${MINGW_PACKAGE_PREFIX}-"{boost,mpfr,utf8cpp,gmp,python})
url="https://ledger-cli.org/"
msys2_repository_url="https://github.com/ledger/ledger"
msys2_references=(
    'archlinux: ledger'
    "cpe: cpe:2.3:a:ledger-cli:ledger"
)
source=("https://github.com/ledger/ledger/archive/refs/tags/v3.3.2.tar.gz")
sha256sums=('555296ee1e870ff04e2356676977dcf55ebab5ad79126667bc56464cb1142035')

build() {
    mkdir -p "${srcdir}/build-${MSYSTEM}"
    cd "${srcdir}/build-${MSYSTEM}"
    MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
		       ${MINGW_PREFIX}/bin/cmake.exe -Wno-dev \
		       -GNinja\
		       -DBUILD_DEBUG=OFF \
		       -DBUILD_DOCS=OFF \
		       -DBUILD_LIBRARY=OFF \
		       -DBUILD_WEB_DOCS=OFF \
		       -DCMAKE_GNUtoMS=OFF \
		       -DUSE_DOXYGEN=OFF \
		       -DUSE_GPGME=OFF \
		       -DUSE_PYTHON=OFF \
		       -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
		       ../${_realname}-${pkgver}
    ${MINGW_PREFIX}/bin/cmake.exe --build .
}

package() {
    cd "${srcdir}/build-${MSYSTEM}"
    DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install .
}
