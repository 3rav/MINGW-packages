# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=installer
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.4.0
pkgrel=1
pkgdesc="A low-level library for installing from a Python wheel distribution (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://github.com/pradyunsg/installer'
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python-flit"
  "${MINGW_PACKAGE_PREFIX}-cc"
  "unzip"
)
_commit="32836e96e14d85db1f864ab5ff46ceb84b4a7673"
_launcher_commit="8c455825fc1308d34a1a47238a9eb6d56f732142"
source=("${_realname}-$pkgver"::"git+https://github.com/pradyunsg/installer.git#commit=${_commit}"
        "simple_launcher"::"git+https://bitbucket.org/vinay.sajip/simple_launcher.git#commit=${_launcher_commit}")
sha256sums=('SKIP'
            'SKIP')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

}

build() {
  cd "${srcdir}/simple_launcher"
  windres --input launcher.rc --output resources.res --output-format=coff

  local _gui_cflags="$CFLAGS -mwindows"
  local _cli_cflags="$CFLAGS"
  local _gui_lflags="-lshlwapi"
  local _cli_lflags="-lshlwapi"
  case "$MSYSTEM" in
  MINGW*)
    _gui_cflags+=" -mcrtdll=msvcr120"
    _cli_cflags+=" -mcrtdll=msvcr120"
    ;;
  esac

  case "${MINGW_CHOST}" in
  i686-w64-mingw32)
    cc $_cli_cflags -o t32.exe launcher.c resources.res $_cli_lflags
    cc $_gui_cflags -o w32.exe launcher.c resources.res $_gui_lflags
    ;;
  x86_64-w64-mingw32)
    cc $_cli_cflags -o t64.exe launcher.c resources.res $_cli_lflags
    cc $_gui_cflags -o w64.exe launcher.c resources.res $_gui_lflags
    ;;
  aarch64-w64-mingw32)
    cc $_cli_cflags -o t64-arm.exe launcher.c resources.res $_cli_lflags
    cc $_gui_cflags -o w64-arm.exe launcher.c resources.res $_gui_lflags
    ;;
  *)
    echo "Unsupported CHOST ${MINGW_CHOST}" && false
    ;;
  esac

  cd "${srcdir}/${_realname}-${pkgver}"

  flit build --no-setup-py
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  local site_packages="$(python3 -c "import sysconfig; print(sysconfig.get_path('purelib', 'posix_prefix', {'base': ''}), end='')")"
  local target="${pkgdir}${MINGW_PREFIX}${site_packages}"
  mkdir -p "${target}"
  unzip -q "dist/"*.whl -d "${target}"

  # Replace launchers
  rm "${target}"/installer/_scripts/*.exe
  cd "${srcdir}/simple_launcher"
  cp *.exe "${target}"/installer/_scripts/

  # compile Python bytecode
  ${MINGW_PREFIX}/bin/python -m compileall -d / "${target}"
  ${MINGW_PREFIX}/bin/python -O -m compileall -d / "${target}"
}
