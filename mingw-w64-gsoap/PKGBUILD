# Maintainer: gym603 <gui_yuan_miao@163.com>

_realname=gsoap
_ver_major=2
_ver_minor=8
_ver_patch=121
_dir="${_realname}-${_ver_major}.${_ver_minor}"
_file="${_realname}_${_ver_major}.${_ver_minor}.${_ver_patch}.zip"

pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver="${_ver_major}.${_ver_minor}.${_ver_patch}"
pkgrel=1
pkgdesc="An extensive suite of portable C and C++ software to develop XML Web services with powerful type-safe XML data bindings (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
license=('GPL2')
url="https://gsoap2.sourceforge.io/"
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-autotools")
source=(https://prdownloads.sourceforge.net/gsoap2/${_realname}_${pkgver}.zip)
sha256sums=('d5a66b9d5189143a6adba757a085f84d3c31c03b2948939cf99851003a2934a8')

build() {
  [[ -d "${srcdir}/build-${MSYSTEM}" ]] && rm -rf "${srcdir}/build-${MSYSTEM}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  ../${_dir}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --disable-ssl

  make -j1
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}/${_dir}"
  install -Dm644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.txt"
}
