# Maintainer: ZUO Zhihua <zuo.zhihua@qq.com>

_realname=fortran-stdlib
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.6.1
pkgrel=1
pkgdesc="Fortran Standard Library (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
msys2_references=(
  'aur: fortran_stdlib'
)
url="https://github.com/fortran-lang/stdlib"
license=('spdx:MIT')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread"
         $([[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]] || echo "${MINGW_PACKAGE_PREFIX}-gcc-libgfortran")
         "${MINGW_PACKAGE_PREFIX}-openblas")
makedepends=("${MINGW_PACKAGE_PREFIX}-fc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-python-fypp"
             "${MINGW_PACKAGE_PREFIX}-openblas")
source=("https://github.com/fortran-lang/stdlib/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
	      "001-using-openblas.patch")
sha256sums=('b46964e19e6b47ad5e63ac8a3706c4fa33504339000ca0cd0475a0cc06b0b7bd'
            '1ec6c36684c2c37bcb3adb0d8c13f1182377365643e575e399e7076d6a79e59d')

prepare() {
  cd "${srcdir}/stdlib-${pkgver}"
  patch -p1 -i ${srcdir}/001-using-openblas.patch
}

build() {
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DBUILD_TESTING=off \
      -DCMAKE_MAXIMUM_RANK=4 \
      -G Ninja \
      -DBUILD_SHARED_LIBS=ON \
      ../stdlib-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .
}
