# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=gi-docgen
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=2021.8+3+g50d4de9
pkgrel=1
pkgdesc="Documentation generator for GObject-based libraries (mingw-w64)"
arch=('any')
url='https://gnome.pages.gitlab.gnome.org/gi-docgen/'
license=('Apache' 'GPL3')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-jinja"
         "${MINGW_PACKAGE_PREFIX}-python-markdown" 
         "${MINGW_PACKAGE_PREFIX}-python-markupsafe" 
         "${MINGW_PACKAGE_PREFIX}-python-pygments"
         "${MINGW_PACKAGE_PREFIX}-python-toml" 
         "${MINGW_PACKAGE_PREFIX}-python-typogrify")
makedepends=(git
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python-sphinx")
checkdown=("${MINGW_PACKAGE_PREFIX}-python-setuptools")
_commit=50d4de940f97c017a79012ae741a0e98b51c1dfb  # main
source=("git+https://gitlab.gnome.org/GNOME/gi-docgen.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_realname}"
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd "${srcdir}/${_realname}"

  ## (OPTIONAL) Only if setuptools-scm is used
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  cd "${srcdir}"
  cp -r "${_realname}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/python setup.py build
   make -C docs
}

check() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  "${MINGW_PREFIX}"/bin/flake8 --show-source
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    "${MINGW_PREFIX}"/bin/python setup.py install --prefix="${MINGW_PREFIX}" \
      --root="${pkgdir}" --optimize=1 --skip-build

  ## (OPTIONAL) This entire section should be removed
  ## if the package does NOT instal anything in the ${MINGW_PREFIX}/bin directory.
  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*-script.py; do
    # Remove shebang line
    sed -e '1 { s/^#!.*$// }' -i "${_f}"
  done
  # setup.py puts this in the wrong place
  mkdir -p "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig"
  mv "${pkgdir}${MINGW_PREFIX}/share/pkgconfig/gi-docgen.pc" \
    "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/gi-docgen.pc"
  rm -rf "${pkgdir}${MINGW_PREFIX}/share/pkgconfig"
  
  mkdir -p "$pkgdir${MINGW_PREFIX}/share/doc/${_realname}"
  cp -r docs/_build/* "$pkgdir${MINGW_PREFIX}/share/doc/${_realname}"  
}
