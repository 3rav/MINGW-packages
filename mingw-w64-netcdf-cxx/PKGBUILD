
_realname=netcdf-cxx
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=4.3.1
pkgrel=2
pkgdesc="NetCDF C++ bindings (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://github.com/Unidata/netcdf-cxx4/"
license=(custom)
depends=("${MINGW_PACKAGE_PREFIX}-autotools"
         "${MINGW_PACKAGE_PREFIX}-cc"
         "${MINGW_PACKAGE_PREFIX}-netcdf>=4.6.0")
source=("${url}/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('e3fe3d2ec06c1c2772555bf1208d220aab5fee186d04bd265219b0bc7a978edc')

prepare() {
  cd "${_realname}${pkgver/.*/}-${pkgver}"

  autoreconf -vfi
}

build() {
  mkdir -p "build-${_realname}-${pkgver}-${MSYSTEM}"
  cd "build-${_realname}-${pkgver}-${MSYSTEM}"
  ../${_realname}${pkgver/.*/}-${pkgver}/configure -C \
    --prefix="${MINGW_PREFIX}"

  # file VERSION (generated by configure) confuses those
  # CLANG32/64 header files with #include <version>
  mv -f VERSION .VERSION

  make libh5bzip2_la_LIBADD=-lhdf5 LIBS=-lnetcdf
}

check() {
  make -j1 check -C "build-${_realname}-${pkgver}-${MSYSTEM}"
}

package() {
  make install DESTDIR="${pkgdir}" -C "build-${_realname}-${pkgver}-${MSYSTEM}"
  install -Dm644 "${_realname}${pkgver/.*/}-${pkgver}"/COPYRIGHT -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${pkg_realnamename}/"
}
