# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=libvterm
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.3.1
pkgrel=1
pkgdesc='Abstract library implementation of a VT220/xterm/ECMA-48 terminal emulator (mingw-w64)'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url='https://www.leonerd.org.uk/code/libvterm/'
license=('spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-cc" "libtool")
source=("https://www.leonerd.org.uk/code/libvterm/libvterm-${pkgver}.tar.gz"
        0001-make-remove-bin-sources.patch)
sha256sums=('25a8ad9c15485368dfd0a8a9dca1aec8fea5c27da3fa74ec518d5d3787f0c397'
            '6ca3142bcd4cf520c1f565038c9fb5b9d928f90a68d446660917812449393712')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-make-remove-bin-sources.patch"
}

build() {
  cp -rf "${_realname}-${pkgver}" "build-${MSYSTEM}"
  cd "build-${MSYSTEM}"

  make PREFIX="${MINGW_PREFIX}"
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  make PREFIX="${MINGW_PREFIX}" DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
