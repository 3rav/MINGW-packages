# Contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname=proto-plus
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=1.23.0
pkgrel=1
pkgdesc="Beautiful, Pythonic protocol buffers. (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://github.com/googleapis/proto-plus-python'
license=('spdx:Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python-protobuf")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-python-wheel")
options=('!strip')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz"
        "001-support-protobuf-5.x.patch")
sha256sums=('89075171ef11988b3fa157f5dbd8b9cf09d65fffee97e29ce403cd8defba19d2'
            '01e0d7d6ab550b756d60506c1020b4c2ee902ae326167b79c619d026fafc7037')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # https://github.com/googleapis/proto-plus-python/pull/433
  patch -Np1 -i "${srcdir}/001-support-protobuf-5.x.patch"
}

build() {
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
