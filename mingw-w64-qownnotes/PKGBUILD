# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=qownnotes
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=23.3.8
pkgrel=1
pkgdesc="Plain-text file markdown note taking with Nextcloud/ownCloud integration"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://www.qownnotes.org/'
license=('spdx:GPL-2.0-or-later')
depends=(
  "${MINGW_PACKAGE_PREFIX}-qt5-base"
  "${MINGW_PACKAGE_PREFIX}-qt5-svg"
  "${MINGW_PACKAGE_PREFIX}-qt5-declarative"
  "${MINGW_PACKAGE_PREFIX}-openssl"
  "${MINGW_PACKAGE_PREFIX}-qt5-websockets"
  "${MINGW_PACKAGE_PREFIX}-aspell"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-qt5-tools"
  "${MINGW_PACKAGE_PREFIX}-cc"
)
source=("https://download.tuxfamily.org/${_realname}/src/${_realname}-${pkgver}.tar.xz")
sha256sums=('ca7a6d26891ab6816c4cdf5b0563b615c1e4aa0e6dd74a9dc791290727be2d74')

build() {
  cd "${_realname}-${pkgver}"

  qmake \
    QMAKE_CFLAGS_RELEASE="${CFLAGS}" \
    QMAKE_CXXFLAGS_RELEASE="${CXXFLAGS}" \
    QMAKE_LFLAGS_RELEASE="${LDFLAGS}"
  make
}

package() {
  cd "${_realname}-${pkgver}"

  # install application
  install -D -m755 release/QOwnNotes.exe "${pkgdir}${MINGW_PREFIX}/bin/QOwnNotes.exe"

  # install visuals
  install -D -m644 PBE.QOwnNotes.desktop "${pkgdir}${MINGW_PREFIX}/share/applications/PBE.QOwnNotes.desktop"
  for format in {16x16,24x24,32x32,48x48,64x64,96x96,128x128,256x256,512x512}; do
    install -D -m644 "images/icons/${format}/apps/QOwnNotes.png" "${pkgdir}${MINGW_PREFIX}/share/icons/hicolor/$format/apps/QOwnNotes.png"
  done
  install -D -m644 "images/icons/scalable/apps/QOwnNotes.svg" "${pkgdir}${MINGW_PREFIX}/share/icons/hicolor/scalable/apps/QOwnNotes.svg"

  # install languages
  install -d "${pkgdir}${MINGW_PREFIX}/share/qt5/translations/"
  install -D -m644 languages/*.qm "${pkgdir}${MINGW_PREFIX}/share/qt5/translations/"
}
