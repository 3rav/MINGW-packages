# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Maintainer: Ray Donnelly <mingw.android@gmail.com>
# Contributor: Saul Ibarra Corretge <saghul@gmail.com>
# Contributor: Frode Solheim <frode@fs-uae.net>

_primary_python="no"
_pybasever=3.10
_realname=python

if [[ "${_primary_python}" == "yes" ]]; then
  pkgbase="mingw-w64-${_realname}"
  pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
  provides=("${MINGW_PACKAGE_PREFIX}-python3"
            "${MINGW_PACKAGE_PREFIX}-python${_pybasever}")
  conflicts=("${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python${_pybasever}"
            "${MINGW_PACKAGE_PREFIX}-python2<2.7.16-7")
  replaces=("${MINGW_PACKAGE_PREFIX}-python3"
            "${MINGW_PACKAGE_PREFIX}-python${_pybasever}")
else
  pkgbase="mingw-w64-${_realname}${_pybasever}"
  pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}${_pybasever}")
fi
pkgver=${_pybasever}.4
pkgrel=3
pkgdesc="A high-level scripting language (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
license=('PSF')
url="https://www.python.org/"
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-bzip2"
         "${MINGW_PACKAGE_PREFIX}-libffi"
         "${MINGW_PACKAGE_PREFIX}-mpdecimal"
         "${MINGW_PACKAGE_PREFIX}-ncurses"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-sqlite3"
         "${MINGW_PACKAGE_PREFIX}-tcl"
         "${MINGW_PACKAGE_PREFIX}-tk"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-xz"
         "${MINGW_PACKAGE_PREFIX}-tzdata")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-autotools"
  "autoconf-archive"
)
options=('makeflags') # 'debug' '!strip')
source=("https://www.python.org/ftp/python/${pkgver%rc?}/Python-${pkgver}.tar.xz"
        0001-sysconfig-make-_sysconfigdata.py-relocatable.patch
        0002-restore-setup-config.patch
        0003-build-add-with-nt-threads-and-make-it-default-on-min.patch
        0004-MINGW-translate-gcc-internal-defines-to-python-platf.patch
        0005-MINGW-configure-MACHDEP-and-platform-for-build.patch
        0006-MINGW-preset-configure-defaults.patch
        0007-MINGW-configure-largefile-support-for-windows-builds.patch
        0008-MINGW-add-srcdir-PC-to-CPPFLAGS.patch
        0009-MINGW-init-system-calls.patch
        0010-MINGW-build-in-windows-modules-winreg.patch
        0011-MINGW-determine-if-pwdmodule-should-be-used.patch
        0012-issue6672-v2-Add-Mingw-recognition-to-pyport.h-to-al.patch
        0013-MINGW-configure-for-shared-build.patch
        0014-MINGW-dynamic-loading-support.patch
        0015-MINGW-ignore-main-program-for-frozen-scripts.patch
        0016-MINGW-setup-_multiprocessing-module.patch
        0017-MINGW-setup-select-module.patch
        0018-MINGW-setup-_ctypes-module-with-system-libffi.patch
        0019-MINGW-defect-winsock2-and-setup-_socket-module.patch
        0020-MINGW-exclude-unix-only-modules.patch
        0021-MINGW-setup-msvcrt-and-_winapi-modules.patch
        0022-sysconfig-MINGW-build-extensions-with-GCC.patch
        0023-MINGW-setup-_ssl-module.patch
        0024-MINGW-generalization-of-posix-build-in-sysconfig.py.patch
        0025-MINGW-support-stdcall-without-underscore.patch
        0026-use-replace-instead-rename-to-avoid-failure-on-windo.patch
        0027-MINGW-customize-site.patch
        0028-add-python-config-sh.patch
        0029-cross-darwin-feature.patch
        0030-mingw-system-libffi.patch
        0031-msys-mingw-prefer-unix-sep-if-MSYSTEM.patch
        0032-mingw-use-posix-getpath.patch
        0033-mingw-add-ModuleFileName-dir-to-PATH.patch
        0034-mingw-add-BUILDIN_WIN32_MODULEs-time-msvcrt.patch
        0035-msys-cygwin-semi-native-build-sysconfig.patch
        0036-sysconfig-mingw-sysconfig-like-posix.patch
        0037-build-_winapi-earlier-so-we-can-use-it-in-distutils-.patch
        0038-cross-dont-add-multiarch-paths-if-cross-compiling.patch
        0039-mingw-use-backslashes-in-compileall-py.patch
        0040-msys-convert_path-fix-and-root-hack.patch
        0041-allow-static-tcltk.patch
        0042-mingw-pdcurses_ISPAD.patch
        0043-builddir-fixes.patch
        0044-msys-monkeypatch-os-system-via-sh-exe.patch
        0045-msys-replace-slashes-used-in-io-redirection.patch
        0046-remove_path_max.default.patch
        0047-dont-link-with-gettext.patch
        0048-ctypes-python-dll.patch
        0049-gdbm-module-includes.patch
        0050-use-gnu_printf-in-format.patch
        0051-mingw-fix-ssl-dont-use-enum_certificates.patch
        0052-fix-using-dllhandle-and-winver-mingw.patch
        0053-Add-AMD64-to-sys-config-so-msvccompiler-get_build_ve.patch
        0054-MINGW-link-with-additional-library.patch
        0055-install-msilib.patch
        0056-fix-signal-module-build.patch
        0057-build-build-winconsoleio-and-_testconsole.patch
        0058-expose-sem_unlink.patch
        0059-cygpty-isatty.patch
        0060-disable-broken-gdbm-module.patch
        0061-build-link-win-resource-files-and-build-pythonw.patch
        0062-disable-readline.patch
        0063-fix-isselectable.patch
        0064-use-_wcsnicmp-instead-wcsncasecmp.patch
        0065-_xxsubinterpretersmodule.patch
        0066-sqlite3-module-defines.patch
        0067-configure.ac-fix-inet_pton-check.patch
        0068-set-venv-activate-path-unix.patch
        0069-venv-creation-fixes.patch
        0070-pass-gen-profile-ldflags.patch
        0071-pkg-config-windows-must-link-ext-with-python-lib.patch
        0072-importlib-bootstrap-path-sep.patch
        0073-pathlib-path-sep.patch
        0074-warnings-fixes.patch
        0075-fix-build-testinternalcapi.patch
        0076-extend-MS_WINDOWS-flag.patch
        0077-clang-arm64.patch
        0078-configure.ac-set-MINGW-stack-reserve.patch
        0079-Don-t-use-os.pathsep-to-find-EOF.patch
        0080-Fix-extension-suffix-for-c-extensions-on-mingw.patch
        0081-Change-the-get_platform-method-in-sysconfig.patch
        0082-distutils-compiler-customize-mingw-cygwin-compilers.patch
        0083-distutils-compiler-enable-new-dtags.patch
        0084-distutils-MINGW-build-extensions-with-GCC.patch
        0085-distutils-use-Mingw32CCompiler-as-default-compiler-f.patch
        0086-distutils-find-import-library.patch
        0087-distutils-avoid-circular-dependency-from-time-module.patch
        0088-distutils-generalization-of-posix-build-in-distutils.patch
        0089-distutils-mingw-sysconfig-like-posix.patch
        0090-distutils-get_versions-fixes.patch
        0091-distutils-install-layout-as-posix.patch
        0092-distutils-msys-convert_path-fix-and-root-hack.patch
        0093-distutils-mingw-build-optimized-ext.patch
        0094-distutils-cygwinccompiler-dont-strip-modules-if-pyde.patch
        0095-distutils-get-compilers-from-env-vars.patch
        0096-distutils-add-windmc-to-cygwinccompiler.patch
        0097-distutils-fix-msvc9-import.patch
        0098-distutils-mingw-add-LIBPL-to-library-dirs.patch
        0099-distutils-Change-the-get_platform-method-in-distutil.patch
        0100-build-Fix-ncursesw-include-lookup.patch
        0101-tests-fix-test_bytes.patch
        0102-time-fix-strftime-not-raising-for-invalid-year-value.patch
        0103-ctypes-find_library-c-should-return-None-with-ucrt.patch
        0104-build-Disable-checks-for-dlopen-dlfcn.patch
        0105-Fix-install-location-of-the-import-library.patch
        0106-Set-MSYS2_ARG_CONV_EXCL-for-the-shared-Python-module.patch
        0107-build-Integrate-venvlauncher-build-installation-into.patch
        0108-configure.ac-set-_WIN32_WINNT-version.patch
        0109-configure.ac-don-t-check-for-clock_-functions.patch
        0110-expanduser-normpath-paths-coming-from-env-vars.patch
        0111-Add-support-for-Windows-7-Python-3.10.patch
        0112-CI-test-the-build-and-add-some-mingw-specific-tests.patch
        0113-smoketests-test-that-_decimal-exists.patch
        0114-Prefer-sysconfig.python_build.patch
        0115-Define-PY3_DLLNAME-to-fix-build.patch
        0116-distutils-remove-checks-for-ancient-gcc-binutils.patch
        0117-distutils-split-CC-env-var-before-passing-to-subproc.patch
        0118-_testconsole.c-Fix-casing-path-sep.patch
        0119-Return-consistent-architecture-markers-for-python-on.patch
        0120-distutils-add-back-gcc_version.patch
        0121-fix-mingw-cross-compiling-in-setup.py.patch
        0122-Use-actions-setup-python-for-setting-up-correct-vers.patch
        0123-handle-ncursesw-pkg-config-when-cross-compiling.patch
        0124-CI-add-cross-llvm-mingw-jobs.patch
        0125-Skip-building-c-extensions-if-python-is-running-in-b.patch
        0126-mingw_smoketests-fix-_UCRT-condition.patch
        0127-CI-fix-sed-pattern-for-python-config.py-shebang.patch
        0128-Modify-sys.winver-to-match-upstream.patch
        0129-Change-user-site-packages-path-to-include-the-enviro.patch
        0130-Build-venvlauncher.exe-from-PC-launcher.c.patch
        0131-PC-launcher.c-Use-function-available-in-original-msv.patch
        0132-Add-a-test-for-checking-the-new-launchers.patch
        0133-Revert-Add-a-test-for-checking-the-new-launchers.patch
        0134-Revert-PC-launcher.c-Use-function-available-in-origi.patch
        0135-Revert-Build-venvlauncher.exe-from-PC-launcher.c.patch
        0136-configure-Include-a-header-in-the-check-for-_beginth.patch
        0137-configure.ac-Default-to-without-c-locale-coercion-on.patch
        0138-CI-remove-without-c-locale-coercion.patch
        0139-Commit-regenerated-importlib.patch
        0140-fixup-cygpty-isatty.patch)

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}/Python-${pkgver}"

 apply_patch_with_msg 0001-sysconfig-make-_sysconfigdata.py-relocatable.patch \
  0002-restore-setup-config.patch \
  0003-build-add-with-nt-threads-and-make-it-default-on-min.patch \
  0004-MINGW-translate-gcc-internal-defines-to-python-platf.patch \
  0005-MINGW-configure-MACHDEP-and-platform-for-build.patch \
  0006-MINGW-preset-configure-defaults.patch \
  0007-MINGW-configure-largefile-support-for-windows-builds.patch \
  0008-MINGW-add-srcdir-PC-to-CPPFLAGS.patch \
  0009-MINGW-init-system-calls.patch \
  0010-MINGW-build-in-windows-modules-winreg.patch \
  0011-MINGW-determine-if-pwdmodule-should-be-used.patch \
  0012-issue6672-v2-Add-Mingw-recognition-to-pyport.h-to-al.patch \
  0013-MINGW-configure-for-shared-build.patch \
  0014-MINGW-dynamic-loading-support.patch \
  0015-MINGW-ignore-main-program-for-frozen-scripts.patch \
  0016-MINGW-setup-_multiprocessing-module.patch \
  0017-MINGW-setup-select-module.patch \
  0018-MINGW-setup-_ctypes-module-with-system-libffi.patch \
  0019-MINGW-defect-winsock2-and-setup-_socket-module.patch \
  0020-MINGW-exclude-unix-only-modules.patch \
  0021-MINGW-setup-msvcrt-and-_winapi-modules.patch \
  0022-sysconfig-MINGW-build-extensions-with-GCC.patch \
  0023-MINGW-setup-_ssl-module.patch \
  0024-MINGW-generalization-of-posix-build-in-sysconfig.py.patch \
  0025-MINGW-support-stdcall-without-underscore.patch \
  0026-use-replace-instead-rename-to-avoid-failure-on-windo.patch \
  0027-MINGW-customize-site.patch \
  0028-add-python-config-sh.patch \
  0029-cross-darwin-feature.patch \
  0030-mingw-system-libffi.patch \
  0031-msys-mingw-prefer-unix-sep-if-MSYSTEM.patch \
  0032-mingw-use-posix-getpath.patch \
  0033-mingw-add-ModuleFileName-dir-to-PATH.patch \
  0034-mingw-add-BUILDIN_WIN32_MODULEs-time-msvcrt.patch \
  0035-msys-cygwin-semi-native-build-sysconfig.patch \
  0036-sysconfig-mingw-sysconfig-like-posix.patch \
  0037-build-_winapi-earlier-so-we-can-use-it-in-distutils-.patch \
  0038-cross-dont-add-multiarch-paths-if-cross-compiling.patch \
  0039-mingw-use-backslashes-in-compileall-py.patch \
  0040-msys-convert_path-fix-and-root-hack.patch \
  0041-allow-static-tcltk.patch \
  0042-mingw-pdcurses_ISPAD.patch \
  0043-builddir-fixes.patch \
  0044-msys-monkeypatch-os-system-via-sh-exe.patch \
  0045-msys-replace-slashes-used-in-io-redirection.patch \
  0046-remove_path_max.default.patch \
  0047-dont-link-with-gettext.patch \
  0048-ctypes-python-dll.patch \
  0049-gdbm-module-includes.patch \
  0050-use-gnu_printf-in-format.patch \
  0051-mingw-fix-ssl-dont-use-enum_certificates.patch \
  0052-fix-using-dllhandle-and-winver-mingw.patch \
  0053-Add-AMD64-to-sys-config-so-msvccompiler-get_build_ve.patch \
  0054-MINGW-link-with-additional-library.patch \
  0055-install-msilib.patch \
  0056-fix-signal-module-build.patch \
  0057-build-build-winconsoleio-and-_testconsole.patch \
  0058-expose-sem_unlink.patch \
  0059-cygpty-isatty.patch \
  0060-disable-broken-gdbm-module.patch \
  0061-build-link-win-resource-files-and-build-pythonw.patch \
  0062-disable-readline.patch \
  0063-fix-isselectable.patch \
  0064-use-_wcsnicmp-instead-wcsncasecmp.patch \
  0065-_xxsubinterpretersmodule.patch \
  0066-sqlite3-module-defines.patch \
  0067-configure.ac-fix-inet_pton-check.patch \
  0068-set-venv-activate-path-unix.patch \
  0069-venv-creation-fixes.patch \
  0070-pass-gen-profile-ldflags.patch \
  0071-pkg-config-windows-must-link-ext-with-python-lib.patch \
  0072-importlib-bootstrap-path-sep.patch \
  0073-pathlib-path-sep.patch \
  0074-warnings-fixes.patch \
  0075-fix-build-testinternalcapi.patch \
  0076-extend-MS_WINDOWS-flag.patch \
  0077-clang-arm64.patch \
  0078-configure.ac-set-MINGW-stack-reserve.patch \
  0079-Don-t-use-os.pathsep-to-find-EOF.patch \
  0080-Fix-extension-suffix-for-c-extensions-on-mingw.patch \
  0081-Change-the-get_platform-method-in-sysconfig.patch \
  0082-distutils-compiler-customize-mingw-cygwin-compilers.patch \
  0083-distutils-compiler-enable-new-dtags.patch \
  0084-distutils-MINGW-build-extensions-with-GCC.patch \
  0085-distutils-use-Mingw32CCompiler-as-default-compiler-f.patch \
  0086-distutils-find-import-library.patch \
  0087-distutils-avoid-circular-dependency-from-time-module.patch \
  0088-distutils-generalization-of-posix-build-in-distutils.patch \
  0089-distutils-mingw-sysconfig-like-posix.patch \
  0090-distutils-get_versions-fixes.patch \
  0091-distutils-install-layout-as-posix.patch \
  0092-distutils-msys-convert_path-fix-and-root-hack.patch \
  0093-distutils-mingw-build-optimized-ext.patch \
  0094-distutils-cygwinccompiler-dont-strip-modules-if-pyde.patch \
  0095-distutils-get-compilers-from-env-vars.patch \
  0096-distutils-add-windmc-to-cygwinccompiler.patch \
  0097-distutils-fix-msvc9-import.patch \
  0098-distutils-mingw-add-LIBPL-to-library-dirs.patch \
  0099-distutils-Change-the-get_platform-method-in-distutil.patch \
  0100-build-Fix-ncursesw-include-lookup.patch \
  0101-tests-fix-test_bytes.patch \
  0102-time-fix-strftime-not-raising-for-invalid-year-value.patch \
  0103-ctypes-find_library-c-should-return-None-with-ucrt.patch \
  0104-build-Disable-checks-for-dlopen-dlfcn.patch \
  0105-Fix-install-location-of-the-import-library.patch \
  0106-Set-MSYS2_ARG_CONV_EXCL-for-the-shared-Python-module.patch \
  0107-build-Integrate-venvlauncher-build-installation-into.patch \
  0108-configure.ac-set-_WIN32_WINNT-version.patch \
  0109-configure.ac-don-t-check-for-clock_-functions.patch \
  0110-expanduser-normpath-paths-coming-from-env-vars.patch \
  0111-Add-support-for-Windows-7-Python-3.10.patch \
  0112-CI-test-the-build-and-add-some-mingw-specific-tests.patch \
  0113-smoketests-test-that-_decimal-exists.patch \
  0114-Prefer-sysconfig.python_build.patch \
  0115-Define-PY3_DLLNAME-to-fix-build.patch \
  0116-distutils-remove-checks-for-ancient-gcc-binutils.patch \
  0117-distutils-split-CC-env-var-before-passing-to-subproc.patch \
  0118-_testconsole.c-Fix-casing-path-sep.patch \
  0119-Return-consistent-architecture-markers-for-python-on.patch \
  0120-distutils-add-back-gcc_version.patch \
  0121-fix-mingw-cross-compiling-in-setup.py.patch \
  0122-Use-actions-setup-python-for-setting-up-correct-vers.patch \
  0123-handle-ncursesw-pkg-config-when-cross-compiling.patch \
  0124-CI-add-cross-llvm-mingw-jobs.patch \
  0125-Skip-building-c-extensions-if-python-is-running-in-b.patch \
  0126-mingw_smoketests-fix-_UCRT-condition.patch \
  0127-CI-fix-sed-pattern-for-python-config.py-shebang.patch \
  0128-Modify-sys.winver-to-match-upstream.patch \
  0129-Change-user-site-packages-path-to-include-the-enviro.patch \
  0130-Build-venvlauncher.exe-from-PC-launcher.c.patch \
  0131-PC-launcher.c-Use-function-available-in-original-msv.patch \
  0132-Add-a-test-for-checking-the-new-launchers.patch \
  0133-Revert-Add-a-test-for-checking-the-new-launchers.patch \
  0134-Revert-PC-launcher.c-Use-function-available-in-origi.patch \
  0135-Revert-Build-venvlauncher.exe-from-PC-launcher.c.patch \
  0136-configure-Include-a-header-in-the-check-for-_beginth.patch \
  0137-configure.ac-Default-to-without-c-locale-coercion-on.patch \
  0138-CI-remove-without-c-locale-coercion.patch \
  0139-Commit-regenerated-importlib.patch \
  0140-fixup-cygpty-isatty.patch
 
  autoreconf -vfi
}

build() {
  declare -a _extra_config
  if check_option "debug" "n"; then
    _extra_config+=("--enable-optimizations")
    # FIXME: https://github.com/msys2-contrib/cpython-mingw/issues/10
    # _extra_config+=("--with-lto")
  else
    CFLAGS+=" -O0 -ggdb"
    CXXFLAGS+=" -O0 -ggdb"
    _extra_config+=("--with-pydebug")
  fi

  [[ -d "${srcdir}/build-${CARCH}" ]] && rm -rf "${srcdir}/build-${CARCH}"
  mkdir -p "${srcdir}/build-${CARCH}" && cd "${srcdir}/build-${CARCH}"

  ../Python-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --host=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --enable-shared \
    --with-system-expat \
    --with-system-ffi \
    --with-system-libmpdec \
    --without-ensurepip \
    --enable-loadable-sqlite-extensions \
    --with-tzpath=${MINGW_PREFIX}/share/zoneinfo \
    "${_extra_config[@]}"

  make
}

check() {
  cd "${srcdir}/build-${CARCH}"

  # Some basic tests to ensure nothing major or MSYS2 specific features are broken
  ./python.exe "../Python-${pkgver}/mingw_smoketests.py"
  MSYSTEM= ./python.exe "../Python-${pkgver}/mingw_smoketests.py"
}

package() {
  cd "${srcdir}/build-${CARCH}"

  make -j1 install DESTDIR="${pkgdir}"

  # gdb pretty printers for debugging Python itself; to use:
  # python
  # sys.path.append('C:/msys64/mingw64/share/gdb/python3')
  # import python_gdb
  # reload(python_gdb)
  # end
  if [[ "${_primary_python}" == "yes" ]]; then
    [[ -d "${pkgdir}${MINGW_PREFIX}"/share/gdb/python3/ ]] || mkdir -p "${pkgdir}${MINGW_PREFIX}"/share/gdb/python3/
    cp -f python.exe-gdb.py "${pkgdir}${MINGW_PREFIX}"/share/gdb/python3/python_gdb.py
  fi

  # some useful "stuff"
  install -dm755 "${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/Tools/{i18n,scripts}
  install -m755 "${srcdir}/Python-${pkgver}"/Tools/i18n/{msgfmt,pygettext}.py "${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/Tools/i18n/
  install -m755 "${srcdir}/Python-${pkgver}"/Tools/scripts/{README,*py} "${pkgdir}${MINGW_PREFIX}"/lib/python${_pybasever}/Tools/scripts/

  # fixup shebangs
  for fscripts in 2to3 2to3-${_pybasever} idle3 idle${_pybasever} pydoc3 pydoc${_pybasever}; do
    sed -i "s|$(cygpath -w ${MINGW_PREFIX} | sed 's|\\|\\\\|g')/bin/python${_pybasever}.exe|/usr/bin/env python${_pybasever}.exe|g" \
      "${pkgdir}${MINGW_PREFIX}"/bin/${fscripts}
  done

  # default aliases for all scripts/binaries
  if [[ "${_primary_python}" == "yes" ]]; then
    # Default names are aliases for Python now
    cp "${pkgdir}${MINGW_PREFIX}"/bin/python3.exe "${pkgdir}${MINGW_PREFIX}"/bin/python.exe
    cp "${pkgdir}${MINGW_PREFIX}"/bin/python3w.exe "${pkgdir}${MINGW_PREFIX}"/bin/pythonw.exe
    cp "${pkgdir}${MINGW_PREFIX}"/bin/python3-config "${pkgdir}${MINGW_PREFIX}"/bin/python-config
    cp "${pkgdir}${MINGW_PREFIX}"/bin/idle3 "${pkgdir}${MINGW_PREFIX}"/bin/idle
    cp "${pkgdir}${MINGW_PREFIX}"/bin/pydoc3 "${pkgdir}${MINGW_PREFIX}"/bin/pydoc
  fi

  # delete anything that could conflict with the primary Python
  if [[ "${_primary_python}" != "yes" ]]; then
    rm "${pkgdir}${MINGW_PREFIX}"/bin/python3.exe
    rm "${pkgdir}${MINGW_PREFIX}"/bin/python3w.exe
    rm "${pkgdir}${MINGW_PREFIX}"/bin/python3-config
    rm "${pkgdir}${MINGW_PREFIX}"/bin/idle3
    rm "${pkgdir}${MINGW_PREFIX}"/bin/pydoc3
    rm "${pkgdir}${MINGW_PREFIX}"/bin/2to3
    rm "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/python3-embed.pc
    rm "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/python3.pc
    rm "${pkgdir}${MINGW_PREFIX}"/share/man/man1/python3.1
  fi
}

sha256sums=('80bf925f571da436b35210886cf79f6eb5fa5d6c571316b73568343451f77a19'
            'ff4f499fadf9c10a48a8aff9ada282b41a36ba3a6ad5acd8b8aea9dd82c5b365'
            '29483d5272cc44e2fc536a9e39a61a5aed0830f7ef6ed8769d0213f53673bfce'
            '47947bf433ac7658a3791b0dcd950f0b83433323687ccc8fa4fed914ff02e67f'
            '10b6aebefcfe1fe4300dcaa10668a9d49f46345617160812e71bf6f55f4be004'
            '15691a7a57818b0dd94aa6e81a354f225413c4427206929889440e19e0d33f94'
            'd02da43095c1f51370758b4368f9cd735b0eb63c3c8c9dcc07aaa36aaca11413'
            '1c1abc6cfaa6b378aa3cb97200f9698474a340ef1eab6e78835224113e2cc5c9'
            '34b8076b3f937045a952af72cb1f9287b2b1913ef4e6be72ab41f1c34f50830e'
            'fb2a034e0830ee62e2c61dcd8d67ff956d6f4c769436d94f564134ba8a813359'
            '249f555f8c3f2c25183a4f3c83103ef4faabaeeb97d7ea934ae099d9fba2a2f5'
            'caa88ad5c1d269fbdb3c5203b240f7ccc6f279b077b8f6d2b32d0ea1f83af746'
            '38d6a61e18f776c00e2256dfdb5d6fa5026ffa72e667da5b338bbb01361f5d7a'
            'f897b42e4748b40c5ef54b01f83d6d7b4fd3158de0ca8823da5cc5ed73c42e22'
            'dcf7c12f9e09ab9d2f6200bb0f7c8032c3d9325a09ff1d9d56adc7a041d27d23'
            'c0be38ad7eab5fecde9be95bf343142a62750d35417dd79b486acb0d9f57454e'
            '35bfc9b14fe24e28a50c7f35f0a04b13f79a5e387327ac65c5a1eb8b74d68540'
            'de2e63489547b6189759e796a11d563f5aad39ab18900c4572d0eb6203ab7922'
            '21492c89266a9c4b9859632c8c13c75f31a9f6f452d463d1f55f383e9d60dd76'
            '80ddd2e68b43615ed9595268b607ecee49920628cf78ca2b433820fcbf3d29d4'
            'f176be7d56b699c98d76741db2cd82ee1cfd590f1889254ad5b0d32371d45fd2'
            '6449f34dae15679add75a5d5052e27ecb52f86c59649be4361f5fc865bbdb566'
            '197b562c3580439c2567045592844d1ab48d3cc937fb4b92af9332233349da40'
            '1af3d23600627b7cbdfbe7d672ded33cbe3193d30b0b3748ae251b653a9c04ab'
            'a0d8556b5c69ff3c67bd164b81f11e45a2b079a13cce1582331e8f612cb1f79d'
            'bd1a7094ca91e624586fc0b636bede89f581dfbe1017a0ceec2129ff8746f61f'
            'bbb39b5b03bf299f47d13a9f11391ad0d8c416f3caaacfa71552d5a92438b99d'
            'b300d31d0bf51e84f2918613288875e5b5cee93f7f7cba9ac20d7237cef257e4'
            '1c97d4c136a6bd5af4c3b9a363ae7999cff6eccd660c2291bbb2cda42719685b'
            'bd58ada35e47dc06bee91d56dee3d679e61cf0648812f23167d4c2b6ec65e8ed'
            '0245ca1d3c40e7f7d24dee52ee4cde8da4f8542cc37854b4e0994928bf48dcdc'
            '3f611118dc78e01b7fb134eb138b9a6580cacbf2878fc8fa83af5c7f5fa08bc3'
            '3ece494d66e1d1e18811abc9af2d583600734af30c714a46a1c44aca016c0a68'
            '60d91c9c0ca7cfb78c5f2383c345da7e07ddb5325c02ac5402b51d2e89887ecb'
            'c5cd3a87b4467bfd8ccc21706fd426cb63e726e738e31e0f760bfd1681729124'
            '9b983677f121532972cc4e1960e603e042b889ca64cc2492f9f515561aea37a9'
            '9021a55eea1bce8139975d4c85579687866f605fa897489a024699ec77c9cde3'
            'f53305ba7f83af6864b31bd94aa0d82f21eafa53ed65bb583bbf3ed7e1e0bc17'
            'e44a95c33419af109d4e8160f5663fa60b8164c0872116a1ce1534d17d3e5460'
            'dfd1fed3472b799ccaae1ad46cb13eb4ffadd7e9dff5baeef1748dc450c2951e'
            '7c06809bca40c379dcd1733c2d40788a27493b99a3e7614136052061f30354bb'
            'cfff122b8324cfa60668327e6b7617e3e951c2ffc241d8072ef5c280488705cc'
            'b39ce03d493a3ae1c72ec1970c17579417bace48dcbae174eec4600c324992ff'
            '79597a9c27bf5cb801bf0ec2be844f42ebe6a7c6f1eb07bca69848a9d599f49b'
            '2ded566736be68a0f9544730ec70ffab93ad0444f3699e840a4be7d381a7ad7b'
            '5d13efbf0b2a9e625caf626ca087ab7f5073ab2f14a061853c2f0c2a401a7a7c'
            'a0ac66c286bf41f1603838ba23001f37ea1b9c4d48a71c6c9b7df0c4f6a118dd'
            'c2edf9ea47ea8b5525e2471c18b6b6abdf60751750362c05f6ed1183debd95e2'
            'b27fcbedcab3479a3bdf9122690a569f5cd076c3236ecd63fef589dd99f2d841'
            'd3ffb0ffb40fea212c0769a4c42220b4b4f142374e5d7508c1e9233b73eea122'
            '174790c8e617037282434e4aefe37e205225ce281d650bb18f7b8232a3707659'
            '5fda49cd8cd36f8696923b2d1d85e7e9a519b8214954df22664be94293c6d213'
            '3c1b06dcca4a28a0ef2d98fe65bdaee7f160a2d47d75457e8ed6342e5ff0431d'
            '027f9f99963d48da7734d4a6401043aef358501d99fc1f2c5c09bbf5903d7a08'
            '008435fc3c42f59cd4a8b285e118371bc25f0c431303c15a1c9515fae8c932d8'
            '3829908ad227f6b0d36051eae86a464ac908e817911a49a34b4aac56c86d5001'
            '053082a9e52676b4f2e178fcd30ee9b97acf746f7d1cf2a145648b9d7a60f3cf'
            'd5ed6c11195b3dbce7c6513490ff00851260f11a50a2045489c4244f7a56a937'
            '23aecb336b6df23a1b6d83bf878eb9e388f090db6ce1c16aa773f792b27ae465'
            '0d643225b89e7a24fffdb9d5369283afe0fe777b99470b6d81caa6261cc8d174'
            'cc22a3bf744ed6e6303cec9bc5a017736c99f763419f1f61ca98469ec21c05cd'
            '743fe36624be9ae549201111aa2e39df8e0326aabc70334dfe8d6cd94ceb18a2'
            'c0b1ebe90a39b58190785ba0ff6c7508bb0b6bea14c347288263875ba9512e46'
            'c76ddf3198d0bd3deacfdec698ff5f129c8eced2f4999f4f02641cde2b36ce8c'
            '579306f0d49846582f91596340ac5463b90105a5c3eea0625923a3923f90bec0'
            '39817b5e1d02098221e95c31c84483304f316967127144a540c7bfee9931fb18'
            'bb66ca1e10e9214b7b246d8ba0a02ff191c7fbf4efd72b1c9215f55f9b6d6dc0'
            '31eb1938052183056b2b8fea2ad32c31a57bc251ce424c2450a62a86f7c87612'
            '3d44d6cb69100ad9209dff7c97ca5bca8d1d9cb0cffbf0b47ff779b446842c52'
            '8a55e4afe016620ad18f4340eb82f6b47315724018b77e12310118771b6c5ce6'
            '637ce063ffdfce8046f9058a5f741f94e1e8c31d2cf8b3478750654d0914cfc2'
            '14d3526bb0cd8c53d40e355ec2c45901b5016aa90a5c94be4453540c18ad857f'
            '0007529bfbe30c55684208a3f5865ead3006f2be823896d5b438cd64ad888891'
            '761843fa9ac579edc77cac53fe5c5314d79e6c0907aaaf0064a4d46cbd0d4b81'
            '3a0b99ee1dd16ffc9c89164b6ba7d29c0a8d8d0cccaa82a8befcda55ef7d8c5c'
            '90b660cb00a1f533329848fe3b0d16f73484ee8d3f249739b1ec9c14e22b8922'
            'ba24199f495c87aac8a79634a4d0968889118c7c93ce865187ee0472c365f69d'
            '8be94a76a8d816dd0f67cab9686a02da063fd77dd68f9a1b7328f0a56bb10b3e'
            'e333b84a0bf57274cfd7a415fe15f85665ee238a87135bad010e8a55e3301a3b'
            'a53ff1594ee2836941257cd2ed74e1f506979ced14ce513ce6c35817761998cf'
            'da5e19fbe4bb56459d1a5669427226502dbb335504a9bed96bb65ae58aed510e'
            'fd2441c7422b7ff5fe23c6d1ed7c5cc3a1b34a7f52a50620adb0515bb2d1d135'
            '64c9816ef45879c84a5ce6ae3c4dd4b26e440da08e377febb3090d0f7efa62d2'
            '6fa3a1cd84feeab3830fce3b921b698943fafa56e04ebd4a109eb46c2a934928'
            'cfb9260e8c7c53bca366e739789e8e1d4c592d9aa7fa57b313145bd0e4867f15'
            '9975e751ee718747ab53d9c6cbe683dfc3b251e1c6fea7df70e9fadc2ce16374'
            '1e4eb47629956c9465547bb5b4159bd509f4955d79938c551ad6a5be5fa1a258'
            '619eda3823c84622169a67e170bc4b0d6e546c973a24da4501258e1f1cffbab0'
            '31a949e9feec327e82521229667016917b83e0e81c13795712c8187af7b9bd65'
            '75d2c21a01764cd861bc9c7477d23b52d9b4f9579a0fb4994aa6b61b19e3823c'
            'e60664fbac431dbadedf1eb553f8c3520e13cc79daeed4db976d67196a1fa7fe'
            'dc21ac2ac3d2af71095d8f6561661b675851965f9514ad183bcb79e9f034d912'
            'a809907d25a9ad0d1a20f2aab01c3fdbd02154fc7667d0822d467f766ed1c0d5'
            'db5c5883c18fe1a5ed687ab2fb671525c072cca60d136ce884b3c9c33e76dc86'
            'a98018b7603be9d0cda360107e559a4268bfd3fa0f5bad925cfe71aa062ebfe3'
            '5ec10a4cee32fe6b00db31bdef2a9def55bb049c15de3a23153b7eb7c7459a5e'
            'ece9f066e1b62ec7e444df3f37eb9f300d4ee437003abab959dfc7eb41803b46'
            'cfd760a29388b5c1491d58d18c934635f40c95689805d30e616a3d76295a5c3e'
            'f660c28497b601afdbe1e23e68107ac22c8e4bcdc5891e1f88b22bafcfd8cbb5'
            'f94ab8e0a7181cdaf61735e246c798dc7a2d7226a98547198950f5075007cb1a'
            '294d3550073a443c9c2cfd8ee10f5a37353ad5ad038d8455065cd30411bba0e6'
            '362939fb8c5266432ac87a0cdfd995b06328bc8cc2a20cf7de03ac8696d9fdb8'
            'c572c37a0d10d20a267ed0f9d8ef16051bda01c48b5e624852fcdf30c21993f1'
            '1a555795144b044c18a8ff0b4cb7f2a5bb67da9d9b67403e0255747de0d545c1'
            'fbe58c2ef62acc1db597ccf9d73fb99fa6b5a3c7562d90de22a3a41b93c01e33'
            'f04df7b7151c6784baa2b0e5ddccc0ac796399c3757a9ca312620d1a8ba6d0f3'
            '515f1e9c6229c8d83c2a2299377a027538bef4293432e74a345e4c3378b1ee65'
            'd04a59a9942bac19b08b5b54a5cabdfd18a02cb78a48c9767dbc13e94a5b6941'
            'f67e368ffc870a5d45aeab9477578ad33fe49905ffe1045c3faa21850e0332ad'
            '3921153255f3c1559fe4157e33a4fa28b5bd458f6f868e1d368d68f2be0cafab'
            'eecfbb0604b0021161240fcab7c295a5c20caffeb940c70d2ca65a8e816a3fc5'
            '8f78acf4d5435a175525c26018743d38a7d2a833c3dd1daa2a5c8b8bacd8cdf1'
            '4f0a726e8feb077764f66e283a8a06604c6e57a3d9e9c6fca75c7842b8da5ac2'
            '43a31294d982c33aa9be0e24cadfb4cef05a5fd43475a9c20234a3d226e84121'
            'e2bcd929e10eb2721d9cac4922b2266b23cf63135d8ff5ddd89c5b13b9b030b1'
            '5db116ed5ef23c901f0ecc638a5d500e47d19de8b603b040b60ab0dcb05b1046'
            'ecfa351a49a91a8633dc5f8ca0fd0fe16a65846a681ecc6f1c75ecdd78696106'
            'f7705fabda334c3faf3174a7927f19ff0171b5af388e3593a6260184a504eb32'
            '98068d2f3e447c4575f0c1d0bca3156edcefb9ffeeaa0aad0614f510d1977a49'
            '6090eba08e12a27499e10c8c01c5ed2adf2212df6b38078857959c71ae417f33'
            '7368f70b5c180bac37665e7ddc6170faa042bef121d7265b3453640dc5a9f7b5'
            'af5b9d3b68132b622798ee0f14a34579ee40218eae5fa292f6faf41bafed8b55'
            '36ec1645b92005e6a2b63fd29a41650b2ce4ff36c538257342e917d5f1c6c627'
            'eb16c64c5dff3d536537782c88fb06bcff8f61d8b24de8ac9cb05573027150fc'
            '60a70599a6a3c0e9bfc31efcdcc7319fd660ee733194fa7a5dd6a808956d2743'
            'ef63f00ae51a37c9ef109932a3f5bb8c9a1f0fd371d614237a0eb76d75e12342'
            '5060cc67a12228a0706c818bbea96e0d8093457d607fc782760f6aa7f825f195'
            '7151bdf0f0d8358d80ffdd47c3c42e763271c837fcb19010d67fb2dad3bca53b'
            'b7f809ca5fe43c5f73816e9bd0810bbd568f3cbbb162385d1cbf80dd9a9bc42e'
            'f5ef673a595361590ae53d3336d000018d1fb67b0350232593fedadca04549d1'
            '1903513a2ed394c13b613db9a403544bda66cceba744fa9584e2c678ba0e18a5'
            'd6832736da5652f509134fb23650958cf762e19644611e51bf999430c478fd19'
            'e5ace2eefaf1bd25aed596a960c89e76e666a58a734d5d6456e332c9469b256f'
            'aa3ab009f069babf2cbee3ac41952a7a7730232ab8ded5b71f28ae4b2b2101a9'
            '63971614165530904a4a0ef82f17c74a86b6a9e2b269ce31ce9f993f02a2428a'
            'c04565cf2b9689e3e598f8885ea3ce3d7de1c1ebc89964633067ae368a3303bc'
            '2b211ca1d0f79183d22d38826671f5971b5977d1b2be47305ab1548dfeed7515'
            'bcaf2966c14c1a3d042718704c278b7c5960656bce7d6203a8b31a2ce869aa4b'
            'f9667bb9d17607b4e206ccf91ae08c430ba166d2c0e2eb85c5c8c8973fe81255'
            'fd7d8039527d42c3c4bc28b50d31ebc17eed9436043579ce4466e8f1219e6a1f'
            '93886b7636000b37c0e5a1c495f7ccac9c1988464b370ca2df3c3009388a43f1')
