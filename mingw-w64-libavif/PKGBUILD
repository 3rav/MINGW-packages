# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libavif
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.1.0
pkgrel=2
pkgdesc="Library for encoding and decoding .avif files (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://github.com/AOMediaCodec/libavif"
license=('spdx:BSD-2-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-aom"
         "${MINGW_PACKAGE_PREFIX}-dav1d"
         "${MINGW_PACKAGE_PREFIX}-rav1e"
         $( [[ ${CARCH} == i686 ]] || \
            echo "${MINGW_PACKAGE_PREFIX}-svt-av1" )
         "${MINGW_PACKAGE_PREFIX}-libyuv"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libsharpyuv"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-cc")
checkdepends=("${MINGW_PACKAGE_PREFIX}-gtest")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/AOMediaCodec/libavif/archive/v${pkgver}.tar.gz"
        https://github.com/AOMediaCodec/libavif/commit/53fe96de1b7707fe830d213828cfaebd71793a8a.patch)
sha512sums=('b1a7d69ef4019ff99a04396f4b0912c359e4dc50b292607ad88c7bf3035750b9b6fb52463fe210e9a476bd88de8920a2b16d405c08ce2c489fbbf6a79bc3b7f6'
            '4aeb35b1dfcfda54f2c77806cae325ed6c158203c83fe985ce135b7521530e0ab8af949398ec950777e460452c2b4398eefde16d66411c9d108cf8d8e75e9eb5')

prepare() {
  cd "${_realname}-${pkgver}"

  # Revert "Put AVIF_NODISCARD after AVIF_API"
  patch -p1 -i "${srcdir}/53fe96de1b7707fe830d213828cfaebd71793a8a.patch"
}

build() {
  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G Ninja \
    -DBUILD_SHARED_LIBS=OFF \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    "${extra_config[@]}" \
    -DAVIF_BUILD_APPS=SYSTEM \
    -DAVIF_CODEC_AOM=SYSTEM \
    -DAVIF_CODEC_DAV1D=SYSTEM \
    -DAVIF_CODEC_RAV1E=SYSTEM \
    -DAVIF_CODEC_SVT=$( [[ ${CARCH} == i686 ]] &&
                        echo "OFF" || echo "SYSTEM" ) \
    -DAVIF_BUILD_GDK_PIXBUF=ON \
    -DAVIF_LIBSHARPYUV=SYSTEM \
    -S "${_realname}-${pkgver}" \
    -B "build-${MSYSTEM}-static"

  ${MINGW_PREFIX}/bin/cmake --build "build-${MSYSTEM}-static"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G Ninja \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_DLL_NAME_WITH_SOVERSION=ON \
    "${extra_config[@]}" \
    -DAVIF_BUILD_APPS=ON \
    -DAVIF_CODEC_AOM=SYSTEM \
    -DAVIF_CODEC_DAV1D=SYSTEM \
    -DAVIF_CODEC_RAV1E=SYSTEM \
    -DAVIF_CODEC_SVT=$( [[ ${CARCH} == i686 ]] &&
                        echo "OFF" || echo "SYSTEM" ) \
    -DAVIF_BUILD_GDK_PIXBUF=ON \
    -DAVIF_LIBSHARPYUV=SYSTEM \
    -S "${_realname}-${pkgver}" \
    -B "build-${MSYSTEM}-shared"

  ${MINGW_PREFIX}/bin/cmake --build "build-${MSYSTEM}-shared"
}

check() {
  cd ${srcdir}/build-${MSYSTEM}-shared
  ${MINGW_PREFIX}/bin/cmake -DAVIF_BUILD_TESTS=ON ../${_realname}-${pkgver}
  ${MINGW_PREFIX}/bin/cmake --build .
  ${MINGW_PREFIX}/bin/ctest ./ || true
}

package() {
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install "build-${MSYSTEM}-static"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install "build-${MSYSTEM}-shared"

  install -Dm644 ${srcdir}/${_realname}-${pkgver}/README.md ${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}/README.md
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
