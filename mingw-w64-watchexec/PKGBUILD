_realname=watchexec
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.20.6
pkgrel=1
url="https://watchexec.github.io/"
pkgdesc="Executes commands in response to file modifications"
license=('Apache-2.0')
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
makedepends=("${MINGW_PACKAGE_PREFIX}-rust")
source=("https://github.com/watchexec/watchexec/archive/v${pkgver}.tar.gz")
sha256sums=('0e296689d3c4a4df64b036ca4047056401c137ee895b3fd1cb63375c3d2388d4')
options=('strip')

prepare() {
  cd "${_realname}-${pkgver}"

  cargo fetch --locked
}

build() {
  cd "${_realname}-${pkgver}"

  cargo build --release --frozen
}

check() {
  cd "${_realname}-${pkgver}"

  cargo test --release --frozen
}

package() {
  cd "${_realname}-${pkgver}"

  install -Dm755 target/release/watchexec "$pkgdir"$MINGW_PREFIX/bin/watchexec
  install -Dm644 completions/zsh "$pkgdir"$MINGW_PREFIX/share/zsh/site-functions/_watchexec
  install -Dm644 doc/watchexec.1 "$pkgdir"$MINGW_PREFIX/share/man/man1/watchexec.1
  install -Dm644 doc/watchexec.1.html "$pkgdir"$MINGW_PREFIX/share/doc/watchexec/watchexec.1.html
  install -Dm644 doc/logo.svg "$pkgdir"$MINGW_PREFIX/share/icons/hicolor/scalable/apps/watchexec.svg
  install -Dm644 LICENSE "$pkgdir"$MINGW_PREFIX/share/licenses/watchexec/LICENSE
  install -Dm644 README.md "$pkgdir"$MINGW_PREFIX/share/doc/watchexec/README.md
}
