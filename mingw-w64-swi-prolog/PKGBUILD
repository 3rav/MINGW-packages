# Maintainer: Matthias Gondan <Matthias.Gondan-Rochon@uibk.ac.at>

_realname=swi-prolog
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=8.4.3
pkgrel=1
pkgdesc="Prolog environment (mingw-w64)"
arch=(any)
mingw_arch=('mingw64' 'mingw32' 'ucrt64' 'clang64' 'clang32')
url="https://www.swi-prolog.org/"
license=("FreeBSD")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread-git"
         "${MINGW_PACKAGE_PREFIX}-zlib")
optdepends=("${MINGW_PACKAGE_PREFIX}-qt6-base: for swipl-win")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-gmp"
             "${MINGW_PACKAGE_PREFIX}-libarchive"
             "${MINGW_PACKAGE_PREFIX}-libyaml"
             "${MINGW_PACKAGE_PREFIX}-db"
             "${MINGW_PACKAGE_PREFIX}-pcre"
             "${MINGW_PACKAGE_PREFIX}-qt6-base"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
source=("https://www.swi-prolog.org/download/stable/src/swipl-${pkgver}.tar.gz"
        "01-skip-installation-of-system-dlls.patch"
        "02-stack-size-for-clang.patch"
        "03-unix-style-directory-structure.patch"
        "04-boot-build-home.patch"
        "05-swipl-win.patch")
sha256sums=('946119a0b5f5c8f410ea21fbf6281e917e61ef35ac0aabbdd24e787470d06faa'
            '604ea58fa5684290ea568caa7a9b3e235e58578263449e2d6e629c2854ea193d'
            'f0e1133921b120c7105b6a345fcdeeb86aa6167410efdf3dd8928a7d31584e8a'
            '5f19258b6a56c1a5e1d4708b46d75f31885c01efd25205a9dabdb90979152c75'
            'e903c39e5b5c5ad4c62bb17323b5b9b54f1f6aa1e5f87e8e95e8d1aeb7b5a72e'
            'bee692b2b61762a078862d49fb5f2ab9dd46b77626fc37c9dc921d681c3c857f')

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Np1 -i "${srcdir}/$_patch"
  done
}

prepare() {
  cd ${srcdir}/swipl-${pkgver}
  apply_patch_with_msg \
    01-skip-installation-of-system-dlls.patch \
    02-stack-size-for-clang.patch \
    03-unix-style-directory-structure.patch \
    04-boot-build-home.patch \
    05-swipl-win.patch
}

build() {
  [[ -d "${srcdir}/build-${MSYSTEM}" ]] && rm -rf "${srcdir}/build-${MSYSTEM}"
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
    -G Ninja \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DMSYS2=1 \
    -DMINGW_ROOT=${MINGW_PREFIX} \
    "${extra_config[@]}" \
    -DWIN32_DLLS="" \
    -DSWIPL_PACKAGES_X=OFF \
    -DSWIPL_PACKAGES_JAVA=OFF \
    -DINSTALL_DOCUMENTATION=OFF \
    -DSWIPL_{SHARED,STATIC}_LIB=ON \
    ../swipl-${pkgver}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
  install -Dm644 "${srcdir}/swipl-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}

# check() {
#   cd "${srcdir}/build-${MSYSTEM}"
# 
#   "${MINGW_PREFIX}"/bin/cmake.exe --build . --target test
# }

