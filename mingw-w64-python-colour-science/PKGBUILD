# Contributor: Brien Dieterle <briend@gmail.com>
# Contributor: Miloš Komarčević <miloskomarcevic@aim.com>

_realname=colour-science
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.4.0
pkgrel=1
pkgdesc="Python library for a multitude of colour science applications (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
url='https://www.${_realname}.org'
license=('BSD-3-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-python-imageio"
         "${MINGW_PACKAGE_PREFIX}-python-numpy"
         "${MINGW_PACKAGE_PREFIX}-python-scipy"
         "${MINGW_PACKAGE_PREFIX}-python-typing_extensions")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-build"
             "${MINGW_PACKAGE_PREFIX}-python-installer"
             "${MINGW_PACKAGE_PREFIX}-python-poetry-core")
optdepends=("${MINGW_PACKAGE_PREFIX}-python-matplotlib"
            "${MINGW_PACKAGE_PREFIX}-python-networkx"
            "${MINGW_PACKAGE_PREFIX}-python-pandas"
            "${MINGW_PACKAGE_PREFIX}-python-tqdm"
            "${MINGW_PACKAGE_PREFIX}-python-trimesh")
conflicts=("${MINGW_PACKAGE_PREFIX}-python-${_realname//-science/}")
source=("${_realname//-science/}-${pkgver}.tar.gz"::"https://github.com/${_realname}/${_realname//-science/}/archive/v${pkgver}.tar.gz"
        "0001-poetry-core-build.patch"::"https://github.com/${_realname}/${_realname//-science/}/commit/1b5bd5ead7065d003b7aaba9bc35d23b79d9ea5c.diff")
sha256sums=('fdd4dc66306f2930c542102de35236e9080e1b0c5dfc525ebed25e5e3919dc5a'
            'e82b1118536e0feef4e81532efe70c0200ada23e3874c84a9ffff57b3e51bf17')

prepare() {
  cd "${srcdir}/${_realname//-science/}-${pkgver}"

  patch -Np1 -i "${srcdir}/0001-poetry-core-build.patch"
}

build() {
  cd "${srcdir}"
  cp -r "${_realname//-science/}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  ${MINGW_PREFIX}/bin/python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    ${MINGW_PREFIX}/bin/python -m installer --prefix=${MINGW_PREFIX} \
    --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}
