# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=pivy
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python-${_realname}")
pkgver=0.6.8
pkgrel=4
pkgdesc='Python bindings to Coin3D (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clangarm64' 'clang32')
url='https://github.com/coin3d/pivy'
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-soqt-qt5")
makedepends=("${MINGW_PACKAGE_PREFIX}-python-setuptools"
             "${MINGW_PACKAGE_PREFIX}-swig"
             "${MINGW_PACKAGE_PREFIX}-cc"
             #"${MINGW_PACKAGE_PREFIX}-glu"
             "${MINGW_PACKAGE_PREFIX}-cmake")
source=("${_realname}-${pkgver}.tar.gz"::"${url}/archive/${pkgver}.tar.gz"
        "001-pivy-0.6.5-setup-py-msystem-env.patch"
        "002-pivy-0.6.8-fake-header-cmath.patch")
sha256sums=('c443dd7dd724b0bfa06427478b9d24d31e0c3b5138ac5741a2917a443b28f346'
            '0a37cf9f25ed3b0aca4547b484f448c840cf6a4d1f604ceed7094f8970a9c5b5'
            'b0132c7cf459c15ab8d35dda1e418140932968b351b325fcccb22e8c05362147')

prepare() {
  cd "${srcdir}"
  rm -rf "python-build-${MSYSTEM}" | true
  cp -r "${_realname}-${pkgver}" "python-build-${MSYSTEM}" && cd "python-build-${MSYSTEM}"

  patch -p1 -i "${srcdir}/001-pivy-0.6.5-setup-py-msystem-env.patch"
  patch -p1 -i "${srcdir}/002-pivy-0.6.8-fake-header-cmath.patch"
}

build() {
  msg "Python build for ${MSYSTEM}"
  cd "${srcdir}/python-build-${MSYSTEM}"

  if [[ "${MSYSTEM}" == "MINGW32" ]]; then
    # Works around gcc OOM on 32bit somehow
    CFLAGS+=" -Os -flarge-source-files"
  fi

  GENERATOR="Ninja" \
  ${MINGW_PREFIX}/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}" -O1 --skip-build
  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python-${_realname}/LICENSE"
}

# vim: ts=2 sw=2 et:
