# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libpqxx
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=7.9.0
pkgrel=1
pkgdesc="C++ client API for PostgreSQL (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
license=("custom")
url="http://pqxx.org/development/libpqxx/"
source=("https://github.com/jtv/libpqxx/archive/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('a1fafd5f6455f6c66241fca1f35f5cb603251580b99f9a0cf1b5d0a586006f16')
depends=("${MINGW_PACKAGE_PREFIX}-postgresql>=9.1")
makedepends=("${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-cmake")
options=('staticlibs' 'strip')

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    ${extra_config[@]} \
    -DBUILD_DOC=OFF \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_TESTING=OFF \
    ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .
  
  install -Dm 644 "${srcdir}"/${_realname}-${pkgver}/COPYING -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
  install -Dm 644 "${srcdir}"/${_realname}-${pkgver}/README.md -t "${pkgdir}${MINGW_PREFIX}/share/doc/${_realname}"
}
