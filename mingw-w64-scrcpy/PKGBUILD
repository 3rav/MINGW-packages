# Maintainer: Wu Zhenyu <wuzy01@qq.com>

realname=scrcpy
pkgbase=mingw-w64-$realname
pkgname=$MINGW_PACKAGE_PREFIX-$realname
pkgver=1.23
pkgrel=1
pkgdesc='Display and control your Android device (mingw-w64)'
arch=(any)
mingw_arch=(mingw32 mingw64 ucrt64 clang64 clang32 clangarm64)
url='https://github.com/Genymobile/scrcpy'
license=(Apache)
depends=($MINGW_PACKAGE_PREFIX-ffmpeg $MINGW_PACKAGE_PREFIX-SDL2
        $MINGW_PACKAGE_PREFIX-libusb)
makedepends=($MINGW_PACKAGE_PREFIX-make
        $MINGW_PACKAGE_PREFIX-gcc
        $MINGW_PACKAGE_PREFIX-pkg-config
        $MINGW_PACKAGE_PREFIX-meson)
source=("$realname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$url/releases/download/v$pkgver/$realname-server-v$pkgver")
sha256sums=('47686af76e2314404deda0eea58761ca1378a6a1567408b4560683461c7ea18b'
        '2a913fd47478c0b306fca507cb0beb625e49a19ff9fc7ab904e36ef5b9fe7e68')

build() {
  mkdir -p build
  meson build $realname-$pkgver \
    --buildtype release \
    -D b_lto=true \
    -D b_ndebug=true \
    -D prebuilt_server="../$realname-server-v$pkgver"
  ninja -C build
}

package() {
  DESTDIR=$pkgdir ninja -C build install
  install -Dm644 $realname-$pkgver/LICENSE -t \
    "$pkgdir$MINGW_PREFIX/share/licenses/$realname"
}
