# Contributor: Jason Qiu <jasonwqiu@gmail.com>

_realname=streamlink
pkgbase=mingw-w64-python-${_realname}
pkgver=3.2.0
pkgrel=1
pkgdesc="A CLI utility that extracts streams from various services and pipes them into a video player of choice."
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://github.com/streamlink/streamlink"
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-python"
         "${MINGW_PACKAGE_PREFIX}-python"-{pycryptodome,isodate,pycountry,requests,pysocks,websocket-client,charset-normalizer})
makedepends=("${MINGW_PACKAGE_PREFIX}-python-"{setuptools,wheel})
checkdepends=("${MINGW_PACKAGE_PREFIX}-python-"{freezegun,pytest,requests-mock})
optdepends=("${MINGW_PACKAGE_PREFIX}-ffmpeg: Required to play streams that are made up of separate audio and video streams, eg. YouTube 1080p+")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/streamlink/streamlink/releases/download/${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('9770d2d83844c5378a73e14130dcb760abc856566caa0a41fc5b97a0ded5d926')

prepare() {
    cp -r "${_realname}-${pkgver}" "python-build-${CARCH}"
}

build() {
  cd "${srcdir}/python-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python setup.py build
}

check() {
  cd "${srcdir}/python-build-${CARCH}"
  ${MINGW_PREFIX}/bin/python setup.py pytest
}

package() {
  cd "${srcdir}/python-build-${CARCH}"
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
    ${MINGW_PREFIX}/bin/python setup.py install --prefix="${MINGW_PREFIX}" \
        --root="${pkgdir}" --optimize=1 --skip-build

  for _f in "${pkgdir}${MINGW_PREFIX}"/bin/*-script.py; do
    # Remove shebang line
    sed -e '1 { s/^#!.*$// }' -i "${_f}"
  done

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/python-${_realname}/LICENSE
}
