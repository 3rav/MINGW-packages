# Maintainer: Peter Budai <peterbud@hotmail.com>

_realname=openlibm
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.7.5
pkgrel=3
pkgdesc="High quality system independent, portable, open source libm implementation (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url='https://openlibm.org'
license=('MIT')
options=('strip' 'staticlibs')
source=("${_realname}-${pkgver}.tar.gz::https://github.com/JuliaMath/${_realname}/archive/v${pkgver}.tar.gz"
        01-mingw-build.patch
        02-mingw-clang-symbol.patch
        03-mingw-importlib-install.patch
        04-mingw-aarch64.patch)
sha256sums=('be983b9e1e40e696e8bbb7eb8f6376d3ca0ae675ae6d82936540385b0eeec15b'
            'ea684a10d608ffee487be0876d180c6d0124315302494fd7bc712a35885b925e'
            '15891e30dad6c25e4f6b476be6ec77fab06872349bffb945afc8306a69c8dbf0'
            '93a7417e899ecac7fc3fb69ccf616245679f35e7382139b664758b371beb5f22'
            'dea68006af4e9b84fbe051d7d4ca8cb2c2f60dcc6a774c5196360eb6d3964403')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  # https://github.com/JuliaMath/openlibm/commit/60dec83e3322282eeea283a1950b25fc91d39233 (partial)
  apply_patch_with_msg 01-mingw-build.patch
  # https://github.com/JuliaMath/openlibm/commit/d0ef09a62d726d8a4422c8541ef0d076b26b50b5
  apply_patch_with_msg 02-mingw-clang-symbol.patch
  # https://github.com/JuliaMath/openlibm/commit/a9568fbd227b42baa31ceca550550b7df792a6e8
  apply_patch_with_msg 03-mingw-importlib-install.patch
  # https://github.com/JuliaMath/openlibm/commit/3d4a902f316d20a6c8c26b21fedc79d564e68cab
  apply_patch_with_msg 04-mingw-aarch64.patch
}

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  cp -rf "${srcdir}/${_realname}-${pkgver}" "${srcdir}/build-${CARCH}"

  cd "${srcdir}/build-${CARCH}"
  local _compilersetting="USEGCC=1"
  if [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]]; then
    _compilersetting="USECLANG=1"
  fi

  make prefix=${MINGW_PREFIX} ${_compilersetting}
}

check() {
  cd "${srcdir}"/build-${CARCH}
  make test
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make  prefix=${MINGW_PREFIX}  DESTDIR="${pkgdir}" install

  install -Dm644 ${srcdir}/build-${CARCH}/LICENSE.md ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
