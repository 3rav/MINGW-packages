_realname=epstool
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.09
pkgrel=1
license=("GPL 2")
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
pkgdesc="utility to create or extract preview images in EPS files, fix bounding boxes and convert to bitmaps"
depends=("${MINGW_PACKAGE_PREFIX}-ghostscript")
makedepends=()
url="http://www.ghostgum.com.au/software/epstool.htm"
source=(http://www.ghostgum.com.au/download/${_realname}-${pkgver}.tar.gz
        "0001-mingw-buildrule.patch")
sha256sums=('e3538857e0c8c21a5a6cbd28fba75d71825318daea6525a4d20e385d758ca847'
            'ace0835a4ab156f4ea78f8b9bf76490f2a84889bd790ed46a4bc7d7d0820e8d5')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  # patch from https://hg.octave.org/mxe-octave/file/c30da1cd5e3b/src/mingw-epstool-1-fixes.patch
  rm -rf src/mingwcom.mak && patch -p1 -i "${srcdir}/0001-mingw-buildrule.patch"
}

build() {
  cd ${srcdir}/${_realname}-${pkgver}

  make prefix="${pkgdir}/${MINGW_PREFIX}"
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}

  make prefix="${pkgdir}/${MINGW_PREFIX}" install
}
