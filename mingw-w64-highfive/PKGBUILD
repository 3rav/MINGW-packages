# Maintainer: Václav Šmilauer <eu@doxos.eu>

_realname=highfive
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.3.1
pkgrel=1
pkgdesc='Modern header-only C++11 friendly interface for libhdf5.'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url='https://github.com/BlueBrain/HighFive'
license=('custom')
# besides cmake, build deps are for unit tests (at package build time)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-boost"
  "${MINGW_PACKAGE_PREFIX}-opencv"
  "${MINGW_PACKAGE_PREFIX}-eigen3"
  "${MINGW_PACKAGE_PREFIX}-hdf5"
)
depends=("${MINGW_PACKAGE_PREFIX}-hdf5")
options=('staticlibs' '!strip' '!buildflags')
source=("${_realname}-${pkgver}.tar.gz::https://github.com/BlueBrain/HighFive/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('41728a1204bdfcdcef8cbc3ddffe5d744c5331434ce3dcef35614b831234fcd7')

prepare(){
  mv "${srcdir}/HighFive-${pkgver}" "${_realname}-${pkgver}"
  cd "${_realname}-${pkgver}"
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  # HIGHFIVE_* options only affect tests (at package-build time),
  # not what is installed
  # (xtensor is not packaged, thus OFF)

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DHIGHFIVE_USE_BOOST=ON \
    -DHIGHFIVE_USE_EIGEN=ON \
    -DHIGHFIVE_USE_XTENSOR=OFF \
    -DHIGHFIVE_USE_OPENCV=ON \
    -DHIGHFIVE_UNIT_TESTS=ON \
    -DHIGHFIVE_PARALLEL_HDF5=OFF \
    -DHIGHFIVE_BUILD_DOCS=OFF \
    -DCMAKE_RUNTIME_OUTPUT_DIRECTORY="${srcdir}/build-${MINGW_CHOST}" \
    -S ../${_realname}-${pkgver} -B .

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"

  # run unit tests
  make test
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" -t "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/"
}
