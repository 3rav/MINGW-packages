# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=gettext
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.21
pkgrel=1
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://www.gnu.org/software/gettext/"
pkgdesc="GNU internationalization library (mingw-w64)"
# GPL3 for the package as a whole and LGPL for some parts, see the license files
license=(GPL3 partial:'LGPL2.1')
depends=("${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-libiconv"
         #"${MINGW_PACKAGE_PREFIX}-termcap"
        )
makedepends=("${MINGW_PACKAGE_PREFIX}-ncurses"
             "${MINGW_PACKAGE_PREFIX}-autotools")
options=('strip' 'staticlibs')
source=("https://ftp.gnu.org/pub/gnu/${_realname}/${_realname}-${pkgver}.tar.gz"
        0001-libtool-hide-using-cross-tools-not-prefixed-with-hos.patch
        0002-libtool-hide-library-was-moved.patch
        0003-libtool-hide-Could-not-determine-host-file-name.patch
        0004-libtool-hide-seems-to-be-moved.patch
        0005-Fix-compilation-of-pthread_sigmask.c.patch
        0006-libtool-hide-remember-to-run.patch
        0007-libtool-hide-has-not-been-installed-in.patch
        0008-Force-redirected-output-s-encoding.patch
        0009-Typo-func__fatal_error-should-be-func_fatal_error.patch
        0010-build-Fix-build-failure-on-mingw-formatstring_ruby.patch
        0011-fix-interference-between-libintl-boost-header-files.patch)
sha256sums=('c77d0da3102aec9c07f43671e60611ebff89a996ef159497ce8e59d075786b12'
            'cf88da22e06683433aec46fb799f526a2a760ba088d7180c74bed2f5dc5c03b7'
            '33cc3ec86a0c8eef7769e816f42364e9ee43e4e31d59bdfe05889df2d0ef5852'
            'f752abb114446ff0c9ad5990433614ac6e936597a4660d4cfc3371c5d8fe3032'
            'db1f29cd8917ac0c0af41be3092488fee7d58cb693775b7cba8d18b416b0f6f1'
            'cbc2f533012d646521afa20f8b256917fce040741ff42cf53fb6dd7123a6670a'
            '4931cfccb2d458d1de9e26eadfc98f129f3c3a148a852123215de9cef9d439a5'
            'da1b9e85c368ba01af3412a5a9a9c7e8a66f60b01643b7789e938b1e5c74e636'
            'e75be4d84ce54cd9dc0d2ff738d7e9c77856a593f777fece9dcdd29ab8812b6b'
            'dcafa97dff683e075a9779f1a0a0d684a4a755343f711b11f7096de7f635ba19'
            'c4a85dc6d781d29bdcdc557d8867408f2181d871338f6a1c4b9e6fcd78dc3e7f'
            '41201d74f5f352c4fe219316340521fc3f9a0f3a7c572a3ddd3b8df33d4dab9a')
validpgpkeys=('462225C3B46F34879FC8496CD605848ED7E69871') #Daiki Ueno

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying ${_patch}"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  apply_patch_with_msg \
    0001-libtool-hide-using-cross-tools-not-prefixed-with-hos.patch \
    0002-libtool-hide-library-was-moved.patch \
    0003-libtool-hide-Could-not-determine-host-file-name.patch \
    0004-libtool-hide-seems-to-be-moved.patch \
    0005-Fix-compilation-of-pthread_sigmask.c.patch \
    0006-libtool-hide-remember-to-run.patch \
    0007-libtool-hide-has-not-been-installed-in.patch \
    0008-Force-redirected-output-s-encoding.patch \
    0009-Typo-func__fatal_error-should-be-func_fatal_error.patch \
    0010-build-Fix-build-failure-on-mingw-formatstring_ruby.patch \
    0011-fix-interference-between-libintl-boost-header-files.patch

  libtoolize --automake --copy --force
  WANT_AUTOMAKE=latest ./autogen.sh --skip-gnulib
}

build() {
  [[ -d ${srcdir}/build-${MSYSTEM} ]] && rm -rf ${srcdir}/build-${MSYSTEM}
  mkdir -p ${srcdir}/build-${MSYSTEM} && cd ${srcdir}/build-${MSYSTEM}

  export lt_cv_deplibs_check_method='pass_all'
  export MSYS2_ARG_CONV_EXCL="-DLOCALEDIR=;-DLIBDIR=;-DLOCALE_ALIAS_PATH="

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --disable-java \
    --disable-native-java \
    --disable-csharp \
    --enable-static \
    --enable-threads=win32 \
    --enable-relocatable \
    --without-emacs \
    --disable-openmp \
    --without-cvs \
    --without-git \
    --with-included-libcroco \
    --with-included-libunistring \
    --with-included-libxml \
    --with-included-glib \
    --with-libncurses-prefix=${MINGW_PREFIX} \
    --disable-silent-rules

  # to make relocate() in gnulib-lib work
  sed -s "s|${MINGW_PREFIX}|$(cygpath -m ${MINGW_PREFIX})|g" -i gettext-tools/config.h

  make
}

package() {
  cd ${srcdir}/build-${MSYSTEM}
  make DESTDIR="${pkgdir}" install

  # Licenses
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING"                                 "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/gettext-runtime/COPYING"                 "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/gettext-runtime/COPYING"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/gettext-runtime/intl/COPYING.LIB"        "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/gettext-runtime/intl/COPYING.LIB"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/gettext-runtime/libasprintf/COPYING"     "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/gettext-runtime/libasprintf/COPYING"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/gettext-runtime/libasprintf/COPYING.LIB" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/gettext-runtime/libasprintf/COPYING.LIB"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/gettext-tools/COPYING"                   "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/gettext-tools/COPYING"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/gettext-tools/gnulib-lib/libxml/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/gettext-tools/gnulib-lib/libxml/COPYING"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/gnulib-local/lib/libxml/COPYING"         "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/gnulib-local/lib/libxml/COPYING"
}
